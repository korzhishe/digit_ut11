
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ЗначенияФункциональныхОпций = ЗначенияФункциональныхОпций();
	
	СтрокаБазовая = ?(ЗначенияФункциональныхОпций.БазоваяВерсия, "Базовая", "");
	РасширенныйЗаказ = ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	ЭтоЗаказ = ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЗаказКлиента")
		ИЛИ ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента");
	
	КлючВарианта = ?(НЕ РасширенныйЗаказ И ЭтоЗаказ,
		"СостояниеРасчетовПоОбъектуРасчетовУпрощенныйКонтекст" + СтрокаБазовая,
		"СостояниеРасчетовПоОбъектуРасчетовКонтекст" + СтрокаБазовая);
		
	ОткрытьФорму("Отчет.СостояниеРасчетовСКлиентами.Форма",
		Новый Структура("КлючНазначенияИспользования, Отбор, КлючВарианта, СформироватьПриОткрытии, ВидимостьКомандВариантовОтчетов",
			ПараметрКоманды,
			Новый Структура("ЗаказКлиента, КонтекстПоДокументу", ПараметрКоманды, Истина),
			КлючВарианта,
			Истина,
			Ложь),
		,
		"ЗаказКлиента=" + ПараметрКоманды,
		ПараметрыВыполненияКоманды.Окно);
	
КонецПроцедуры

&НаСервере
Функция ЗначенияФункциональныхОпций()
	
	ЗначенияФункциональныхОпций = Новый Структура;
	ЗначенияФункциональныхОпций.Вставить("ИспользоватьРасширенныеВозможностиЗаказаКлиента", ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	ЗначенияФункциональныхОпций.Вставить("БазоваяВерсия", ПолучитьФункциональнуюОпцию("БазоваяВерсия"));
	
	Возврат ЗначенияФункциональныхОпций;
	
КонецФункции
