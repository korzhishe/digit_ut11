
///////////////////////////////////////////////
// Функции преобразования взял тут http://help1c.com 

// Функция вычислет степень числа
// _База    - Число                - Число, возводимое в степень
// _Степ   - Число                - Степень числа
&НаКлиенте
Функция Степень(_База, _Степ)
	Результат = 1;
	Для К = 1 По _Степ Цикл
		Результат = Результат *_База;                              
	КонецЦикла;
	Возврат Результат;
КонецФункции // Возвращает Число, возведенное в степень

// Функция переводит десятичное число в шестнадцатеричное
// Параметры:  _Число                - Число                - Десятичное число
// Возвращаемое значение:  Строка - Шестнадцатеричное число
//
&НаКлиенте
Функция DecToHex(Знач _Число)
	База = 16;
	Результат = "";
	Пока _Число <> 0 Цикл
		Поз =_Число % База;
		Результат = Сред("0123456789ABCDEF", Поз + 1, 1) + Результат;
		_Число = Цел(_Число / База);
	КонецЦикла;
	// >>Gmix  немного доработал
	Если Результат = "" Тогда
	    Результат="0";
	КонецЕсли;
	Если СтрДлина(Результат)=1 Тогда
		Результат="0"+Результат;
	КонецЕсли;
    // <<Gmix
	Возврат Результат;
КонецФункции // DecToHex()
 
// Функция переводит шестнадцатеричное число в десятичное
// Параметры: _Hex     - Строка              - Шестнадцатеричное число
// Возвращаемое значение: Число   - Десятичное число
//
&НаКлиенте
Функция HexToDec(Знач _Hex)
	База = 16;
	_Hex = СокрЛП(_Hex);
	СтаршаяСтепень = СтрДлина(_Hex) - 1;
	Результат = 0;
	счСимволов = 1;
	Пока СтаршаяСтепень >=0 Цикл
		_HexСимвол = Сред(_Hex, счСимволов, 1);
		Представление = Найти("0123456789ABCDEF", _HexСимвол) - 1;
		Результат = Результат + Представление * Степень(База, СтаршаяСтепень);
		СтаршаяСтепень = СтаршаяСтепень - 1;
		СчСимволов = СчСимволов + 1;
	КонецЦикла;   
	Возврат Результат;
КонецФункции // HexToDec() 

&НаКлиенте
Функция ПреобразоватьЦвет(ОБ_Цвет)
	Если НЕ ОБ_Цвет.Вид=ВидЦвета.Абсолютный Тогда
		// Попробуем преобразовать
		Стр_ИмяВремФайла=ПолучитьИмяВременногоФайла(".mxl");
		
		ОБ_Таб=Новый ТабличныйДокумент;
		ОБ_Таб.Область().ЦветФона=ОБ_Цвет;
		ОБ_Таб.Записать(Стр_ИмяВремФайла,ТипФайлаТабличногоДокумента.HTML);
		
		Текст=Новый ТекстовыйДокумент;
		Текст.Прочитать(Стр_ИмяВремФайла);
		УдалитьФайлы(Стр_ИмяВремФайла);
		
		Стр_ТекстНТМЛ=Текст.ПолучитьТекст();
		
		Стр_КлючПоиска="background: #";
		Ч_ПозНач=Найти(Стр_ТекстНТМЛ,Стр_КлючПоиска);
		Если Ч_ПозНач>0 Тогда
			Стр_ЦветHEX=ВРЕГ(Сред(Стр_ТекстНТМЛ,Ч_ПозНач+СтрДлина(Стр_КлючПоиска),7));
			Возврат Новый Цвет(HexToDec(Сред(Стр_ЦветHEX,1,2)),HexToDec(Сред(Стр_ЦветHEX,3,2)),HexToDec(Сред(Стр_ЦветHEX,5,2)));	
		КонецЕсли;
	КонецЕсли;
	Возврат ОБ_Цвет;
КонецФункции // ПреобразоватьЦвет()


//Начало выбора цвета
&НаКлиенте
Процедура HTMLЦветНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	Диалог = Новый ДиалогВыбораЦвета;	
	Оповещение = Новый ОписаниеОповещения("ВыборЦветаЗавершение", ЭтотОбъект); 
	Диалог.Показать(Оповещение);  
		
КонецПроцедуры

//Оповещение выбора цвета
&НаКлиенте 
Процедура ВыборЦветаЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	Если НЕ Результат = Неопределено Тогда 
		
		ВыбранныйЦвет = Результат; 
		ОБ_Цвет=ПреобразоватьЦвет(ВыбранныйЦвет);
		КодЦвета=""+DecToHex(ОБ_Цвет.Красный)+DecToHex(ОБ_Цвет.Зеленый)+DecToHex(ОБ_Цвет.Синий);
        Объект.HTMLЦвет = КодЦвета;

	КонецЕсли; 
КонецПроцедуры 


//После записи
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ГАЛ_Карты40 Тогда
		Оповестить("ОбработкаВводаНовогоПолигона_карты", Объект.Ссылка);
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ГАЛ_Карты40") Тогда
		 ГАЛ_Карты40 = Истина;
	КонецЕсли;
	
КонецПроцедуры

