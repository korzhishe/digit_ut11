#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	РеквизитыСправочника = Метаданные.Справочники.РассылкиОтчетов.Реквизиты;
	Элементы.СерверИКаталог.Подсказка      = РеквизитыСправочника.SharePointКаталог.Подсказка;
	Элементы.Логин.Подсказка               = РеквизитыСправочника.SharePointЛогин.Подсказка;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Сервер, Каталог, Логин, Пароль, ИДКлиента, СекретКлиента, ТенантИД");
	Если ЭтотОбъект.Сервер = "" Тогда
		ЭтотОбъект.Сервер = "сервер";
	КонецЕсли;
	Если ЭтотОбъект.Каталог = "" Тогда
		ЭтотОбъект.Каталог = "/каталог/";
	КонецЕсли;
	ВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СерверИКаталогПриИзменении(Элемент)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РассылкаОтчетовКлиентСервер.РазобратьSharePointАдрес(СерверИКаталог), "Сервер, Каталог");
	ВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Если Сервер = "" Тогда
		ПолныйАдрес = НСтр("ru = 'https://сервер/каталог/'");
	Иначе
		ПолныйАдрес = "https://"+ Сервер + Каталог;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект);
	ПоказатьВводСтроки(Обработчик, ПолныйАдрес, НСтр("ru = 'Введите полный Share Point адрес'"))
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	ЗначениеВыбора = Новый Структура("Сервер, Каталог, Логин, Пароль, ИДКлиента, СекретКлиента, ТенантИД");
	ЗаполнитьЗначенияСвойств(ЗначениеВыбора, ЭтотОбъект);
	ОповеститьОВыборе(ЗначениеВыбора);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьДоступность(Форма, Изменения = "")
	Если Не СтрЗаканчиваетсяНа(Форма.Каталог, "/") Тогда
		Форма.Каталог = Форма.Каталог + "/";
	КонецЕсли;
	Форма.СерверИКаталог = "https://"+ Форма.Сервер + Форма.Каталог;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВвода, ДополнительныеПараметры) Экспорт
	Если РезультатВвода <> Неопределено Тогда
		ПарольДоВвода = Пароль;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РассылкаОтчетовКлиентСервер.РазобратьSharePointАдрес(РезультатВвода));
		Если Пароль = ПарольСкрыт() Тогда
			Пароль = ПарольДоВвода;
		КонецЕсли;
		ВидимостьДоступность(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПарольСкрыт()
	Возврат "********";
КонецФункции

#КонецОбласти
