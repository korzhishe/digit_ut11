#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ПометкаУдаления Тогда
		ПроверитьДублиЭлемента(Отказ);
		ПроверитьСтрануПроисхождения(Отказ);
	КонецЕсли;
	
	//БА_Начало
	СтрокаКода = Код;//"123456/150116/54321";	
	Если СтрЧислоВхождений(СтрокаКода, "/") >= 2 Тогда  		
		ДлинаСтроки = СтрДлина(СтрокаКода);
		тт = СтрНайти(СтрокаКода, "/");
		ДлинаСтроки = ДлинаСтроки - тт;
		СтрокаКода = Прав(СтрокаКода, ДлинаСтроки);
		тт = СтрНайти(СтрокаКода, "/");
		СтрокаКода = Лев(СтрокаКода, тт - 1);
		Если СтрДлина(СтрокаКода) = 6 Тогда
			Попытка
				ДатаВКодеД = Дата("20" + Прав(СтрокаКода, 2), Сред(СтрокаКода, 3, 2), "01");
				ДатаВКоде = Строка(ДобавитьМесяц(ДатаВКодеД, -1)); 
				ДатаВКоде = Сред(ДатаВКоде, 4, 7);
			Исключение
				ДатаВКоде = "";
			КонецПопытки;
			ДатаПроизводства 	= ДатаВКоде; //ДобавитьМесяц(ДатаВКоде, -1);
			дп_ДатаПроизводства	= ДобавитьМесяц(ДатаВКодеД, -1);
		КонецЕсли;
	КонецЕсли;    	
	//БА_Конец
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Процедура ПроверитьДублиЭлемента(Отказ)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НомераГТД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НомераГТД КАК НомераГТД
	|ГДЕ
	|	НомераГТД.Ссылка <> &Ссылка
	|	И Не НомераГТД.ПометкаУдаления
	|	И НомераГТД.Код = &Код
	|	И НомераГТД.СтранаПроисхождения = &СтранаПроисхождения
	|");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("СтранаПроисхождения", СтранаПроисхождения);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Аналогичный номер ГТД уже существует: %1'"),
			Выборка.Ссылка);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ЭтотОбъект,
			, // Поле
			,
			Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСтрануПроисхождения(Отказ)

	Если СтранаПроисхождения = Справочники.СтраныМира.Россия Тогда
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Создание номера ГТД для товара, страной происхождения которого является: %1, не требуется'"),
			СтранаПроисхождения);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ЭтотОбъект,
			,
			,
			Отказ);
	
	КонецЕсли;

КонецПроцедуры
 
#КонецОбласти

#КонецОбласти

#КонецЕсли
