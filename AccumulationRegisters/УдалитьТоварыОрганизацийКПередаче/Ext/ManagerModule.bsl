#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Перечень (таблица) позиций и количеств к оформлению передач в разрезе измерений регистра товаров к передаче
Функция ТоварыКПередаче() Экспорт
	Запрос = Новый Запрос(ТекстОстаткиКПередачеИПотреблению());
	Результат = Запрос.Выполнить();
	
	Потребления = Новый ТаблицаЗначений;
	Для Каждого Колонка Из Результат.Колонки Цикл
		Потребления.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	КПотреблению = 0.;
	Порядок = Новый Структура("НомерГТД, Номенклатура, Характеристика, Склад, ВидЗапасовПродавца, ОрганизацияВладелец");

	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Для Каждого ЭлементПорядка Из Порядок Цикл
			Если ЭлементПорядка.Значение <> Выборка[ЭлементПорядка.Ключ] Тогда
				КПотреблению = 0.;
				ЗаполнитьЗначенияСвойств(Порядок, Выборка);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Выборка.КПередаче > КПотреблению Тогда
			Потребление = Потребления.Добавить();
			ЗаполнитьЗначенияСвойств(Потребление, Выборка);
			Потребление.КПередаче = Потребление.КПередаче - КПотреблению;
			КПотреблению = 0.;
		Иначе
			КПотреблению = КПотреблению + Выборка.КПотреблению - Выборка.КПередаче;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Потребления;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Текст запроса остатков к передаче и потреблению на каждый день.
// Поля результата запроса:
//		{Период, ПоступленияПериод, ДатаОформления, ОрганизацияВладелец, Номенклатура, Характеристика,
//		Склад, ВидЗапасовПродавца, НомерГТД, КПотреблению, КПередаче}
// Упорядоченность:
//		{ОрганизацияВладелец, Номенклатура, Характеристика,
//		Склад, ВидЗапасовПродавца, НомерГТД, Период}
Функция ТекстОстаткиКПередачеИПотреблению()
	Возврат "
		|ВЫБРАТЬ
		|	Потребления.Период,
		|	МАКСИМУМ(Поступления.Период) КАК ПоступленияПериод,
		|	(ВЫБОР
		|		КОГДА МАКСИМУМ(Поступления.Период) ЕСТЬ NULL ТОГДА Потребления.Период
		|		КОГДА МАКСИМУМ(Поступления.Период) < НАЧАЛОПЕРИОДА(Потребления.Период, МЕСЯЦ)
		|			ТОГДА НАЧАЛОПЕРИОДА(Потребления.Период, МЕСЯЦ)
		|		КОГДА НАЧАЛОПЕРИОДА(МАКСИМУМ(Поступления.Период), ДЕНЬ) = Потребления.Период ТОГДА Потребления.Период
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(МАКСИМУМ(Поступления.Период), ДЕНЬ), ДЕНЬ, 1)
		|		КОНЕЦ) КАК ДатаОформления,
		|	Потребления.ОрганизацияВладелец,
		|	Потребления.Номенклатура, Потребления.Характеристика,
		|	Потребления.Склад, Потребления.ВидЗапасовПродавца, Потребления.НомерГТД,
		//|	Потребления.КоличествоПриход КАК Потреблено, Потребления.КоличествоРасход КАК Передано,
		|	(ВЫБОР КОГДА Потребления.КоличествоПриход < Потребления.КоличествоРасход
		|		ТОГДА Потребления.КоличествоРасход - Потребления.КоличествоПриход
		|		ИНАЧЕ 0. КОНЕЦ) КАК КПотреблению,
		|	(ВЫБОР КОГДА Потребления.КоличествоПриход < Потребления.КоличествоРасход ТОГДА 0.
		|		ИНАЧЕ Потребления.КоличествоПриход - Потребления.КоличествоРасход КОНЕЦ) КАК КПередаче
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизацийКПередаче.Обороты( , , ДЕНЬ) КАК Потребления
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
		|		ПО ВидыЗапасов.Ссылка = Потребления.ВидЗапасовПродавца
		|	// поступления товаров не позднее момента текущего потребления - эти поступления мы могли потребить
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК Поступления
		|		ПО Поступления.ВидДвижения = значение(ВидДвиженияНакопления.Приход)
		|		И Поступления.Период <= КОНЕЦПЕРИОДА(Потребления.Период, ДЕНЬ)
		|		И Поступления.Номенклатура = Потребления.Номенклатура И Поступления.Характеристика = Потребления.Характеристика
		|		И Поступления.Склад = Потребления.Склад И Поступления.Организация = Потребления.ОрганизацияВладелец
		|		И Поступления.ВидЗапасов = ВидыЗапасов.ВидЗапасовВладельца И Поступления.НомерГТД = Потребления.НомерГТД
		|		И Поступления.Количество > 0. И (Потребления.КоличествоПриход > 0. ИЛИ Потребления.КоличествоРасход > 0.)
		|ГДЕ
		|	Потребления.КоличествоПриход <> Потребления.КоличествоРасход
		|
		|СГРУППИРОВАТЬ ПО
		|	Потребления.ОрганизацияВладелец,
		|	Потребления.Номенклатура, Потребления.Характеристика,
		|	Потребления.Склад, Потребления.ВидЗапасовПродавца, Потребления.НомерГТД,
		|	Потребления.Период,
		|	Потребления.КоличествоПриход, Потребления.КоличествоРасход
		|
		|УПОРЯДОЧИТЬ ПО
		|	Потребления.ОрганизацияВладелец,
		|	Потребления.Номенклатура, Потребления.Характеристика,
		|	Потребления.Склад, Потребления.ВидЗапасовПродавца, Потребления.НомерГТД,
		|	Потребления.Период
		|";
КонецФункции

#КонецОбласти

#КонецЕсли