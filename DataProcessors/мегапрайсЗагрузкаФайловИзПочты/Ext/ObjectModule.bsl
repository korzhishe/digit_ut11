

//============================================================================
// ПОДКЛЮЧЕНИЕ

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

Функция СведенияОВнешнейОбработке() Экспорт
	
	РегистрационныеДанные = Новый Структура;
	
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	
	ДобавитьКоманду(Команды,"Мегапрайс: Загрузка файлов из почты","ОткрытиеФормы","ОткрытиеФормы",,);
	ДобавитьКоманду(Команды,"Мегапрайс: Загрузка файлов из почты","ВыполнитьОбработку","ВызовСерверногоМетода",);
	
	//Инициализация сведений об обработке 
	РегистрационныеДанные.Вставить("Вид","ДополнительнаяОбработка");
	РегистрационныеДанные.Вставить("Назначение",Новый Массив);
	РегистрационныеДанные.Вставить("Наименование","Мегапрайс: Загрузка файлов из почты");
	РегистрационныеДанные.Вставить("Версия","2019");
	РегистрационныеДанные.Вставить("БезопасныйРежим",Ложь);
	РегистрационныеДанные.Вставить("Информация","");
	РегистрационныеДанные.Вставить("Команды",Команды);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

//============================================================================
// 


Процедура ВыполнитьОбработку() Экспорт
	
	Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		УчетнаяЗапись = УправлениеЭлектроннойПочтой.ПолучитьУчетнуюЗаписьДляОтправкиПоУмолчанию();
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Сообщить("Не выбрана учетная запись электронной почты.");
		Возврат;
	КонецЕсли;
	
	ТаблицаПартнеров.Очистить();
	ТаблицаСообщений.Очистить();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	мегапрайсПрофилиПрайсов.Ссылка КАК ПрофильПрайса,
	|	мегапрайсПрофилиПрайсов.ЗаданиеВремяНачало КАК ЗаданиеВремяНачало,
	|	мегапрайсПрофилиПрайсов.Приоритет КАК Приоритет,
	|	мегапрайсПрофилиПрайсов.Партнер КАК Партнер,
	|	мегапрайсПрофилиПрайсов.ФайлКаталогНаЛокальномДиске КАК КаталогНаЛокальномДиске,
	|	мегапрайсПрофилиПрайсов.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты
	|ИЗ
	|	Справочник.мегапрайсПрофилиПрайсов КАК мегапрайсПрофилиПрайсов
	|ГДЕ
	|	мегапрайсПрофилиПрайсов.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет УБЫВ";
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаПартнеров.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Если ВыполнитьЗагрузкуФайлов Тогда
		
		УстановитьОтключениеБезопасногоРежима(Истина);
		Профиль = РаботаСПочтовымиСообщениямиСлужебный.ИнтернетПочтовыйПрофиль(УчетнаяЗапись,Истина);

		Соединение = Новый ИнтернетПочта;
		Соединение.Подключиться(Профиль,ПротоколИнтернетПочты.IMAP);
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("New", ОтборНовые);
		ПараметрыОтбора.Вставить("Seen", ОтборПрочитанные);
		Если ЗначениеЗаполнено(ОтборПослеДатыОтправления) Тогда
			ПараметрыОтбора.Вставить("AfterDateOfPosting", ОтборПослеДатыОтправления);
		КонецЕсли;
		Заголовки = Соединение.ПолучитьЗаголовки(ПараметрыОтбора);
		Если Заголовки.Количество() = 0 Тогда
			Сообщить("На сервере нет сообщений!");
			Возврат;
		КонецЕсли;
		
		МассивСообщений = Соединение.Выбрать(Ложь,Заголовки,Истина); //ИНТЕРНЕТПОЧТА	 	
		Для Каждого Сообщение из МассивСообщений Цикл
			КоличествоВложений = Сообщение.Вложения.Количество();
			Если КоличествоВложений = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстСообщения = ""+Сообщение.Отправитель.Адрес+"   "+Сообщение.Тема+"    "+Сообщение.ДатаОтправления; 
			НоваяСтрока = ТаблицаСообщений.Добавить();
			НоваяСтрока.ТекстСообщения = ТекстСообщения;
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("АдресЭлектроннойПочты", СокрЛП(Сообщение.Отправитель.Адрес));
			НайденыСтроки = ТаблицаПартнеров.НайтиСтроки(ПараметрыОтбора);
			
			Для Каждого ВыборкаПартнер Из НайденыСтроки Цикл
				ВыборкаПартнер.Сообщение = ТекстСообщения;

				КаталогДляСохранения = ВыборкаПартнер.КаталогНаЛокальномДиске;
				Если НЕ ЗначениеЗаполнено(КаталогДляСохранения) Тогда
					Продолжить;
				КонецЕсли;
				
				Для Каждого Вложение из Сообщение.Вложения Цикл
					Сообщить(Вложение.Имя);  
					
					ПутьДоФайла = КаталогДляСохранения+"\"+Вложение.Имя;
					Вложение.Данные.Записать(ПутьДоФайла);
				КонецЦикла;			
			КонецЦикла;
			
		КонецЦикла;
		
		Соединение.Отключиться();
		УстановитьОтключениеБезопасногоРежима(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры


