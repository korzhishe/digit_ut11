

Процедура ТоварыНоменклатураПриИзменении(НовыйДокумент,ТекущаяСтрока)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", Новый Структура("Дата, Валюта, ВидЦены", НовыйДокумент.Дата, НовыйДокумент.Валюта, НовыйДокумент.ВидЦены));
	СтруктураДействий.Вставить("ПересчитатьСумму", "Количество");
	СтруктураДействий.Вставить("ЗаполнитьПризнакВедетсяУчетПоГТД", Новый Структура("Номенклатура", "ВедетсяУчетПоГТД"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ПроверитьЗаполнитьНазначение");
	
	//СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",ЭтаФорма.ИмяФормы, "Товары"));

	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры


Процедура ГенерацияОстатковПоставщиковНаВиртуальныйСклад(ВиртуальныйСкладПоставщика = Неопределено) Экспорт
	
	мегапрайсИспользоватьВиртуальныеОстатки = Константы.мегапрайсИспользоватьВиртуальныеОстатки.Получить();
	Если НЕ мегапрайсИспользоватьВиртуальныеОстатки Тогда
		Сообщить("Механизм Виртуальных складских остатков НЕ включен.");
		Возврат;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ВиртуальныйСкладПоставщика) Тогда
		Сообщить("Виртуальный склад не выбран!");
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОприходованиеИзлишковТоваров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОприходованиеИзлишковТоваров КАК ОприходованиеИзлишковТоваров
	|ГДЕ
	|	ОприходованиеИзлишковТоваров.Проведен = ИСТИНА
	|	И ОприходованиеИзлишковТоваров.Склад = &ВиртуальныйСклад";
	
	Запрос.УстановитьПараметр("ВиртуальныйСклад", ВиртуальныйСкладПоставщика);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();	
	Пока РезультатЗапроса.Следующий() Цикл
		Попытка
			ОприходованиеСсылка = РезультатЗапроса.Ссылка.ПолучитьОбъект();
			ОприходованиеСсылка.Записать(РежимЗаписиДокумента.ОтменаПроведения);	
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;			
		
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	               |	ЦеныНоменклатурыСрезПоследних.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	               |	ЦеныНоменклатурыСрезПоследних.ПрайсПартнера.Партнер КАК Партнер,
	               |	ЦеныНоменклатурыСрезПоследних.ПрайсПартнера КАК ПрайсПартнера,
	               |	ЦеныНоменклатурыСрезПоследних.Валюта КАК Валюта,
	               |	МИНИМУМ(ЦеныНоменклатурыСрезПоследних.Цена) КАК МинимальнаяЦена,
	               |	СУММА(ЦеныНоменклатурыСрезПоследних.Количество) КАК Количество,
	               |	ЦеныНоменклатурыСрезПоследних.ПрайсПартнера.ВиртуальныйСкладПоставщика КАК ВиртуальныйСкладПоставщика
	               |ИЗ
	               |	РегистрСведений.мегапрайсЦеныНоменклатурыПоставщиков.СрезПоследних(, ПрайсПартнера.ВиртуальныйСкладПоставщика = &ВиртуальныйСклад) КАК ЦеныНоменклатурыСрезПоследних
	               |ГДЕ
	               |	ЦеныНоменклатурыСрезПоследних.Количество > 0
	               |	И ЦеныНоменклатурыСрезПоследних.Номенклатура.ЭтоГруппа = ЛОЖЬ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЦеныНоменклатурыСрезПоследних.Номенклатура,
	               |	ЦеныНоменклатурыСрезПоследних.ХарактеристикаНоменклатуры,
	               |	ЦеныНоменклатурыСрезПоследних.ПрайсПартнера.Партнер,
	               |	ЦеныНоменклатурыСрезПоследних.ПрайсПартнера,
	               |	ЦеныНоменклатурыСрезПоследних.Валюта,
	               |	ЦеныНоменклатурыСрезПоследних.ПрайсПартнера.ВиртуальныйСкладПоставщика";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ВиртуальныйСклад", ВиртуальныйСкладПоставщика);
		
	ДокументОрганизация    = Справочники.Организации.УправленческаяОрганизация; //ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(ДокументОрганизация);
	ДокументМенеджер       = Пользователи.ТекущийПользователь();
	ДокументПодразделение  = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(ДокументМенеджер, ДокументПодразделение);		
	
	ВыборкаСтрока = Запрос.Выполнить().Выбрать();
		
	НовыйДокумент = Документы.ОприходованиеИзлишковТоваров.СоздатьДокумент();
	НовыйДокумент.Дата          = ТекущаяДата();
	НовыйДокумент.Организация   = ДокументОрганизация;
	НовыйДокумент.Подразделение = ДокументПодразделение;
	НовыйДокумент.СтатьяДоходов = ПланыВидовХарактеристик.СтатьиДоходов.ПрибыльУбытокПрошлыхЛет;
	НовыйДокумент.Склад         = ВиртуальныйСкладПоставщика;	
	НовыйДокумент.Валюта        = Константы.ВалютаРегламентированногоУчета.Получить();
	
	НовыйДокумент.Ответственный = ДокументМенеджер;
	НовыйДокумент.Комментарий   = "Генерация виртуальных остатков";	

	
	Пока ВыборкаСтрока.Следующий() Цикл	
		
		Если НЕ ЗначениеЗаполнено(ВыборкаСтрока.Номенклатура) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВыборкаСтрока.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТЧ = НовыйДокумент.Товары.Добавить();
		СтрокаТЧ.Номенклатура     = ВыборкаСтрока.Номенклатура;
		СтрокаТЧ.Характеристика   = ВыборкаСтрока.ХарактеристикаНоменклатуры;
		СтрокаТЧ.Количество       = ВыборкаСтрока.Количество;
		СтрокаТЧ.Цена  = ВыборкаСтрока.МинимальнаяЦена;		
		СтрокаТЧ.Сумма = СтрокаТЧ.Цена*СтрокаТЧ.Количество;	
		СтрокаТЧ.ВидЗапасов = Справочники.ВидыЗапасов.ВидЗапасовДокумента(НовыйДокумент.Организация,Перечисления.ХозяйственныеОперации.ОприходованиеТоваров)
		
		//ТоварыНоменклатураПриИзменении(НовыйДокумент,СтрокаТЧ);		
	КонецЦикла;		
	
	Если НовыйДокумент.Товары.Количество() > 0 Тогда
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
			Сообщить("Виртуальные остатки обновлены "+ НовыйДокумент.Ссылка);
			Мегапрайс.мегапрайсЗаписатьСобытиеВЖурнал(ВиртуальныйСкладПоставщика,"Виртуальные остатки обновлены "+ НовыйДокумент.Ссылка);
		Исключение
			Сообщить(ОписаниеОшибки());
			Мегапрайс.мегапрайсЗаписатьСобытиеВЖурнал(ВиртуальныйСкладПоставщика,ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли;
			
КонецПроцедуры
