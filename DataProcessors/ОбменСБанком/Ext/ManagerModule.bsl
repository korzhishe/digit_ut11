#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура проверяет заполнение таблицы с документами.
//
// Параметры:
//   Объект		- Объект						  - обработка ОбменСБанком.
//   Документ	- ТабличнаяЧасть, ТаблицаЗначений - таблица с данными документов.
//   Отказ		- Булево						  – флаг наличия проблемы в текущем сеансе работы обработки.
Процедура ПроверитьЗаполнениеТаблицыДокументов(Объект, Документ, Отказ = Ложь) Экспорт
			
	Для НомСтр = 0 По Документ.Количество() - 1 Цикл
		
		Если Документ[НомСтр].Готовность > 0 Тогда
						
			Стр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"), Документ[НомСтр].ОписаниеОшибок);
			
			Если Документ[НомСтр].Свойство("Выгружать") Тогда
				Поле = "Объект.ПлатежныеДокументы[" + НомСтр + "].Выгружать";
			Иначе
				Поле = "ДокументыКИмпорту[" + НомСтр + "].Загружать";
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Стр, 
				, 
				Поле,
				, 
				Отказ);
			
		КонецЕсли;
		          		
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполняет таблицу документов на экспорт.
//
// Параметры:
//   ОбъектПлатежныеДокументы	- ТабличнаяЧасть				  - таблица с данными документов.
//   ДокументыНаЭкспорт			- ТабличнаяЧасть, ТаблицаЗначений - заполняемая таблица с данными документов.
//   НачПериода					- Дата							  – начало обрабатываемого периода.
//   КонПериода					- Дата							  - конец обрабатываемого периода.
//   Организация				- СправочникСсылка.Организации	  - информация об организации выгрузки.
//   Программа					- Строка						  – информация о программе банка, для которой формируется файл.
//   Файл						- Строка						  - информация о пути к файлу выгрузки.
Процедура ЗаполнитьДокументыНаЭкспорт(ОбъектПлатежныеДокументы, 
									 	ДокументыНаЭкспорт, 
										НачПериода, 
										КонПериода, 
										Организация, 
										Программа, 
										Файл) Экспорт
	 	
	ВыгружатьПлатежноеПоручение = Истина;
		
	УсловиеОтбораПоТипуПлатежногоДокумента = "";
	
	Если ВыгружатьПлатежноеПоручение Тогда
		УсловиеОтбораПоТипуПлатежногоДокумента = 
			УсловиеОтбораПоТипуПлатежногоДокумента + "ТипПлатежногоДокумента = &ПлатежноеПоручение";
	КонецЕсли;
			
	ТекстЗапроса = ПолучитьТекстЗапросаПоСписаниеБезналичныхДенежныхСредств(УсловиеОтбораПоТипуПлатежногоДокумента, 
																			Организация, 
																			Программа, 
																			Файл);
		
	ТекстЗапроса = ТекстЗапроса + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяДокумента,
	|	Дата,
	|	Документ";
	
	ЗапросПоДокументам = Новый Запрос(ТекстЗапроса);
	ЗапросПоДокументам.УстановитьПараметр("ДатаНачало",          НачалоДня(НачПериода));
	ЗапросПоДокументам.УстановитьПараметр("ДатаОкончание",       КонецДня(КонПериода));
	ЗапросПоДокументам.УстановитьПараметр("Организация",         Организация);
	ЗапросПоДокументам.УстановитьПараметр("Программа",           Программа);
	ЗапросПоДокументам.УстановитьПараметр("Файл",                Файл);
    ЗапросПоДокументам.УстановитьПараметр("ПлатежноеПоручение",  
											Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	ЗапросПоДокументам.УстановитьПараметр("СтрокаСписаниеБезналичных", НСтр("ru='Списание безналичных денежных средств'"));
	
	ОбъектПлатежныеДокументы.Загрузить(ЗапросПоДокументам.Выполнить().Выгрузить());
	
	КоллекцияЭлементов = ДокументыНаЭкспорт.ПодчиненныеЭлементы;
	Для Каждого СтрокаДокумента Из ОбъектПлатежныеДокументы Цикл
		
		ПроверитьНаКорректностьИПустоеЗначениеЭкспорта(СтрокаДокумента, КоллекцияЭлементов);
		
		СтрокаДокумента.Выгружать = ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок);
		
	КонецЦикла;

КонецПроцедуры

// Процедура выгружает данные в файл.
//
// Параметры:
//   ПлатежныеДокументы			- ТабличнаяЧасть				  - таблица с данными документов.
//   Кодировка					- Строка						  - информация о кодировке файла выгрузки.
//   Программа					- Строка						  – информация о программе банка, для которой формируется файл.
//   НачПериода					- Дата							  – начало обрабатываемого периода.
//   КонПериода					- Дата							  - конец обрабатываемого периода.
//   ФайлВыгрузки				- Строка						  - информация о пути к файлу выгрузки.
//   Организация				- СправочникСсылка.Организации	  - информация об организации выгрузки.
//   АдресВременногоХранилища	- Строка						  – адрес, указывающий на значение во временном хранилище.
//   ВыгружатьКПП				- Булево						  - флаг:выгружать КПП плательщика и получателя для всех документов.
Процедура Выгрузить(ПлатежныеДокументы, 
					Кодировка, 
					Программа, 
					НачПериода, 
					КонПериода, 
					ФайлВыгрузки,
					Организация, 
					АдресВременногоХранилища, 
					ВыгружатьКПП) Экспорт
	
	ПотокВыгрузки = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		
	// Формируем заголовок.
	ПотокВыгрузки.ДобавитьСтроку("1CClientBankExchange");
	
	Если ТекущаяДатаСеанса() >= '20140101' Тогда
		ВерсияФормата = "1.02";
	Иначе
		ВерсияФормата = "1.01";
	КонецЕсли;
	ПотокВыгрузки.ДобавитьСтроку("ВерсияФормата=" + ВерсияФормата);
	
	ПотокВыгрузки.ДобавитьСтроку("Кодировка="     + Кодировка);
	ПотокВыгрузки.ДобавитьСтроку("Отправитель="   + Метаданные.Синоним);
	ПотокВыгрузки.ДобавитьСтроку("Получатель="    + Программа);
	ПотокВыгрузки.ДобавитьСтроку("ДатаСоздания="  + Формат(ТекущаяДата(), "ДЛФ=Д"));
	ПотокВыгрузки.ДобавитьСтроку("ВремяСоздания=" + Формат(ТекущаяДата(), "ДЛФ=В"));
	ПотокВыгрузки.ДобавитьСтроку("ДатаНачала="    + Формат(НачПериода, "ДЛФ=Д"));
	ПотокВыгрузки.ДобавитьСтроку("ДатаКонца="     + Формат(КонПериода, "ДЛФ=Д"));

	ПереченьРасчСчетов = ПлатежныеДокументы.Выгрузить(,"ОрганизацияРасчСчет");
	
	ПереченьРасчСчетов.Свернуть("ОрганизацияРасчСчет");
	
	Для СчетчикСтрок = 0 По ПереченьРасчСчетов.Количество() - 1 Цикл
		ПотокВыгрузки.ДобавитьСтроку("РасчСчет="      + СокрЛП(ПереченьРасчСчетов[СчетчикСтрок].ОрганизацияРасчСчет));
	КонецЦикла;	
 		
	ИмяДокумента = "Платежное поручение";
	ПотокВыгрузки.ДобавитьСтроку("Документ=" + ИмяДокумента);
			
	// Выводим отмеченные секции документов.
	Для Каждого СтрокаСекции Из ПлатежныеДокументы Цикл
		
		Если НЕ СтрокаСекции.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		
		Буфер = ПолучитьСекциюДокумент(СтрокаСекции, Организация, ВыгружатьКПП, ВерсияФормата);
		
		КоличествоСрокСекции = СтрЧислоСтрок(Буфер);
		
		Для Сч = 1 По КоличествоСрокСекции Цикл
			ПотокВыгрузки.ДобавитьСтроку(СтрПолучитьСтроку(Буфер, Сч));
		КонецЦикла;
		
		СтрокаСекции.Готовность = -2;
		
	КонецЦикла;
	
	// Записываем на диск.
	ПотокВыгрузки.ДобавитьСтроку("КонецФайла");
	
	ПоместитьВоВременноеХранилище(ПотокВыгрузки, АдресВременногоХранилища);
	
КонецПроцедуры

// Процедура заполняет таблицу документов для импорта.
//
// Параметры:
//   ДокументыДляИмпорта	- ТаблицаЗначений - таблица с данными документов.
//   Таблица				- ТаблицаЗначений - заполняемая таблица с данными документов.
//
//	 ПараметрыЗаполнения 	- Соответствие:
//			ВыводитьСообщения	- Булево						- флаг: сообщать пользователю о возникающих проблемах.
//			ФайлЗагрузки		- Строка						- информация о пути к файлу загрузки.
//			Программа			- Строка						– информация о программе банка, которая сформировала импортируемый файл.
//			Кодировка			- Строка						- информация о кодировке файла загрузки.
//			Организация			- СправочникСсылка.Организации	- информация об организации загрузки.
//			НастройкаЗаполнения	- ТаблицаЗначений				- таблица со значениями по умолчанию для документов.
//
//   ТаблицаКонтрагентов	  - ДеревоЗначений	– информация о контрагентах.
//   Импорт_Заголовок		  - Строка			- данные для заполнения заголовка файла импорта.
//   Импорт_РасчетныеСчета	  - ТаблицаЗначений	- информация о расчетных счетах контрагетовконтрагентах.
//   АдресВременногоХранилища - Строка			– адрес, указывающий на значение во временном хранилище.
Процедура ЗаполнитьДокументыНаИмпорт(ДокументыДляИмпорта, 
										Таблица,  
										ПараметрыЗаполнения,
										ТаблицаКонтрагентов, 
										Импорт_Заголовок, 
										Импорт_РасчетныеСчета, 
										АдресВременногоХранилища) Экспорт
	
	Импорт_ТекстДляРазбора = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	ВыводитьСообщения	= ПараметрыЗаполнения["ВыводитьСообщения"];
    ФайлЗагрузки		= ПараметрыЗаполнения["ФайлЗагрузки"];
	Программа			= ПараметрыЗаполнения["Программа"];
	Кодировка			= ПараметрыЗаполнения["Кодировка"];
	Организация			= ПараметрыЗаполнения["Организация"];
	НастройкаЗаполнения	= ПараметрыЗаполнения["НастройкаЗаполнения"];
		
	Импорт_ВидыДокументов = Новый Массив;
	
	// Подготавливаем структуры обработки данных.
	Импорт_ПризнакОбмена = Ложь;
	НайденКонецФайла     = Ложь;
	
	СписокДокументов = Новый СписокЗначений;
			
	Импорт_РасчетныеСчета.Очистить();
	
	ДокументыДляИмпорта.Очистить();
		
	// Заполняем первичные структуры данных.
	Импорт_КоличествоСтрок = СтрЧислоСтрок(Импорт_ТекстДляРазбора);
	Импорт_ТекущаяСтрока = 1;
	
	Пока Импорт_ТекущаяСтрока <= Импорт_КоличествоСтрок Цикл
		
		Стр = ПолучитьСтрокуИмпорта(Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора);
		
		Если Лев(Врег(СокрЛП(Стр)), 14) = "СЕКЦИЯДОКУМЕНТ" Тогда
			
			Значение = "";
			Тег = "";
			РазобратьТеговуюСтроку(Стр, Тег, Значение);
			
			Если Тег = "СЕКЦИЯДОКУМЕНТ" Тогда
				
				НоваяСтрокаДокументов = ДокументыДляИмпорта.Добавить();
				НоваяСтрокаДокументов.Операция = Значение;
				Если НЕ ЗагрузитьСекциюДокумента(НоваяСтрокаДокументов, 
													Импорт_ТекущаяСтрока, 
													Импорт_КоличествоСтрок, 
													Импорт_ТекстДляРазбора) Тогда
					Возврат;
				КонецЕсли;
				
			Иначе
				
				Сообщение = Новый СообщениеПользователю;
		
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Нарушена структура файла импорта, строка %1 : %2'"), Импорт_ТекущаяСтрока - 1, Стр);
					
				Сообщение.Сообщить();
				
				Возврат;
				
			КонецЕсли;
			
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 14) = "СЕКЦИЯРАСЧСЧЕТ" Тогда
			
			СтрокаРССчетов = Импорт_РасчетныеСчета.Добавить();
			
			Если НЕ ЗагрузитьСекциюРасчСчета(СтрокаРССчетов, 
												Импорт_ТекущаяСтрока, 
												Импорт_КоличествоСтрок, 
												Импорт_ТекстДляРазбора) Тогда
				
				Сообщение = Новый СообщениеПользователю;
		
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Нарушена структура файла импорта в секции описания расчетного счета! Строка: %1'"), 
									Импорт_ТекущаяСтрока - 1);
					
				Сообщение.Сообщить();
				
				Возврат;
				
			КонецЕсли;
						
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 8) = "РАСЧСЧЕТ" Тогда
			
			Значение = "";
			Тег = "";
			
			РазобратьТеговуюСтроку(Стр, Тег, Значение);
			
			Если Тег = "РАСЧСЧЕТ" Тогда
				
				Если СчетПринадлежитОрганизации(Значение, Организация) Тогда
					ПараметрыОтбора = Новый Структура;
					ПараметрыОтбора.Вставить("РасчСчет", Значение);
					
					СтрокаРССчетов = Импорт_РасчетныеСчета.НайтиСтроки(ПараметрыОтбора);
					
					Если СтрокаРССчетов.Количество() = 0 Тогда
						СтрокаРССчетов = Импорт_РасчетныеСчета.Добавить();
						СтрокаРССчетов.РасчСчет = Значение;
					КонецЕсли;
										
				Иначе
					
					Если ВыводитьСообщения Тогда
												
						Сообщение = Новый СообщениеПользователю;
												
						Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru='В заголовке файла указан счет, не принадлежащий организации: %1!'"), Значение);
												
						Сообщение.Сообщить();
						
					КонецЕсли;
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 8) = "ДОКУМЕНТ" Тогда
			
			Импорт_ВидыДокументов.Добавить(Значение);

		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 10) = "КОНЕЦФАЙЛА" Тогда
			
			Если НЕ Импорт_ПризнакОбмена Тогда
								
				Сообщение = Новый СообщениеПользователю;
				
				Сообщение.Текст = НСтр("ru='В файле импорта отсутствует признак обмена ""1CClientBankExchange""!'");
				
				Сообщение.Сообщить();
				
				Возврат;
				
			КонецЕсли;
			
			НайденКонецФайла = Истина;
			НомерСтроки = 0;
			
			// Формируем объектные структуры данных.
			Для Каждого СтрокаДокумента Из ДокументыДляИмпорта Цикл
				// Последовательно обрабатываем каждую загруженную строку.
				
				БанковскийСчет = "";
						
				Если НЕ СчетПринадлежитОрганизации(СтрокаДокумента.ПлательщикСчет, Организация, БанковскийСчет)
					 и НЕ СчетПринадлежитОрганизации(СтрокаДокумента.ПолучательСчет, Организация, БанковскийСчет)Тогда
					Продолжить;
				КонецЕсли;	
								
				СчетПринадлежитОрганизации(СтрокаДокумента.ПлательщикСчет, Организация, БанковскийСчет);
				
				РаспознатьДанныеВСтрокеДокумента(СтрокаДокумента, БанковскийСчет, Организация, НастройкаЗаполнения);
				НомерСтроки = НомерСтроки + 1;
				СтрокаДокумента.НомерСтроки = НомерСтроки;
				// Для каждого реквизита (= колонка) надо проверить на пустое значение.
				ТаблицаДокументыДляИмпорта = ДокументыДляИмпорта.Выгрузить();
				Для Каждого КолонкаИмпорта Из ТаблицаДокументыДляИмпорта.Колонки Цикл
					ПроверитьНаПустоеЗначениеИмпорта(СтрокаДокумента, 
														КолонкаИмпорта.Имя, 
														Таблица.ПодчиненныеЭлементы["ДокументыКИмпорту" + КолонкаИмпорта.Имя].Заголовок);
				КонецЦикла;
				
				Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("Строка")
					ИЛИ ТипЗнч(СтрокаДокумента.СчетКонтрагента) = Тип("Строка") Тогда
					// Добавляем реквизиты в табличную часть для дальнейшего использования.
					СписокНенайденных(СтрокаДокумента, БанковскийСчет, ТаблицаКонтрагентов);
					
				КонецЕсли;
			КонецЦикла;
			
			// Удалим ненужные строки из таблицы.
			Количество = ДокументыДляИмпорта.Количество() - 1;
			Для Ном = 0 По Количество Цикл
				Если ДокументыДляИмпорта[Количество - Ном].НомерСтроки = 0 Тогда
					ДокументыДляИмпорта.Удалить(Количество - Ном);
				КонецЕсли;
			КонецЦикла;
			
			Возврат;
			
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 20) = "1CCLIENTBANKEXCHANGE" Тогда
			
			Импорт_ПризнакОбмена = Истина;
			
		Иначе
			
			ЗагрузитьСтрокуЗаголовка(Стр, Импорт_ТекущаяСтрока, Импорт_Заголовок);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ НайденКонецФайла Тогда
		
		Импорт_РасчетныеСчета.Очистить();
		ДокументыДляИмпорта.Очистить();
		
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = НСтр("ru='Файл загрузки не соответствует стандарту (не найдена секция КонецФайла)!'");
		
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура загружает платежные документы в информационную базу.
//
// Параметры:
//   ДокументыКИмпорту			  - ТаблицаЗначений				 - таблица с данными документов.
//   Импорт_Заголовок			  - Строка						 - данные для заполнения заголовка файла импорта.
//	 Организация				  - СправочникСсылка.Организации - информация об организации загрузки.
//   Импорт_РасчетныеСчета		  - ТаблицаЗначений				 - информация о расчетных счетах контрагетов.
//   НастройкаЗаполнения		  - ТаблицаЗначений				 - таблица со значениями по умолчанию для документов.
Процедура Загрузить(ДокументыКИмпорту, 
					Импорт_Заголовок, 
					Организация, 
					Импорт_РасчетныеСчета, 
					НастройкаЗаполнения) Экспорт
		
	НачалоИнтервалаЗагрузки = Дата("00010101");
	КонецИнтервалаЗагрузки  = Дата("00010101");
	           	
	Результат = ПолучитьДатуИзСтроки(НачалоИнтервалаЗагрузки, Импорт_Заголовок.ДатаНачала);
	
	СписокБанковскихСчетов = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = 
				НСтр("ru='В заголовке файла загрузки неверно указана дата начала интервала! Файл не может быть загружен!'");
		
		Сообщение.Сообщить();
				
		Возврат;
		
	КонецЕсли;
	
	Результат = ПолучитьДатуИзСтроки(КонецИнтервалаЗагрузки, Импорт_Заголовок.ДатаКонца);
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
			
		Сообщение = Новый СообщениеПользователю;
		
		Сообщение.Текст = НСтр("ru='В заголовке файла импорта неверно указана дата окончания интервала!'");
		
		Сообщение.Сообщить();
				
	КонецЕсли;
	
	// Загружаем отмеченные секции документов.
	Для Каждого СтрокаСекции Из ДокументыКИмпорту Цикл
		
		Если ЗначениеЗаполнено(СтрокаСекции.Документ)
			И ТипЗнч(СтрокаСекции.Документ) <> Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
			И ТипЗнч(СтрокаСекции.Документ) <> Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств") Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаСекции.Загружать Тогда
			
			РезультатПроверки = ПроверитьДокументДляЗагрузки(СтрокаСекции);
			
			Если ПустаяСтрока(РезультатПроверки) Тогда
				
				Если СписокБанковскихСчетов.НайтиПоЗначению(СтрокаСекции.СчетОрганизации) = Неопределено Тогда
					СписокБанковскихСчетов.Добавить(СтрокаСекции.СчетОрганизации);
				КонецЕсли;			
				
				Если ЗначениеЗаполнено(СтрокаСекции.СуммаСписано) Тогда
					ИмяДокумента = "СписаниеБезналичныхДенежныхСредств";
					
				ИначеЕсли ЗначениеЗаполнено(СтрокаСекции.СуммаПоступило) Тогда
					ИмяДокумента = "ПоступлениеБезналичныхДенежныхСредств";
				Иначе

					Если СокрЛП(СтрокаСекции.ПлательщикИНН) = СокрЛП(Организация.ИНН) Тогда
						ИмяДокумента                = "СписаниеБезналичныхДенежныхСредств";
						СтрокаСекции.Списано        = СтрокаСекции.ДатаДок;
						СтрокаСекции.ДатаПроведения = СтрокаСекции.Списано;
					Иначе
						ИмяДокумента                = "ПоступлениеБезналичныхДенежныхСредств";
						СтрокаСекции.Поступило      = СтрокаСекции.ДатаДок;
						СтрокаСекции.ДатаПроведения = СтрокаСекции.Поступило;
					КонецЕсли;
					
					Сообщение = Новый СообщениеПользователю;
						
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='В загружаемом файле не указана дата списания/поступления средств(реквизит ДатаСписано/ДатаПоступило) для платежного документа ""%1"" №%2 от %3.'"), 
								СтрокаСекции.Операция, СтрокаСекции.Номер, СтрокаСекции.ДатаДок);
						
					Сообщение.Сообщить();
				
				КонецЕсли;
								      				
				Если НЕ ЗначениеЗаполнено(СтрокаСекции.Документ) Тогда
					
					// В ИБ не найден документ, необходимо создать новый.					
									
					ОбъектДокумента = Документы[ИмяДокумента].СоздатьДокумент();
										
				Иначе
					
					// В ИБ найден документ, необходимо получить его объект.
					ОбъектДокумента = СтрокаСекции.Документ.ПолучитьОбъект();
					Если ОбъектДокумента = Неопределено Тогда
						ОбъектДокумента = Документы[ИмяДокумента].СоздатьДокумент();
					КонецЕсли;
				КонецЕсли;
				
				ЭтоНовыйДокумент = ОбъектДокумента.ЭтоНовый();
				
				Если ТипЗнч(ОбъектДокумента) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств") Тогда
					
					ЗаполнитьРеквизитыДокументаСписаниеБезналичныхДенежныхСредств(ОбъектДокумента, 
																					ОбъектДокумента.Метаданные().Имя, 
																					СтрокаСекции, 
																					ЭтоНовыйДокумент, 
																					Организация);
					
				ИначеЕсли ТипЗнч(ОбъектДокумента) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
					
					ЗаполнитьРеквизитыДокументаПоступлениеБезналичныхДенежныхСредств(ОбъектДокумента, 
																						ОбъектДокумента.Метаданные().Имя, 
																						СтрокаСекции, 
																						ЭтоНовыйДокумент, 
																						Организация);
				КонецЕсли;

				Если ОбъектДокумента.ПометкаУдаления Тогда
					ЗадатьПометкуНаУдаление(ОбъектДокумента, Ложь);
				КонецЕсли;
				
				ОбъектДокумента.ПроведеноБанком = Истина;
				Если ТипЗнч(ОбъектДокумента) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
					Или ТипЗнч(ОбъектДокумента) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
					ОбъектДокумента.ДатаПроведенияБанком = СтрокаСекции.ДатаПроведения;
				КонецЕсли;
				
				ЗаписатьОбъект(ОбъектДокумента, СтрокаСекции, ЭтоНовыйДокумент, НастройкаЗаполнения);
				
				Если НЕ ОбъектДокумента.ЭтоНовый() Тогда
					
					Если НЕ ЗначениеЗаполнено(СтрокаСекции.Документ) Тогда
						
						СтрокаСекции.Документ = ОбъектДокумента.Ссылка;
						СтрокаСекции.НомерДок = ОбъектДокумента["Номер"];
						
					КонецЕсли;
					
				КонецЕсли;
									
			Иначе
	
				Сообщение = Новый СообщениеПользователю;
								
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Платежный документ ""%1"" №%2 от %3 не может быть загружен:%4%5!'"), 
				СтрокаСекции.Операция, СтрокаСекции.Номер, СтрокаСекции.Дата, Символы.ПС, РезультатПроверки);
												
				Сообщение.Сообщить();
				
			КонецЕсли;
	
		КонецЕсли;

	КонецЦикла;
	
	// Составляем список счетов.
	СписокСчетов = Новый СписокЗначений;
	
	Для Каждого СтрокаСчета Из Импорт_РасчетныеСчета Цикл
		СписокСчетов.Добавить(СокрЛП(СтрокаСчета.РасчСчет));
	КонецЦикла;
	
	// Снимаем признак проведения банком для отсутствующих в выписке документов
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.ПроведеноБанком
	|	И ДанныеДокумента.Организация = &Организация
	|	И ДанныеДокумента.БанковскийСчет.НомерСчета В (&СписокСчетов)
	|	И ДанныеДокумента.ДатаПроведенияБанком МЕЖДУ &ДатаНач И &ДатаКон
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.ПроведеноБанком
	|	И ДанныеДокумента.Организация = &Организация
	|	И ДанныеДокумента.БанковскийСчет.НомерСчета В (&СписокСчетов)
	|	И ДанныеДокумента.ДатаПроведенияБанком МЕЖДУ &ДатаНач И &ДатаКон
	|";
	
	ДатаНачала = "";
	ДатаКонца = "";
	
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(ПолучитьДатуИзСтроки(ДатаНачала, Импорт_Заголовок.ДатаНачала)));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(ПолучитьДатуИзСтроки(ДатаКонца, Импорт_Заголовок.ДатаКонца)));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СписокСчетов", СписокСчетов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаВТаблицеИмпорта = ДокументыКИмпорту.НайтиСтроки(Новый Структура("Документ", Выборка.Ссылка));
		Если СтрокаВТаблицеИмпорта.Количество() = 0 Тогда
			СнятьПризнакПроведенияБанком(Выборка.Ссылка.ПолучитьОбъект());
		КонецЕсли;
	КонецЦикла;
	
	Сообщение = Новый СообщениеПользователю;
	
	Сообщение.Текст = НСтр("ru='Загрузка платежных документов в информационную базу завершена.'");
	
	Сообщение.Сообщить();
	
КонецПроцедуры

// Функция создает контрагента.
//
// Параметры:
//   СтрокаКонтрагент  - элемент дерева значений - данные о контрагенте.
Функция СоздатьКонтрагента(СтрокаКонтрагент = Неопределено) Экспорт
	
	СообщатьОСозданномКонтрагенте 	  = Ложь;
	СообщатьОСозданномБанковскомСчете = Ложь;
		
	// Эти элементы есть у всех справочников.
	Если НЕ ТипЗнч(СтрокаКонтрагент.Реквизит) = Тип("СправочникСсылка.Контрагенты") 
		И (НЕ ТипЗнч(СтрокаКонтрагент.Реквизит) = Тип("СправочникСсылка.ФизическиеЛица"))Тогда
		
		НовыйЭлемент = Справочники.Контрагенты.СоздатьЭлемент();
		
		НовыйЭлемент.Наименование       = СтрокаКонтрагент.Представление;
		НовыйЭлемент.НаименованиеПолное = СтрокаКонтрагент.Представление;
		НовыйЭлемент.ИНН                = СтрокаКонтрагент.ПолучитьЭлементы()[1].Значение;
		НовыйЭлемент.КПП                = СтрокаКонтрагент.ПолучитьЭлементы()[2].Значение;
		
		Если СтрДлина(НовыйЭлемент.ИНН) = 10 Тогда
			НовыйЭлемент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
		Иначе
			НовыйЭлемент.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
		КонецЕсли;
				
		НовыйЭлемент.Записать();
				
		СообщатьОСозданномКонтрагенте = Истина;
			
	Иначе
		
		НовыйЭлемент = СтрокаКонтрагент.Реквизит.ПолучитьОбъект();
		
	КонецЕсли;
	
	Если СообщатьОСозданномКонтрагенте Тогда
		
		Сообщение = Новый СообщениеПользователю;
				
		Сообщение.Текст 
			= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Контрагент (%1) создан.'"), 
																		СтрокаКонтрагент.Представление);
				
		Сообщение.Сообщить();
		
		ПредставлениеКонтрагента = "" + Символы.Таб + "- ";
		
	Иначе
		
		Если ТипЗнч(СтрокаКонтрагент.Реквизит) = Тип("СправочникСсылка.Контрагенты") Тогда 
			ПредставлениеКонтрагента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Контрагенту(%1)'"),
																							СтрокаКонтрагент.Представление);
		ИначеЕсли ТипЗнч(СтрокаКонтрагент.Реквизит) = Тип("СправочникСсылка.ФизическиеЛица")Тогда
			ПредставлениеКонтрагента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Физическому лицу(%1)'"),
																							СтрокаКонтрагент.Представление);																	
		КонецЕсли;
	КонецЕсли;

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Для Каждого Строка Из СтрокаКонтрагент.ПолучитьЭлементы() Цикл
			
		СообщатьОСозданномБанковскомСчете = Ложь;
				
		Если Строка.Представление = "Р/счет" Тогда
			
			Попытка
				
				НомерСчета = Строка.Значение;
				ВалютаСчета = ДенежныеСредстваСервер.ПолучитьВалютуПоНомеруСчета(НомерСчета);
				
				НовыйСчет = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
				
				НовыйСчет.НомерСчета            = НомерСчета;
				НовыйСчет.Владелец              = НовыйЭлемент.Ссылка;
				НовыйСчет.ВалютаДенежныхСредств = ?(ЗначениеЗаполнено(ВалютаСчета), 
													ВалютаСчета, ВалютаРегламентированногоУчета);
				                               				
				РССчет = Строка.ПолучитьЭлементы();
				
				Если НЕ НайтиЭлементДерева(РССчет, "Представление", "РЦ банка") = Неопределено Тогда
					
					// Банк контрагента.
					Если НЕ ПустаяСтрока(РССчет[0].Значение) Тогда
						
						НовыйБанк = НайтиБанк(РССчет[0].Значение, РССчет[1].Значение, "", РССчет[2].Значение);
						
						Если НовыйБанк = Справочники.КлассификаторБанковРФ.ПустаяСсылка() Тогда
											
							НовыйСчет.РучноеИзменениеРеквизитовБанка = Истина;
							НовыйСчет.НаименованиеБанка              = РССчет[0].Значение;
							НовыйСчет.ГородБанка					 = РССчет[1].Значение;
							НовыйСчет.КоррСчетБанка                  = РССчет[2].Значение;
							
						Иначе	
						
							НовыйСчет.Банк = НовыйБанк.Ссылка;
							
						КонецЕсли;
						
					КонецЕсли;
					
					// РЦ банка контрагента.
					Если НЕ ПустаяСтрока(РССчет[3].Значение) Тогда
						
						НовыйБанкКорр = НайтиБанк(РССчет[3].Значение, РССчет[4].Значение, РССчет[5].Значение, РССчет[6].Значение);
						
						Если НовыйБанкКорр = Справочники.КлассификаторБанковРФ.ПустаяСсылка() Тогда
														
							НовыйСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина;
							НовыйСчет.НаименованиеБанкаДляРасчетов              = РССчет[3].Значение;
							НовыйСчет.ГородБанкаДляРасчетов     				= РССчет[4].Значение;
							НовыйСчет.БИКБанкаДляРасчетов                       = РССчет[5].Значение;
							НовыйСчет.КоррСчетБанкаДляРасчетов                  = РССчет[6].Значение;
							
						Иначе	
							
							НовыйСчет.БанкДляРасчетов = НовыйБанкКорр.Ссылка;
							
						КонецЕсли;
						
					КонецЕсли;
					
				Иначе
					
					// Банк контрагента.
					НовыйБанк = НайтиБанк(РССчет[0].Значение, РССчет[1].Значение, РССчет[2].Значение, РССчет[3].Значение);
					
					Если НовыйБанк = Справочники.КлассификаторБанковРФ.ПустаяСсылка() Тогда
						
						НовыйБанк = Справочники.КлассификаторБанковРФ.СоздатьЭлемент();
						
						НовыйБанк.Наименование = РССчет[0].Значение;
						НовыйБанк.Город        = РССчет[1].Значение;
						НовыйБанк.Код          = РССчет[2].Значение;
						НовыйБанк.КоррСчет     = РССчет[3].Значение;
						
						НовыйБанк.Записать();
						
						НовыйСчет.Банк = НовыйБанк.Ссылка;
																
					Иначе	
						
						НовыйСчет.Банк = НовыйБанк.Ссылка;
						
					КонецЕсли;
					
				КонецЕсли;
								
				НовыйСчет.Наименование 
						= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Лев(НСтр("ru='%1, (Расчетный)'"), 100), 
																					РССчет[0].Значение);
				
				НовыйСчет.Записать();
				
				СообщатьОСозданномБанковскомСчете = Истина;
				
			Исключение
				
				Сообщение = Новый СообщениеПользователю;
				
				Сообщение.Текст = НСтр("ru='Не удалось создать банковский счет контрагента!'");
				
				Сообщение.Сообщить();
				
			КонецПопытки;
			
		КонецЕсли;
			
		Если СообщатьОСозданномБанковскомСчете Тогда
			
			Сообщение = Новый СообщениеПользователю;
						
			Сообщение.Текст 
					= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 добавлен банковский счет (%2).'"), 
																				ПредставлениеКонтрагента, 
																				НомерСчета);
						
			Сообщение.Сообщить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТипЗнч(НовыйЭлемент) = Тип("СправочникОбъект.Контрагенты") Тогда
		Если НЕ ЗначениеЗаполнено(НовыйЭлемент.Партнер) Тогда
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
				
				НовыйПартнер = Справочники.Партнеры.СоздатьЭлемент();
				НовыйПартнер.Клиент = Истина;
				НовыйПартнер.Поставщик = Истина;
				НовыйПартнер.ПрочиеОтношения = Истина;
				НовыйПартнер.ДатаРегистрации = ТекущаяДата();
				НовыйПартнер.Комментарий = НСтр("ru = 'Создан автоматически при импорте банковских документов'");
				НовыйПартнер.Наименование = СтрокаКонтрагент.Представление;
				НовыйПартнер.НаименованиеПолное = СтрокаКонтрагент.Представление;
				
				НовыйПартнер.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.Компания;
				
				НовыйПартнер.Записать();
				
				НовыйЭлемент.Партнер = НовыйПартнер.Ссылка;
				
			Иначе
				
				НовыйЭлемент.Партнер = Справочники.Партнеры.НеизвестныйПартнер;
				
			КонецЕсли;
			
		КонецЕсли;
    КонецЕсли;
		
	НовыйЭлемент.Записать();
	
	Возврат НовыйЭлемент.Ссылка;

КонецФункции

// Процедура заполнения типа платежного документа по хозяйственной операции.
//
// Параметры:
//	 Организация			  - СправочникСсылка.Организации						 - информация об организации загрузки.
//   БанковскийСчет			  - СправочникСсылка.БанковскиеСчетаОрганизаций			 - информация о банковском счете.
//   ВидОперации			  - ПеречислениеСсылка.ХозяйственныеОперации			 - вид операции обрабатываемого документа.
//	 СуммаДокумента			  - строка												 - информация о сумме обрабатываемого документа.
//   ТипПлатежногоДокумента	  - ОписаниеТипов										 - тип обрабатываемого документа.
//   ПлатежныйДокумент		  - ДокументСсылка.РасходныйКассовыйОрдер,
//								ДокументСсылка.ПриходныйКассовыйОрдер, 
//								ДокументСсылка.СписаниеБезналичныхДенежныхСредств, 
//								ДокументСсылка.ОтчетБанкаПоОперациямЭквайринга, 
//								ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств - ссылка на обрабатываемый документ.	
Процедура ЗаполнитьТипПлатежногоДокумента(Организация,
											БанковскийСчет,
											ВидОперации,
											СуммаДокумента,
											ТипПлатежногоДокумента,
											ДатаПроведения,
											ПлатежныйДокумент) Экспорт
	
	Если ВидОперации = Перечисления.ХозяйственныеОперации.СдачаДенежныхСредствВБанк Тогда
		ТипПлатежногоДокумента = Новый ОписаниеТипов("ДокументСсылка.РасходныйКассовыйОрдер");
		
	ИначеЕсли ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка Тогда
		ТипПлатежногоДокумента = Новый ОписаниеТипов("ДокументСсылка.ПриходныйКассовыйОрдер");
		
	Иначе
		ТипПлатежногоДокумента = Неопределено;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипПлатежногоДокумента) Тогда
		ПлатежныйДокумент = ПлатежныйДокументПоВидуОперации(
			ВидОперации,
			Организация,
			БанковскийСчет,
			СуммаДокумента,
			ДатаПроведения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Процедура формирует отчет о выгруженных в текущем сеансе работы платежных документах.
//
// Параметры:
//	 ПолеОтчета	 - ТабличныйДокумент			- отчет.
//   Источник	 - ТабличнаяЧасть			 	- таблица с данными документов.
//   Организация - СправочникСсылка.Организации - информация об организации выгрузки.
//   НачПериода  - Дата							– начало обрабатываемого периода.
//   КонПериода	 - Дата							- конец обрабатываемого периода.														  
Процедура ПечатьОтчетаОВыгруженныхПлатежныхДокументах(ПолеОтчета, 
														Источник, 
														Организация, 
														НачПериода, 
														КонПериода) Экспорт
		
	МакетОтчета = ПолучитьМакет("ОтчетОВыгруженныхПлатежныхДокументах");
	
	Шапка    = МакетОтчета.ПолучитьОбласть("Шапка");
	Строка   = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал   = МакетОтчета.ПолучитьОбласть("Подвал");
	НазвОрг  = МакетОтчета.ПолучитьОбласть("НазваниеОрганизации");
	
	НазвОрг.Параметры.НазваниеОрганизации = Организация.Наименование;
	ПолеОтчета.Вывести(НазвОрг);
	
	Шапка.Параметры.ИмяОтчета = НСтр("ru='Отчет о выгруженных документах'");
			
	ОбрБанковскийСчет = "";
    	
	Индекс      = 0;
	ИтогоСуммаП = 0;
	ИтогоСуммаР = 0;
	
	Для Каждого СтрокаИсточника Из Источник Цикл
        БанковскийСчет = СтрокаИсточника.Документ.БанковскийСчет;
		
		Если ОбрБанковскийСчет <>  БанковскийСчет Тогда
			
			Если ЗначениеЗаполнено(ОбрБанковскийСчет) Тогда
				Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;	
				Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;	
	
				ПолеОтчета.Вывести(Подвал);
                			
				Индекс      = 0;
				ИтогоСуммаП = 0;
				ИтогоСуммаР = 0;
			КонецЕсли;	
			
			ОбрБанковскийСчет = БанковскийСчет;
			
			НаимВалюты = БанковскийСчет.ВалютаДенежныхСредств.Наименование;
			НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
			НачалоОтчетногоПериода = Формат(НачПериода, "ДЛФ=D");
			КонецОтчетногоПериода = Формат(КонПериода, "ДЛФ=D");
			
			Шапка.Параметры.ОписаниеПериода	=
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (валюта: %2) с %3 по %4'"),
							СокрЛП(БанковскийСчет.НомерСчета),
							НаимВалюты,
			                НачалоОтчетногоПериода,
			                КонецОтчетногоПериода);
			ПолеОтчета.Вывести(Шапка);
            			
		КонецЕсли;	
		
		Если СтрокаИсточника.Готовность <> -1 Тогда
			Продолжить;
		КонецЕсли;
		
		Строка.Параметры.Плательщик = СтрокаИсточника.ОрганизацияНаим;
		
		НаимВалюты = СтрокаИсточника.Документ.БанковскийСчет.ВалютаДенежныхСредств.Наименование;
		НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
                                                                                                             		
		Строка.Параметры.ПлательщикСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (валюта:%2'"),
		                                             СтрокаИсточника.ОрганизацияРасчСчет,
													 НаимВалюты);
		
		Строка.Параметры.Получатель     = СтрокаИсточника.Контрагент;
				
		Если ЗначениеЗаполнено(СтрокаИсточника.Документ.БанковскийСчетПолучатель) Тогда
			НаимВалюты = СтрокаИсточника.Документ.БанковскийСчетПолучатель.ВалютаДенежныхСредств.Наименование;
			НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
		ИначеЕсли ЗначениеЗаполнено(СтрокаИсточника.Документ.БанковскийСчетКонтрагента) Тогда
			НаимВалюты = СтрокаИсточника.Документ.БанковскийСчетКонтрагента.ВалютаДенежныхСредств.Наименование;
			НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
		Иначе
			НаимВалюты = "";
		КонецЕсли;
				
		Строка.Параметры.ПолучательСчет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (валюта:%2'"),
		                                             СтрокаИсточника.КонтрагентРасчСчет,
													 НаимВалюты);
        		
		Строка.Параметры.СуммаСписание  = СтрокаИсточника.СуммаДокумента;
		ИтогоСуммаР = ИтогоСуммаР + СтрокаИсточника.СуммаДокумента;
		
		Строка.Параметры.Документ = СтрокаИсточника.Документ;
		Индекс = Индекс + 1;
		Строка.Параметры.Индекс = Индекс;
		
		ПолеОтчета.Вывести(Строка);
		
	КонецЦикла;
	
	Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;	
	Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;	
	
	ПолеОтчета.Вывести(Подвал);
	
	ПолеОтчета.ОтображатьГруппировки = Ложь;
	ПолеОтчета.ОтображатьЗаголовки   = Ложь;
	ПолеОтчета.ОтображатьСетку 		 = Ложь;
	ПолеОтчета.ТолькоПросмотр 		 = Истина;
	
КонецПроцедуры

// Процедура формирует отчет о загруженных в текущем сеансе данных.
//
// Параметры:
//	 ПолеОтчета			   - ТабличныйДокумент			  - информация об организации загрузки.
//   Источник			   - ТабличнаяЧасть			 	  - таблица с данными документов.
//   Организация		   - СправочникСсылка.Организации - информация об организации загрузки.
//   Импорт_РасчетныеСчета - ТаблицаЗначений			  - информация о расчетных счетах контрагетов.
//   Импорт_Заголовок	   - Строка						  - данные для заполнения заголовка файла импорта.
Процедура ПечатьОтчетаОЗагруженныхПлатежныхДокументах(ПолеОтчета, 
														Источник, 
														Организация, 
														Импорт_РасчетныеСчета, 
														Импорт_Заголовок) Экспорт
		
	ИмеетсяСекцияРасчСчет = Ложь;
	
	МакетОтчета = ПолучитьМакет("ОтчетОЗагруженныхПлатежныхДокументах");
	
	Шапка   = МакетОтчета.ПолучитьОбласть("Шапка");
	Строка  = МакетОтчета.ПолучитьОбласть("Строка");
	Подвал  = МакетОтчета.ПолучитьОбласть("Подвал");
	Остатки = МакетОтчета.ПолучитьОбласть("Остатки");
	НазвОрг = МакетОтчета.ПолучитьОбласть("НазваниеОрганизации");
	
	НазвОрг.Параметры.НазваниеОрганизации = Организация.Наименование;
	ПолеОтчета.Вывести(НазвОрг);
	
	Для Каждого СтрокаСчета Из Импорт_РасчетныеСчета Цикл
		
		Если НЕ ПустаяСтрока(СтрокаСчета.ДатаНачала) Тогда
			
			Индекс                = 0;
			ИтогоСуммаП           = 0;
			ИтогоСуммаР           = 0;
			ИмеетсяСекцияРасчСчет = Истина;
			
			НаимВалюты = ПоискБанковскогоСчета(СтрокаСчета.РасчСчет).ВалютаДенежныхСредств.Наименование;
			НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
			
			Шапка.Параметры.ИмяОтчета 		= НСтр("ru='Отчет о загруженных документах'");
			Шапка.Параметры.ОписаниеПериода	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (валюта:%2 ) с %3 по %4'"),
		                                             СтрокаСчета.РасчСчет,
													 НаимВалюты,
													 СтрокаСчета.ДатаНачала,
													 СтрокаСчета.ДатаКонца);
            		
			ПолеОтчета.Вывести(Шапка);
			
			Отбор = Новый Структура;
			Отбор.Вставить("СчетОрганизации", ПоискБанковскогоСчета(СтрокаСчета.РасчСчет));
			ОтборПоСчету = Источник.НайтиСтроки(Отбор);
			
			ДатаНачала = ПолучитьДатуИзСтроки(ДатаНачала, СтрокаСчета.ДатаНачала);
						
			Если ЗначениеЗаполнено(СтрокаСчета.ДатаКонца) Тогда
				ДатаКонца = ПолучитьДатуИзСтроки(ДатаКонца, СтрокаСчета.ДатаКонца);
			Иначе
				ДатаКонца = '21000101';
			КонецЕсли;
			
			Для Каждого СтрокаИсточника Из Источник Цикл
				
				Если (СтрокаИсточника.ПлательщикРасчСчет <> СтрокаСчета.РасчСчет) 
					и (СтрокаИсточника.ПолучательРасчСчет <> СтрокаСчета.РасчСчет) Тогда
   		    		Продолжить;
				КонецЕсли;
                				
				Если ТипЗнч(СтрокаИсточника.СчетКонтрагента) = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов")
				 ИЛИ ТипЗнч(СтрокаИсточника.СчетКонтрагента) = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций") Тогда		
					НаимВалюты = СтрокаИсточника.СчетКонтрагента.ВалютаДенежныхСредств.Наименование;
					НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
				Иначе
					НаимВалюты = "";
				КонецЕсли;
				
				Если СтрокаИсточника.СуммаПоступило > 0 
				   И СтрокаИсточника.Поступило >= ДатаНачала И СтрокаИсточника.Поступило <= ДатаКонца Тогда
					
					Строка.Параметры.Контрагент      
							= ?(ПустаяСтрока(СтрокаИсточника.Плательщик1), СтрокаИсточника.Плательщик, СтрокаИсточника.Плательщик1);
					Строка.Параметры.Счет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (валюта:%2 )'"),
		                                             СтрокаИсточника.ПлательщикСчет,
													 НаимВалюты);
 					Строка.Параметры.СуммаПоступление = СтрокаИсточника.СуммаПоступило;
					Строка.Параметры.СуммаСписание    = "";
					Строка.Параметры.Дата             = СтрокаИсточника.Дата;
					ИтогоСуммаП                       = ИтогоСуммаП + СтрокаИсточника.СуммаПоступило;
					
				ИначеЕсли СтрокаИсточника.СуммаСписано > 0 
					И СтрокаИсточника.Списано >= ДатаНачала И СтрокаИсточника.Списано <= ДатаКонца Тогда

					Строка.Параметры.Контрагент      
							= ?(ПустаяСтрока(СтрокаИсточника.Получатель1), СтрокаИсточника.Получатель, СтрокаИсточника.Получатель1);
					Строка.Параметры.Счет = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (валюта:%2 )'"),
		                                             СтрокаИсточника.ПолучательСчет,
													 НаимВалюты);
		       		Строка.Параметры.СуммаСписание    = СтрокаИсточника.СуммаСписано;
					Строка.Параметры.СуммаПоступление = "";
					Строка.Параметры.Дата             = СтрокаИсточника.Дата;
					ИтогоСуммаР                       = ИтогоСуммаР + СтрокаИсточника.СуммаСписано;	
	
				Иначе
					Продолжить;
				КонецЕсли;
				
				Строка.Параметры.Номер             = СтрокаИсточника.Номер;
				Строка.Параметры.НазначениеПлатежа = СтрокаИсточника.НазначениеПлатежа;
				Индекс = Индекс + 1;
				
				Док = СтрокаИсточника.Документ;
				
				Если ЗначениеЗаполнено(Док) Тогда
					
					Строка.Параметры.Документ     = Док;
					Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
					
					Если ТипЗнч(Док) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
					 ИЛИ ТипЗнч(Док) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
						Строка.Параметры.ТипДокумента = СтрокаИсточника.ВидОперации; 
						
					ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ОтчетБанкаПоОперациямЭквайринга") Тогда
						Строка.Параметры.ТипДокумента = СтрокаИсточника.Операция;
						
					Иначе
						Строка.Параметры.ТипДокумента = Док.ТипПлатежногоДокумента;
					КонецЕсли;
					
				Иначе
					
					Строка.Параметры.Документ     = НСтр("ru='НЕ ЗАГРУЖЕН'");
					Строка.Параметры.ТипДокумента = СтрокаИсточника.Операция;
					
					Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
					
				КонецЕсли;
				
				Строка.Параметры.Индекс = Индекс;
				
				ПолеОтчета.Вывести(Строка);
				
			КонецЦикла;
			
			Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;	
			Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;	
						
			ПолеОтчета.Вывести(Подвал);
			
			Остатки.Параметры.Текст1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Остаток на начало %1 :'"),
		                                             СтрокаСчета.ДатаНачала);
            Остатки.Параметры.Текст2 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Остаток на конец %1 :'"),
		                                             СтрокаСчета.ДатаКонца);
 			
			Если НЕ ПустаяСтрока(СтрокаСчета.НачальныйОстаток) 
					И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрокаСчета.НачальныйОстаток, ".", ""),"-",""),",","")) Тогда
				Остатки.Параметры.Сумма1 = Формат(Число(СтрокаСчета.НачальныйОстаток), "ЧЦ=17; ЧДЦ=2; ЧРД=.; ЧРГ=,");
			Иначе
				Остатки.Параметры.Сумма1 = СтрокаСчета.НачальныйОстаток;
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(СтрокаСчета.КонечныйОстаток) 
					И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(СтрЗаменить(СтрЗаменить(СтрокаСчета.КонечныйОстаток, ".", ""),"-",""),",","")) Тогда
				Остатки.Параметры.Сумма2 = Формат(Число(СтрокаСчета.КонечныйОстаток), "ЧЦ=17; ЧДЦ=2; ЧРД=.; ЧРГ=,");
			Иначе
				Остатки.Параметры.Сумма2 = СтрокаСчета.КонечныйОстаток;
			КонецЕсли;
			
			ПолеОтчета.Вывести(Остатки);
			
		КонецЕсли;

	КонецЦикла;

	Если НЕ ИмеетсяСекцияРасчСчет Тогда
		
		Для Каждого СтрокаСчета Из Импорт_РасчетныеСчета Цикл
			
			Индекс      = 0;
			ИтогоСуммаП = 0;
			ИтогоСуммаР = 0;
			
			НаимВалюты = ПоискБанковскогоСчета(СтрокаСчета.РасчСчет).ВалютаДенежныхСредств.Наименование;
			
			Шапка.Параметры.ИмяОтчета 		= НСтр("ru='Отчет о загруженных документах'");
			Шапка.Параметры.ОписаниеПериода	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='по счету %1 (валюта:%2 ) с %3 по %4'"),
		                                             СтрокаСчета.РасчСчет,
													 НаимВалюты,
													 Импорт_Заголовок.ДатаНачала,
													 Импорт_Заголовок.ДатаКонца);

			ПолеОтчета.Вывести(Шапка);
			
			Отбор = Новый Структура;                    
			Отбор.Вставить("СчетОрганизации", ПоискБанковскогоСчета(СтрокаСчета.РасчСчет));
			ОтборПоСчету = Источник.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаИсточника Из Источник Цикл
				
				Если (СтрокаИсточника.ПлательщикРасчСчет <> СтрокаСчета.РасчСчет) 
					и (СтрокаИсточника.ПолучательРасчСчет <> СтрокаСчета.РасчСчет) Тогда
   		    		Продолжить;
				КонецЕсли;
				
				Если ТипЗнч(СтрокаИсточника.СчетКонтрагента) = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов")
				 ИЛИ ТипЗнч(СтрокаИсточника.СчетКонтрагента) = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций") Тогда		
					НаимВалюты = СтрокаИсточника.СчетКонтрагента.ВалютаДенежныхСредств.Наименование;
					НаимВалюты = ?(ЗначениеЗаполнено(НаимВалюты), НаимВалюты, НСтр("ru='не указана'"));
				Иначе
					НаимВалюты = "";
				КонецЕсли;
								
				Если СтрокаИсточника.СуммаПоступило > 0 Тогда
					
					Строка.Параметры.Контрагент       = ?(ПустаяСтрока(СтрокаИсточника.Плательщик1), 
															СтрокаИсточника.Плательщик, СтрокаИсточника.Плательщик1);
					Строка.Параметры.Счет 			  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (валюта:%2 )'"),
		                                             		СтрокаИсточника.ПлательщикСчет,
															НаимВалюты);
					Строка.Параметры.СуммаПоступление = СтрокаИсточника.СуммаПоступило;
					Строка.Параметры.СуммаСписание    = "";
					Строка.Параметры.Дата             = СтрокаИсточника.Дата;
					ИтогоСуммаП                       = ИтогоСуммаП + СтрокаИсточника.СуммаПоступило;
					
				ИначеЕсли СтрокаИсточника.СуммаСписано > 0  Тогда
					
					Строка.Параметры.Контрагент       = ?(ПустаяСтрока(СтрокаИсточника.Получатель1),
															СтрокаИсточника.Получатель,СтрокаИсточника.Получатель1);
					Строка.Параметры.Счет			  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (валюта:%2 )'"),
		                                             		СтрокаИсточника.ПолучательСчет,
													 		НаимВалюты);
 					Строка.Параметры.СуммаСписание    = СтрокаИсточника.СуммаСписано;
					Строка.Параметры.СуммаПоступление = "";
					Строка.Параметры.Дата             = СтрокаИсточника.Дата;
					ИтогоСуммаР                       = ИтогоСуммаР + СтрокаИсточника.СуммаСписано;
					
				Иначе
					
					Продолжить;
					
				КонецЕсли;
												
				Строка.Параметры.Номер = СтрокаИсточника.Номер;
				Строка.Параметры.НазначениеПлатежа = СтрокаИсточника.НазначениеПлатежа;
				Индекс = Индекс + 1;
				
				Док = СтрокаИсточника.Документ;
				
				Если ЗначениеЗаполнено(Док) Тогда
					
					Строка.Параметры.Документ     = Док;
					Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
					
					Если ТипЗнч(Док) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
					 ИЛИ ТипЗнч(Док) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
						Строка.Параметры.ТипДокумента = СтрокаИсточника.ВидОперации; 
						
					ИначеЕсли ТипЗнч(Док) = Тип("ДокументСсылка.ОтчетБанкаПоОперациямЭквайринга") Тогда
						Строка.Параметры.ТипДокумента = СтрокаИсточника.Операция;
						
					Иначе
						Строка.Параметры.ТипДокумента = Док.ТипПлатежногоДокумента;
					КонецЕсли;
					
				Иначе
					
					Строка.Параметры.Документ     = НСтр("ru='НЕ ЗАГРУЖЕН'");
					Строка.Параметры.ТипДокумента = СтрокаИсточника.Операция;
					
					Строка.Области.Строка.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
					
				КонецЕсли;
				
				Строка.Параметры.Индекс = Индекс;
				
				ПолеОтчета.Вывести(Строка);
				
			КонецЦикла;
			
			Подвал.Параметры.ИтогоСуммаП = ИтогоСуммаП;	
			Подвал.Параметры.ИтогоСуммаР = ИтогоСуммаР;	
						
			ПолеОтчета.Вывести(Подвал);
			
			Остатки.Параметры.Текст1 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Остаток на начало %1 :'"),
		                                             Импорт_Заголовок.ДатаНачала);
            Остатки.Параметры.Текст2 = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Остаток на конец %1 :'"),
		                                             Импорт_Заголовок.ДатаКонца);
 			Остатки.Параметры.Сумма1 = "";
			Остатки.Параметры.Сумма2 = "";
			ПолеОтчета.Вывести(Остатки);
			
		КонецЦикла;
	
	КонецЕсли;
	
	ПолеОтчета.ОтображатьГруппировки = Ложь;
	ПолеОтчета.ОтображатьЗаголовки   = Ложь;
	ПолеОтчета.ОтображатьСетку       = Ложь;
	ПолеОтчета.ТолькоПросмотр        = Истина;
		
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ПроверитьНаКорректностьИПустоеЗначениеЭкспорта(СтрокаДокумента, КоллекцияЭлементов)
	
	НепрямыеРасчетыУПлательщика = ЗначениеЗаполнено(СтрокаДокумента.ОрганизацияБанкДляРасчетов);
	НепрямыеРасчетыУПолучателя  = ЗначениеЗаполнено(СтрокаДокумента.КонтрагентБанкДляРасчетов);
	
	Плательщик = "Организация";
	Получатель = "Контрагент";
	
	РеквизитыПлДокЭкс_Основные      = "Номер,Дата,СуммаДокумента";
	РеквизитыПлДокЭкс_Плательщик    = Плательщик + "РасчСчет," + Плательщик + "Наим," + Плательщик + "ИНН";      
	РеквизитыПлДокЭкс_ПлательщикНПР = Плательщик + "РасчСчет," 
										+ Плательщик + "БанкДляРасчетов," 
										+ Плательщик + "ГородБанка," 
										+ Плательщик + "БИКРЦБанка";
	РеквизитыПлДокЭкс_Получатель    = Получатель + "РасчСчет," + Получатель + "ИНН";
	РеквизитыПлДокЭкс_ПолучательНПР = Получатель + "РасчСчет," 
										+ Получатель + "БанкДляРасчетов," 
										+ Получатель + "ГородБанка," 
										+ Получатель + "БИКРЦБанка";

	Экспорт_НеПустые = СоздатьСоответствиеИзСтроки(
	РеквизитыПлДокЭкс_Основные   + "," +
	РеквизитыПлДокЭкс_Плательщик + "," +
	?(НепрямыеРасчетыУПлательщика, РеквизитыПлДокЭкс_ПлательщикНПР + ",", "")+
	РеквизитыПлДокЭкс_Получатель + "," +
	?(НепрямыеРасчетыУПолучателя, РеквизитыПлДокЭкс_ПолучательНПР + ",", ""));
	
	Для Каждого Свойство Из Экспорт_НеПустые Цикл
		ПроверитьНаПустоеЗначениеЭкспорта(СтрокаДокумента, 
											Свойство.Ключ, 
											КоллекцияЭлементов.Найти(Свойство.Ключ).Заголовок);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьНаПустоеЗначениеЭкспорта(СтрокаЭкспорта, ИмяСвойства, ПредставлениеСвойства)
	
	Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта[ИмяСвойства]) Тогда
		Стр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не заполнен реквизит ""%1""!'"), 
																		ПредставлениеСвойства);
		ДобавитьЗамечание(СтрокаЭкспорта, 3, Стр);
		УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗамечание(СтруктураДокумента, НоваяГотовность, ТекстЗамечания)
	
	УстановитьГотовность(СтруктураДокумента.Готовность, НоваяГотовность);
	ДобавитьВСтроку(СтруктураДокумента.ОписаниеОшибок, ТекстЗамечания);
	
КонецПроцедуры

Процедура УстановитьГотовность(ТекущаяГотовность, НоваяГотовность)

	Если ТекущаяГотовность < НоваяГотовность Тогда
		ТекущаяГотовность = НоваяГотовность;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьВСтроку(Буфер, НоваяСтрока)
	
	Если ПустаяСтрока(Буфер) Тогда
		
		Буфер = НоваяСтрока;
		
	Иначе
		
		Буфер = Буфер + Символы.ПС + НоваяСтрока;
		
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьСоответствиеИзСтроки(Знач СтрокаЧерезЗапятую)
	
	НовоеСоответствие = Новый Соответствие;

	ПозицияРазделителя = СтрНайти(СтрокаЧерезЗапятую, ",");
	
	Пока ПозицияРазделителя > 0 Цикл
		
		ИмяИтема = Лев(СтрокаЧерезЗапятую, ПозицияРазделителя - 1);
		НовоеСоответствие.Вставить(ИмяИтема, Истина);
		СтрокаЧерезЗапятую = Сред(СтрокаЧерезЗапятую, ПозицияРазделителя + 1);
		ПозицияРазделителя = СтрНайти(СтрокаЧерезЗапятую, ",");
		
	КонецЦикла;
	
	Если СтрДлина(СтрокаЧерезЗапятую) > 0 Тогда
		
		НовоеСоответствие.Вставить(СтрокаЧерезЗапятую, Истина);
		
	КонецЕсли;
		
	Возврат НовоеСоответствие;
	
КонецФункции

Функция ПривестиЗначение(Значение)
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		
		Возврат СокрЛП(Значение);
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
		
		Возврат Формат(Значение, "ЧРД=.; ЧРГ=' '; ЧГ=0");
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		
		Возврат Формат(Значение, "ДФ=dd.MM.yyyy");
		
	Иначе
		
		Возврат "";
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСекциюДокумент(СтруктураДокумента, Организация, ВыгружатьКПП, ВерсияФормата)
	
	Буфер    = "";
	Реквизит = "";
	СтуртураДляЗаписи = СформироватьСтруктуруЭкспорта(ВерсияФормата);
	ЗаполнитьЗначениеЭкспорта(СтуртураДляЗаписи, СтруктураДокумента, Организация, ВыгружатьКПП);
	
	ДобавитьВСтроку(Буфер, "СекцияДокумент=" + СтруктураДокумента.ВидДокумента);
	
	Для Каждого ВыгружаемыйРеквизит Из СтуртураДляЗаписи Цикл
		
		Значение = ПривестиЗначение(ВыгружаемыйРеквизит.Значение);
				
		Если НЕ ПустаяСтрока(Значение) Тогда
			ДобавитьВСтроку(Буфер, ВыгружаемыйРеквизит.Ключ + "=" + Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ДобавитьВСтроку(Буфер, "КонецДокумента");
	
	Возврат Буфер;
	
КонецФункции

Функция СформироватьСтруктуруЭкспорта(ВерсияФормата)

	СтруктураЭкспорта = Новый Структура;
	
	СтруктураЭкспорта.Вставить( "Выгружать",             ""); // "_"
	СтруктураЭкспорта.Вставить( "Готовность",            ""); // "Готовность"
	
	СтруктураЭкспорта.Вставить( "НомерДок",              ""); // "№ Документа"
	СтруктураЭкспорта.Вставить( "Номер",                 ""); // "Номер"
	СтруктураЭкспорта.Вставить( "Дата",                  ""); // "Дата"
	СтруктураЭкспорта.Вставить( "Операция",              ""); // "Операция"
	СтруктураЭкспорта.Вставить( "СчетОрганизации",       ""); // "Эл. Р/с организации"
	СтруктураЭкспорта.Вставить( "НомерСчетаОрганизации", ""); // "Р/с организации"
	СтруктураЭкспорта.Вставить( "Сумма",                 ""); // "Сумма"
	
	СтруктураЭкспорта.Вставить( "Контрагент",            ""); // "Контрагент"
	СтруктураЭкспорта.Вставить( "СчетКонтрагента",       ""); // "Р/с контрагента"
	СтруктураЭкспорта.Вставить( "НомерСчетаКонтрагента", ""); // "Эл. Р/с контрагента"
	
	СтруктураЭкспорта.Вставить( "КвитанцияДата",         ""); // "Дата форм. квитанции"
	СтруктураЭкспорта.Вставить( "КвитанцияВремя",        ""); // "Время форм. квитанции"
	СтруктураЭкспорта.Вставить( "КвитанцияСодержание",   ""); // "Содержание квитанции"
	
	СтруктураЭкспорта.Вставить( "ПлательщикСчет",        ""); // "Расчетный счет плательщика"
	СтруктураЭкспорта.Вставить( "Плательщик",            ""); // "Плательщик"
	СтруктураЭкспорта.Вставить( "ПлательщикИНН",         ""); // "ИНН плательщика"
	СтруктураЭкспорта.Вставить( "Плательщик1",           ""); // "Наименование плательщика"
	СтруктураЭкспорта.Вставить( "Плательщик2",           ""); // "Расчетный счет плательщика"
	СтруктураЭкспорта.Вставить( "Плательщик3",           ""); // "Банк плательщика"
	СтруктураЭкспорта.Вставить( "Плательщик4",           ""); // "Город банка плательщика"
	СтруктураЭкспорта.Вставить( "ПлательщикРасчСчет",    ""); // "Корсчет банка плательщика"
	СтруктураЭкспорта.Вставить( "ПлательщикБанк1",       ""); // "РЦ банка плательщика"
	СтруктураЭкспорта.Вставить( "ПлательщикБанк2",       ""); // "Город РЦ банка плательщика"
	СтруктураЭкспорта.Вставить( "ПлательщикБИК",         ""); // "БИК РЦ банка плательщика"
	СтруктураЭкспорта.Вставить( "ПлательщикКорсчет",     ""); // "Корсчет РЦ банка плат."
	
	СтруктураЭкспорта.Вставить( "ПолучательСчет",        ""); // "Расчетный счет получателя"
	СтруктураЭкспорта.Вставить( "Получатель",            ""); // "Получатель"
	СтруктураЭкспорта.Вставить( "ПолучательИНН",         ""); // "ИНН получателя"
	СтруктураЭкспорта.Вставить( "Получатель1",           ""); // "Наименование получателя"
	СтруктураЭкспорта.Вставить( "Получатель2",           ""); // "Расчетный счет получателя"
	СтруктураЭкспорта.Вставить( "Получатель3",           ""); // "Банк получателя"
	СтруктураЭкспорта.Вставить( "Получатель4",           ""); // "Город банка получателя"
	СтруктураЭкспорта.Вставить( "ПолучательРасчСчет",    ""); // "Корсчет банка получателя"
	СтруктураЭкспорта.Вставить( "ПолучательБанк1",       ""); // "РЦ банка получателя"
	СтруктураЭкспорта.Вставить( "ПолучательБанк2",       ""); // "Город РЦ банка получателя"
	СтруктураЭкспорта.Вставить( "ПолучательБИК",         ""); // "БИК РЦ банка получателя"
	СтруктураЭкспорта.Вставить( "ПолучательКорсчет",     ""); // "Корсчет РЦ банка пол."
	
	СтруктураЭкспорта.Вставить( "ВидПлатежа",            ""); // "Вид платежа"
	СтруктураЭкспорта.Вставить( "ВидОплаты",             ""); // "Вид оплаты"
	
	Если ВерсияФормата = "1.02" Тогда
		СтруктураЭкспорта.Вставить("Код", ""); // "Уникальный идентификатор платежа"
	КонецЕсли;
	
	СтруктураЭкспорта.Вставить( "СтатусСоставителя",     ""); // "Статус составителя"
	СтруктураЭкспорта.Вставить( "ПлательщикКПП",         ""); // "КПП плательщика"
	СтруктураЭкспорта.Вставить( "ПолучательКПП",         ""); // "КПП получателя"
	СтруктураЭкспорта.Вставить( "ПоказательКБК",         ""); // "Показатель КБК"
	СтруктураЭкспорта.Вставить( "ОКАТО",                 ""); // "ОКАТО"
	СтруктураЭкспорта.Вставить( "ПоказательОснования",   ""); // "Пок. основания"
	СтруктураЭкспорта.Вставить( "ПоказательПериода",     ""); // "Пок. нал. периода"
	СтруктураЭкспорта.Вставить( "ПоказательНомера",      ""); // "Пок. номера док."
	СтруктураЭкспорта.Вставить( "ПоказательДаты",        ""); // "Пок. даты док."
	СтруктураЭкспорта.Вставить( "ПоказательТипа",        ""); // "Пок. типа платежа"
	
	СтруктураЭкспорта.Вставить( "СрокПлатежа",           ""); // "Срок действия аккредитива"
	СтруктураЭкспорта.Вставить( "Очередность",           ""); // "Очередность платежа"
	
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа",     ""); // "Назначение платежа"
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа1",    ""); // "Назначение платежа, стр. 1"
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа2",    ""); // "Назначение платежа, стр. 2"
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа3",    ""); // "Назначение платежа, стр. 3"
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа4",    ""); // "Назначение платежа, стр. 4"
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа5",    ""); // "Назначение платежа, стр. 5"
	СтруктураЭкспорта.Вставить( "НазначениеПлатежа6",    ""); // "Назначение платежа, стр. 6"
	
	СтруктураЭкспорта.Вставить( "СрокАкцепта",           ""); // "Срок акцепта"
	СтруктураЭкспорта.Вставить( "ВидАккредитива",        ""); // "Вид аккредитива"
	СтруктураЭкспорта.Вставить( "УсловиеОплаты1",        ""); // "Условие оплаты, стр. 1"
	СтруктураЭкспорта.Вставить( "УсловиеОплаты2",        ""); // "Условие оплаты, стр. 2"
	СтруктураЭкспорта.Вставить( "УсловиеОплаты3",        ""); // "Условие оплаты, стр. 3"
	СтруктураЭкспорта.Вставить( "ПлатежПоПредст",        ""); // "Платеж по представлению"
	СтруктураЭкспорта.Вставить( "ДополнУсловия",         ""); // "Дополнительные условия"
	СтруктураЭкспорта.Вставить( "НомерСчетаПоставщика",  ""); // "№ счета поставщика"
	СтруктураЭкспорта.Вставить( "ДатаОтсылкиДок",        ""); // "Дата отсылки документов"
	
	СтруктураЭкспорта.Вставить( "Документ",              ""); // "Источник"
	СтруктураЭкспорта.Вставить( "СекцияДокумент",        ""); // "Выгрузка"
	
	СтруктураЭкспорта.Вставить( "ОписаниеОшибок",        ""); // "Замечания"
	СтруктураЭкспорта.Вставить( "ТипДокумента",          ""); // "Тип пл. документа"
	
	Возврат СтруктураЭкспорта;
	
КонецФункции

Процедура ЗаполнитьЗначениеЭкспорта(СтрокаЭкспорта, ВыборкаДляЭкспорта, Организация, ВыгружатьКПП)
	
	Плательщик = "Организация";
	Получатель = "Контрагент";
		
	СтрокаЭкспорта.Номер =	ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(
		ВыборкаДляЭкспорта.Номер,
		Истина, // УдалитьПрефиксИнформационнойБазы
		Истина); // УдалитьПользовательскийПрефикс
	СтрокаЭкспорта.Дата           =	Формат(ВыборкаДляЭкспорта.Дата, "ДЛФ=Д");
	СтрокаЭкспорта.Сумма          =	Формат(ВыборкаДляЭкспорта.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧГ=0");
	СтрокаЭкспорта.ПлательщикСчет =	ВыборкаДляЭкспорта[Плательщик + "РасчСчет"];
	СтрокаЭкспорта.ПолучательСчет =	ВыборкаДляЭкспорта[Получатель + "РасчСчет"];
	СтрокаЭкспорта.ВидПлатежа 	  =	ВыборкаДляЭкспорта.ВидПлатежа;
	
	НепрямыеРасчетыУПлательщика = ЗначениеЗаполнено(ВыборкаДляЭкспорта[Плательщик + "БанкДляРасчетов"]);
	НепрямыеРасчетыУПолучателя  = ЗначениеЗаполнено(ВыборкаДляЭкспорта[Получатель + "БанкДляРасчетов"]);

	// ВидОплаты
	//
	Если ВыборкаДляЭкспорта.ВидДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение Тогда
		СтрокаЭкспорта.ВидОплаты =  "01";
		
	ИначеЕсли ВыборкаДляЭкспорта.ВидДокумента = Перечисления.ТипыПлатежныхДокументов.Аккредитив Тогда
		СтрокаЭкспорта.ВидОплаты =  "08";
		
	ИначеЕсли ВыборкаДляЭкспорта.ВидДокумента = Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение Тогда
		СтрокаЭкспорта.ВидОплаты =  "06";
		
	ИначеЕсли ВыборкаДляЭкспорта.ВидДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование Тогда
		СтрокаЭкспорта.ВидОплаты =  "02";
		
	Иначе
		СтрокаЭкспорта.ВидОплаты = "00";
		
	КонецЕсли;
	
	// Код - идентификатор платежа
	//
	СтрокаЭкспорта.Код = ВыборкаДляЭкспорта.Код;
		
	// Плательщик1
	//
	СтрокаЭкспорта.Плательщик1 = ВыборкаДляЭкспорта[Плательщик + "Наим"];
	
	// Плательщик, ПлательщикИНН
	//
	СтрокаЭкспорта.ПлательщикИНН = ВыборкаДляЭкспорта[Плательщик + "ИНН"];
	СтрокаЭкспорта.Плательщик = "ИНН " + ВыборкаДляЭкспорта[Плательщик + "ИНН"] + " " + СтрокаЭкспорта.Плательщик1; 
	
	// ПлательщикРасчСчет, 
	// ПлательщикБанк1, ПлательщикБанк2, ПлательщикБИК, ПлательщикКорсчет,
	// Плательщик2, Плательщик3, Плательщик4
	//
	Если НепрямыеРасчетыУПлательщика Тогда
		СтрокаЭкспорта.Плательщик2 		  = ВыборкаДляЭкспорта[Плательщик + "РасчСчет"];
		СтрокаЭкспорта.Плательщик3 		  = ВыборкаДляЭкспорта[Плательщик + "Банк"];
		СтрокаЭкспорта.Плательщик4 		  = ВыборкаДляЭкспорта[Плательщик + "ГородБанка"];
		СтрокаЭкспорта.Плательщик 		  = СтрокаЭкспорта.Плательщик 
											+ " р/с " + СтрокаЭкспорта.Плательщик2 + " в " 
											+ СтрокаЭкспорта.Плательщик3 + " " + СтрокаЭкспорта.Плательщик4;
		СтрокаЭкспорта.ПлательщикРасчСчет = ВыборкаДляЭкспорта[Плательщик + "КоррСчет"];
		СтрокаЭкспорта.ПлательщикБанк1    = ВыборкаДляЭкспорта[Плательщик + "БанкДляРасчетов"];
		СтрокаЭкспорта.ПлательщикБанк2    = ВыборкаДляЭкспорта[Плательщик + "ГородРЦБанка"];
		СтрокаЭкспорта.ПлательщикБИК      = ВыборкаДляЭкспорта[Плательщик + "БИКРЦБанка"];
		СтрокаЭкспорта.ПлательщикКорсчет  = ВыборкаДляЭкспорта[Плательщик + "КоррСчетРЦБанка"];
	Иначе
		СтрокаЭкспорта.ПлательщикРасчСчет = ВыборкаДляЭкспорта[Плательщик + "РасчСчет"];
		СтрокаЭкспорта.ПлательщикБанк1    = ВыборкаДляЭкспорта[Плательщик + "Банк"];
		СтрокаЭкспорта.ПлательщикБанк2    = ВыборкаДляЭкспорта[Плательщик + "ГородБанка"];
		СтрокаЭкспорта.ПлательщикБИК      = ВыборкаДляЭкспорта[Плательщик + "БИКБанка"];
		СтрокаЭкспорта.ПлательщикКорсчет  = ВыборкаДляЭкспорта[Плательщик + "КоррСчет"];
	КонецЕсли;
	
	// Получатель1
	//
	СтрокаЭкспорта.Получатель1 = ВыборкаДляЭкспорта[Получатель];
	
	// Получатель, ПолучательИНН
	//
	СтрокаЭкспорта.ПолучательИНН = ВыборкаДляЭкспорта[Получатель + "ИНН"];
	СтрокаЭкспорта.Получатель = "ИНН " + ВыборкаДляЭкспорта[Получатель + "ИНН"] + " " + СтрокаЭкспорта.Получатель1;
	
	// ПолучательРасчСчет, 
	// ПолучательБанк1, ПолучательБанк2, ПолучательБИК, ПолучательКорсчет, 
	// Получатель2, Получатель3, Получатель4
	//
	Если НепрямыеРасчетыУПолучателя Тогда
		СтрокаЭкспорта.Получатель2 		  = ВыборкаДляЭкспорта[Получатель + "РасчСчет"];
		СтрокаЭкспорта.Получатель3 		  = ВыборкаДляЭкспорта[Получатель + "Банк"];
		СтрокаЭкспорта.Получатель4 		  = ВыборкаДляЭкспорта[Получатель + "ГородБанка"];
		СтрокаЭкспорта.Получатель 		  = СтрокаЭкспорта.Получатель + " р/с " + СтрокаЭкспорта.Получатель2 + " в " 
													+ СтрокаЭкспорта.Получатель3 + " " + СтрокаЭкспорта.Получатель4;
		СтрокаЭкспорта.ПолучательРасчСчет = ВыборкаДляЭкспорта[Получатель + "КоррСчет"];
		СтрокаЭкспорта.ПолучательБанк1    = ВыборкаДляЭкспорта[Получатель + "БанкДляРасчетов"];
		СтрокаЭкспорта.ПолучательБанк2    = ВыборкаДляЭкспорта[Получатель + "ГородРЦБанка"];
		СтрокаЭкспорта.ПолучательБИК      = ВыборкаДляЭкспорта[Получатель + "БИКРЦБанка"];
		СтрокаЭкспорта.ПолучательКорсчет  = ВыборкаДляЭкспорта[Получатель + "КоррСчетРЦБанка"];
	Иначе
		СтрокаЭкспорта.ПолучательРасчСчет = ВыборкаДляЭкспорта[Получатель + "РасчСчет"];
		СтрокаЭкспорта.ПолучательБанк1    = ВыборкаДляЭкспорта[Получатель + "Банк"];
		СтрокаЭкспорта.ПолучательБанк2    = ВыборкаДляЭкспорта[Получатель + "ГородБанка"];
		СтрокаЭкспорта.ПолучательБИК      = ВыборкаДляЭкспорта[Получатель + "БИКБанка"];
		СтрокаЭкспорта.ПолучательКорсчет  = ВыборкаДляЭкспорта[Получатель + "КоррСчет"];
	КонецЕсли;
	
	// СтатусСоставителя, 
	// ПлательщикКПП, ПолучательКПП, ПоказательКБК, ОКАТО, 
	// ПоказательОснования, ПоказательПериода, ПоказательНомера, ПоказательДаты, ПоказательТипа
	Если (ВыборкаДляЭкспорта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет
		ИЛИ ВыборкаДляЭкспорта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		ИЛИ ВыборкаДляЭкспорта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
		ИЛИ ВыборкаДляЭкспорта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств
		ИЛИ ВыборкаДляЭкспорта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне
		ИЛИ ВыборкаДляЭкспорта.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы)
		И ВыборкаДляЭкспорта.ПеречислениеВБюджет Тогда
				
		СтрокаЭкспорта.СтатусСоставителя = ВыборкаДляЭкспорта.СтатусСоставителя;
		
		Если ПустаяСтрока(СтрокаЭкспорта.СтатусСоставителя) Тогда
			СтрокаЭкспорта.СтатусСоставителя = "0";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПлательщикКПП) Тогда
			СтрокаЭкспорта.ПлательщикКПП = ВыборкаДляЭкспорта.КПППлательщика;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПлательщикКПП) Тогда
			СтрокаЭкспорта.ПлательщикКПП = "0";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПолучательКПП) Тогда
			СтрокаЭкспорта.ПолучательКПП = ВыборкаДляЭкспорта.КПППолучателя;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПолучательКПП) Тогда
			СтрокаЭкспорта.ПолучательКПП = "0";
		КонецЕсли;
		
		СтрокаЭкспорта.ПоказательКБК = ВыборкаДляЭкспорта.КодБК;
		СтрокаЭкспорта.ОКАТО         = ВыборкаДляЭкспорта.КодОКАТО;
		
		Если ПустаяСтрока(ВыборкаДляЭкспорта.ПоказательОснования) Тогда
			СтрокаЭкспорта.ПоказательОснования = "0";
		Иначе
			СтрокаЭкспорта.ПоказательОснования = ВыборкаДляЭкспорта.ПоказательОснования;
		КонецЕсли;
		
		Если ПустаяСтрока(ВыборкаДляЭкспорта.ПоказательПериода) 
				ИЛИ (ВыборкаДляЭкспорта.ПоказательПериода = "  .  .    ") Тогда
			СтрокаЭкспорта.ПоказательПериода = "0";
		Иначе
			СтрокаЭкспорта.ПоказательПериода = ВыборкаДляЭкспорта.ПоказательПериода;
		КонецЕсли;
		
		Если ПустаяСтрока(ВыборкаДляЭкспорта.ПоказательНомера) Тогда
			СтрокаЭкспорта.ПоказательНомера = "0";
		Иначе
			СтрокаЭкспорта.ПоказательНомера = ВыборкаДляЭкспорта.ПоказательНомера;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаДляЭкспорта.ПоказательДаты) Тогда
			СтрокаЭкспорта.ПоказательДаты = "0";
		Иначе
			СтрокаЭкспорта.ПоказательДаты = Формат(ВыборкаДляЭкспорта.ПоказательДаты,"ДЛФ=Д");
		КонецЕсли;
		
		Если ПустаяСтрока(ВыборкаДляЭкспорта.ПоказательТипа) Тогда
			СтрокаЭкспорта.ПоказательТипа = "0";
		Иначе
			СтрокаЭкспорта.ПоказательТипа = ВыборкаДляЭкспорта.ПоказательТипа;
		КонецЕсли;
		
	ИначеЕсли ВыгружатьКПП Тогда
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПлательщикКПП) Тогда
			СтрокаЭкспорта.ПлательщикКПП = ВыборкаДляЭкспорта.КПППлательщика;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПлательщикКПП) Тогда
			СтрокаЭкспорта.ПлательщикКПП = "0";
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПолучательКПП) Тогда
			СтрокаЭкспорта.ПолучательКПП = ВыборкаДляЭкспорта.КПППолучателя;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПолучательКПП) Тогда
			СтрокаЭкспорта.ПолучательКПП = "0";
		КонецЕсли;
		
	КонецЕсли;
			
	// Очередность
	СтрокаЭкспорта.Очередность = ВыборкаДляЭкспорта.ОчередностьПлатежа;
	
	// НазначениеПлатежа, 
	// НазначениеПлатежа1, НазначениеПлатежа2, НазначениеПлатежа3, 
	// НазначениеПлатежа4, НазначениеПлатежа5, НазначениеПлатежа6
	//
	СтрокаЭкспорта.НазначениеПлатежа 
			= СтрЗаменить(СтрЗаменить(СтрЗаменить(ВыборкаДляЭкспорта.НазначениеПлатежа, Символы.ПС, ""), 
													Символы.ВК, ""), Символы.ПФ, "");
	КоличествоСтрокНП = СтрЧислоСтрок(ВыборкаДляЭкспорта.НазначениеПлатежа);
	
	Если КоличествоСтрокНП > 6 Тогда
		КоличествоСтрокНП = 6;
	КонецЕсли;
	
	Для Сч = 1 По КоличествоСтрокНП Цикл
		СтрокаЭкспорта["НазначениеПлатежа" + Сч] = СтрПолучитьСтроку(ВыборкаДляЭкспорта.НазначениеПлатежа, Сч);
	КонецЦикла;
	
	Для Сч = (КоличествоСтрокНП + 1) По 6 Цикл
		СтрокаЭкспорта["НазначениеПлатежа" + Сч] = "";
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиЭлементДерева(ЭлементыДерева, ИмяКолонки, ИскомоеЗначение)
	
	Для Ном = 0 По ЭлементыДерева.Количество() - 1 Цикл
		
		ЭлементДерева = ЭлементыДерева.Получить(Ном);
		
		Если ЭлементДерева[ИмяКолонки] = ИскомоеЗначение Тогда
			Возврат ЭлементДерева;
		КонецЕсли;
		
		Если ЭлементДерева.ПолучитьЭлементы().Количество() > 0 Тогда
			
			РезультатПоиска = НайтиЭлементДерева(ЭлементДерева.ПолучитьЭлементы(), ИмяКолонки, ИскомоеЗначение);
			
			Если НЕ РезультатПоиска = Неопределено Тогда
				Возврат РезультатПоиска;
			КонецЕсли;
			
		КонецЕсли;
				
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьПартнераИзРегистраСведенийАналитикаУчетаПоПартнерам(Организация, Контрагент)
			
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	 |	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	 |	АналитикаУчетаПоПартнерам.Организация КАК Организация,
	 |	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент
	 |ИЗ
	 |	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	 |ГДЕ
	 |	Организация = &Организация
	 |	И Контрагент = &Контрагент
	 |";
		
	ЗапросПоПартнерам = Новый Запрос(ТекстЗапроса);
	ЗапросПоПартнерам.УстановитьПараметр("Организация", Организация);
	ЗапросПоПартнерам.УстановитьПараметр("Контрагент",  Контрагент);
	
	РезультатЗапроса = ЗапросПоПартнерам.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
	    Возврат Выборка.Партнер;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

Функция ПолучитьТекстЗапросаПоСписаниеБезналичныхДенежныхСредств(УсловиеОтбораПоТипуПлатежногоДокумента, 
																	Организация, 
																	Программа, 
																	Файл)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БезналичныеДенежныеСредства.Регистратор КАК Регистратор
	|	
	|ПОМЕСТИТЬ БезналичныеДенежныеСредства
	|	
	|ИЗ	
	|	РегистрНакопления.ДенежныеСредстваБезналичные КАК БезналичныеДенежныеСредства

	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|	ПО
	|		СписаниеБезналичныхДенежныхСредств.Ссылка = БезналичныеДенежныеСредства.Регистратор
	|		И СписаниеБезналичныхДенежныхСредств.Дата МЕЖДУ &ДатаНачало И &ДатаОкончание	
	|ГДЕ
	|	БезналичныеДенежныеСредства.КСписанию <> 0

	|СГРУППИРОВАТЬ ПО
	|	БезналичныеДенежныеСредства.Регистратор
	|;
	|/////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаСписаниеБезналичных КАК ИмяДокумента,
	|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Документ,
	|	СписаниеБезналичныхДенежныхСредств.Проведен КАК Проведен,
	|	СписаниеБезналичныхДенежныхСредств.ТипПлатежногоДокумента КАК ВидДокумента,
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
	|				КАК Строка(11)) = """"
	|		ТОГДА
    |			СписаниеБезналичныхДенежныхСредств.Номер			
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
	|	КОНЕЦ КАК Номер,
    |	СписаниеБезналичныхДенежныхСредств.Дата КАК Дата,
	|	СписаниеБезналичныхДенежныхСредств.ВидПлатежа КАК ВидПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ПеречислениеВБюджет КАК ПеречислениеВБюджет,
	|	СписаниеБезналичныхДенежныхСредств.СтатусСоставителя КАК СтатусСоставителя,
	
	|	ВЫБОР КОГДА СписаниеБезналичныхДенежныхСредств.РегистрацияВНалоговомОргане В (
	|		ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка),
	|		НЕОПРЕДЕЛЕНО)
	|	ТОГДА
	|		СписаниеБезналичныхДенежныхСредств.Организация.КПП
	|	ИНАЧЕ
	|		СписаниеБезналичныхДенежныхСредств.РегистрацияВНалоговомОргане.КПП
	|	КОНЕЦ КАК КПППлательщика,
	
	|	СписаниеБезналичныхДенежныхСредств.Контрагент.КПП КАК КПППолучателя,
	|	СписаниеБезналичныхДенежныхСредств.КодБК КАК КодБК,
	|	СписаниеБезналичныхДенежныхСредств.КодОКАТО КАК КодОКАТО,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательОснования КАК ПоказательОснования,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательПериода КАК ПоказательПериода,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательНомера КАК ПоказательНомера,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательДаты КАК ПоказательДаты,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательТипа КАК ПоказательТипа,
	|	СписаниеБезналичныхДенежныхСредств.ОчередностьПлатежа КАК ОчередностьПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.НомерСчета КАК КонтрагентРасчСчет,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.ТекстКорреспондента 
	|				КАК Строка(255)) = """"
	|		ТОГДА
    |			ВЫБОР
	|				КОГДА
	|       			(СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация 
	| 						В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)))
	|       			ИЛИ (СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация 
	| 						В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета)))
	|				ТОГДА
	|           		СписаниеБезналичныхДенежныхСредств.ПодотчетноеЛицо.Наименование
	|				ИНАЧЕ
	|					СписаниеБезналичныхДенежныхСредств.Контрагент.НаименованиеПолное
	|			КОНЕЦ
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.ТекстКорреспондента 
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА
	|       	(СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация
	|				В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)))
	|       	ИЛИ (СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация 
	| 				В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета)))
	|		ТОГДА
	|           СписаниеБезналичныхДенежныхСредств.ПодотчетноеЛицо.Наименование
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.Контрагент.Наименование
	|	КОНЕЦ КАК КонтрагентКратко,
	|	ВЫБОР
	|		КОГДА
	|       	(СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация
	|				В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику)))
	|  			ИЛИ (СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация 
	|				В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета)))
	|		ТОГДА
	|           СписаниеБезналичныхДенежныхСредств.ПодотчетноеЛицо.ИНН
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.Контрагент.ИНН
	|	КОНЕЦ КАК КонтрагентИНН,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.НаименованиеБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Банк.Наименование
	|	КОНЕЦ КАК КонтрагентБанк,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.ГородБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Банк.Город
	|	КОНЕЦ КАК КонтрагентГородБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.БИКБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Банк.Код
	|	КОНЕЦ КАК КонтрагентБИКБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.КоррСчетБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Банк.КоррСчет
	|	КОНЕЦ КАК КонтрагентКоррСчет,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК КонтрагентБанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.ГородБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.БанкДляРасчетов.Город
	|	КОНЕЦ КАК КонтрагентГородРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.БИКБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.БанкДляРасчетов.Код
	|	КОНЕЦ КАК КонтрагентБИКРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ КАК КонтрагентКоррСчетРЦБанка,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НомерСчета КАК ОрганизацияРасчСчет,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ТекстКорреспондента КАК Строка(255)) = """"
	|		ТОГДА
	|           СписаниеБезналичныхДенежныхСредств.Организация.НаименованиеСокращенное
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ТекстКорреспондента
	|	КОНЕЦ КАК ОрганизацияНаим,
	|	СписаниеБезналичныхДенежныхСредств.Организация.ИНН КАК ОрганизацияИНН,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НаименованиеБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Наименование
	|	КОНЕЦ КАК ОрганизацияБанк,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ГородБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Город
	|	КОНЕЦ КАК ОрганизацияГородБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БИКБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Код
	|	КОНЕЦ КАК ОрганизацияБИКБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.КоррСчетБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.КоррСчет
	|	КОНЕЦ КАК ОрганизацияКоррСчет,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК ОрганизацияБанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ГородБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Город
	|	КОНЕЦ КАК ОрганизацияГородРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БИКБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Код
	|	КОНЕЦ КАК ОрганизацияБИКРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ КАК ОрганизацияКоррСчетРЦБанка,
	|	СписаниеБезналичныхДенежныхСредств.НазначениеПлатежа КАК НазначениеПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ИдентификаторПлатежа КАК Код
	|ИЗ
	|	БезналичныеДенежныеСредства КАК БезналичныеДенежныеСредства
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|	ПО
	|		СписаниеБезналичныхДенежныхСредств.Ссылка = БезналичныеДенежныеСредства.Регистратор
	|
	|ГДЕ
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация Не В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
	|		)
	|	И (СписаниеБезналичныхДенежныхСредств." + УсловиеОтбораПоТипуПлатежногоДокумента + ")
	|";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Владелец = &Организация
		|";
	КонецЕсли; 
				   
	Если ЗначениеЗаполнено(Программа) Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Программа = &Программа
		|";

	КонецЕсли;
	
	Если ЗначениеЗаполнено(Файл)Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ФайлВыгрузки = &Файл
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&СтрокаСписаниеБезналичных КАК ИмяДокумента,
	|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Документ,
	|	СписаниеБезналичныхДенежныхСредств.Проведен КАК Проведен,
	|	СписаниеБезналичныхДенежныхСредств.ТипПлатежногоДокумента КАК ВидДокумента,
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
	|				КАК Строка(11)) = """"
	|		ТОГДА
    |			СписаниеБезналичныхДенежныхСредств.Номер			
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
	|	КОНЕЦ КАК Номер,
	|	СписаниеБезналичныхДенежныхСредств.Дата КАК Дата,
	|	СписаниеБезналичныхДенежныхСредств.ВидПлатежа КАК ВидПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ПеречислениеВБюджет КАК ПеречислениеВБюджет,
	|	СписаниеБезналичныхДенежныхСредств.СтатусСоставителя КАК СтатусСоставителя,
	|	СписаниеБезналичныхДенежныхСредств.Организация.КПП КАК КПППлательщика,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.КПП КАК КПППолучателя,
	|	СписаниеБезналичныхДенежныхСредств.КодБК КАК КодБК,
	|	СписаниеБезналичныхДенежныхСредств.КодОКАТО КАК КодОКАТО,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательОснования КАК ПоказательОснования,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательПериода КАК ПоказательПериода,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательНомера КАК ПоказательНомера,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательДаты КАК ПоказательДаты,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательТипа КАК ПоказательТипа,
	|	СписаниеБезналичныхДенежныхСредств.ОчередностьПлатежа КАК ОчередностьПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НомерСчета КАК КонтрагентРасчСчет,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ТекстКорреспондента КАК Строка(255)) = """"
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.Организация.НаименованиеСокращенное
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ТекстКорреспондента 
	|	КОНЕЦ КАК Контрагент,
	|	СписаниеБезналичныхДенежныхСредств.Организация.Наименование КАК КонтрагентКратко,
	|	СписаниеБезналичныхДенежныхСредств.Организация.ИНН КАК КонтрагентИНН,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НаименованиеБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.Наименование
	|	КОНЕЦ КАК КонтрагентБанк,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ГородБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.Город
	|	КОНЕЦ КАК КонтрагентГородБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БИКБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.Код
	|	КОНЕЦ КАК КонтрагентБИКБанка,
	
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.КоррСчет КАК КонтрагентКоррСчет,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК КонтрагентБанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ГородБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.Город
	|	КОНЕЦ КАК КонтрагентГородРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БИКБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.Код
	|	КОНЕЦ КАК КонтрагентБИКРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ КАК КонтрагентКоррСчетРЦБанка,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НомерСчета КАК ОрганизацияРасчСчет,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ТекстКорреспондента КАК Строка(255)) = """"
	|		ТОГДА
	|           СписаниеБезналичныхДенежныхСредств.Организация.НаименованиеСокращенное
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ТекстКорреспондента
	|	КОНЕЦ КАК ОрганизацияНаим,
	|	СписаниеБезналичныхДенежныхСредств.Организация.ИНН КАК ОрганизацияИНН,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НаименованиеБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Наименование
	|	КОНЕЦ КАК ОрганизацияБанк,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ГородБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Город
	|	КОНЕЦ КАК ОрганизацияГородБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БИКБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Код
	|	КОНЕЦ КАК ОрганизацияБИКБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.КоррСчетБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.КоррСчет
	|	КОНЕЦ КАК ОрганизацияКоррСчет,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК ОрганизацияБанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ГородБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Город
	|	КОНЕЦ КАК ОрганизацияГородРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БИКБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Код
	|	КОНЕЦ КАК ОрганизацияБИКРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанкаДляРасчетов = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ КАК ОрганизацияКоррСчетРЦБанка,
	|	СписаниеБезналичныхДенежныхСредств.НазначениеПлатежа КАК НазначениеПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ИдентификаторПлатежа КАК Код
	|ИЗ
	|	БезналичныеДенежныеСредства КАК БезналичныеДенежныеСредства
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|	ПО
	|		СписаниеБезналичныхДенежныхСредств.Ссылка = БезналичныеДенежныеСредства.Регистратор
	|
	|ГДЕ
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|		)
	|	И (СписаниеБезналичныхДенежныхСредств." + УсловиеОтбораПоТипуПлатежногоДокумента + ")
	|";
		
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Владелец = &Организация
		|";
	КонецЕсли; 
				   
	Если ЗначениеЗаполнено(Программа) Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Программа = &Программа
		|";

	КонецЕсли;
	
	Если ЗначениеЗаполнено(Файл)Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ФайлВыгрузки = &Файл
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	&СтрокаСписаниеБезналичных КАК ИмяДокумента,
	|	СписаниеБезналичныхДенежныхСредств.Ссылка КАК Документ,
	|	СписаниеБезналичныхДенежныхСредств.Проведен КАК Проведен,
	|	СписаниеБезналичныхДенежныхСредств.ТипПлатежногоДокумента КАК ВидДокумента,
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
	|				КАК Строка(11)) = """"
	|		ТОГДА
    |			СписаниеБезналичныхДенежныхСредств.Номер			
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
	|	КОНЕЦ КАК Номер,
	|	СписаниеБезналичныхДенежныхСредств.Дата КАК Дата,
	|	СписаниеБезналичныхДенежныхСредств.ВидПлатежа КАК ВидПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ПеречислениеВБюджет КАК ПеречислениеВБюджет,
	|	СписаниеБезналичныхДенежныхСредств.СтатусСоставителя КАК СтатусСоставителя,
	|	СписаниеБезналичныхДенежныхСредств.Организация.КПП КАК КПППлательщика,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.КПП КАК КПППолучателя,
	|	СписаниеБезналичныхДенежныхСредств.КодБК КАК КодБК,
	|	СписаниеБезналичныхДенежныхСредств.КодОКАТО КАК КодОКАТО,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательОснования КАК ПоказательОснования,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательПериода КАК ПоказательПериода,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательНомера КАК ПоказательНомера,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательДаты КАК ПоказательДаты,
	|	СписаниеБезналичныхДенежныхСредств.ПоказательТипа КАК ПоказательТипа,
	|	СписаниеБезналичныхДенежныхСредств.ОчередностьПлатежа КАК ОчередностьПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.СуммаДокумента КАК СуммаДокумента,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НомерСчета КАК КонтрагентРасчСчет,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ТекстКорреспондента КАК Строка(255)) = """"
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.НаименованиеСокращенное
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ТекстКорреспондента 
	|	КОНЕЦ КАК Контрагент,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.Наименование КАК КонтрагентКратко,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.ИНН КАК КонтрагентИНН,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НаименованиеБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.Наименование
	|	КОНЕЦ КАК КонтрагентБанк,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ГородБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.Город
	|	КОНЕЦ КАК КонтрагентГородБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БИКБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.Код
	|	КОНЕЦ КАК КонтрагентБИКБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.КоррСчетБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Банк.КоррСчет
	|	КОНЕЦ КАК КонтрагентКоррСчет,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК КонтрагентБанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.ГородБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.Город
	|	КОНЕЦ КАК КонтрагентГородРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БИКБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.Код
	|	КОНЕЦ КАК КонтрагентБИКРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ КАК КонтрагентКоррСчетРЦБанка,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НомерСчета КАК ОрганизацияРасчСчет,
	|	ВЫБОР
	|		КОГДА
	|       	ВЫРАЗИТЬ(СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ТекстКорреспондента КАК Строка(255)) = """"
	|		ТОГДА
	|           СписаниеБезналичныхДенежныхСредств.Организация.НаименованиеСокращенное
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ТекстКорреспондента
	|	КОНЕЦ КАК ОрганизацияНаим,
	|	СписаниеБезналичныхДенежныхСредств.Организация.ИНН КАК ОрганизацияИНН,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НаименованиеБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Наименование
	|	КОНЕЦ КАК ОрганизацияБанк,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ГородБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Город
	|	КОНЕЦ КАК ОрганизацияГородБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БИКБанка
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.Код
	|	КОНЕЦ КАК ОрганизацияБИКБанка,
	|	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Банк.КоррСчет КАК ОрганизацияКоррСчет,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.НаименованиеБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Наименование
	|	КОНЕЦ КАК ОрганизацияБанкДляРасчетов,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ГородБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Город
	|	КОНЕЦ КАК ОрганизацияГородРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БИКБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.Код
	|	КОНЕЦ КАК ОрганизацияБИКРЦБанка,
	|	ВЫБОР
	|		КОГДА
	|       	СписаниеБезналичныхДенежныхСредств.БанковскийСчет.РучноеИзменениеРеквизитовБанка = Истина
	|		ТОГДА
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.КоррСчетБанкаДляРасчетов
	|		ИНАЧЕ
	|			СписаниеБезналичныхДенежныхСредств.БанковскийСчет.БанкДляРасчетов.КоррСчет
	|	КОНЕЦ КАК ОрганизацияКоррСчетРЦБанка,
	|	СписаниеБезналичныхДенежныхСредств.НазначениеПлатежа КАК НазначениеПлатежа,
	|	СписаниеБезналичныхДенежныхСредств.ИдентификаторПлатежа КАК Код
	|ИЗ
	|	БезналичныеДенежныеСредства КАК БезналичныеДенежныеСредства
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
	|	ПО
	|		СписаниеБезналичныхДенежныхСредств.Ссылка = БезналичныеДенежныеСредства.Регистратор
	|
	|ГДЕ
	|	СписаниеБезналичныхДенежныхСредств.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств)
	|		)
	|	И (СписаниеБезналичныхДенежныхСредств." + УсловиеОтбораПоТипуПлатежногоДокумента + ")
	|";
		
	Если ЗначениеЗаполнено(Организация) Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Владелец = &Организация
		|";
	КонецЕсли; 
				   
	Если ЗначениеЗаполнено(Программа) Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.Программа = &Программа
		|";

	КонецЕсли;
	
	Если ЗначениеЗаполнено(Файл)Тогда
		ТекстЗапроса = ТекстЗапроса + 
		"И   СписаниеБезналичныхДенежныхСредств.БанковскийСчет.ФайлВыгрузки = &Файл
		|";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьСтрокуИмпорта(Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора)
	
	Буфер = "";
	
	Пока ПустаяСтрока(Буфер)
	 ИЛИ Лев(Буфер, 2) = "//" Цикл
		
		Если Импорт_ТекущаяСтрока > Импорт_КоличествоСтрок Тогда
			Возврат "";
		КонецЕсли;
			
		Буфер = СокрЛП(СтрПолучитьСтроку(Импорт_ТекстДляРазбора, Импорт_ТекущаяСтрока));
		Импорт_ТекущаяСтрока = Импорт_ТекущаяСтрока + 1;
		
	КонецЦикла;
	
	Возврат Буфер;
	
КонецФункции

Функция РазобратьТеговуюСтроку(СтрокаРазбора, Тег, Значение)
	
	ПозицияПрисваивания = СтрНайти(СтрокаРазбора, "=");
	
	Если ПозицияПрисваивания = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Тег = ВРЕГ(СокрЛП(Лев(СтрокаРазбора, ПозицияПрисваивания - 1)));
	
	Значение = СокрЛП(Сред(СтрокаРазбора, ПозицияПрисваивания + 1));
	
	Возврат НЕ ПустаяСтрока(Тег);
	
КонецФункции

Функция ЗагрузитьСекциюДокумента(СтрокаДокумента, Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора)
	
	Импорт_Загружаемые = СоздатьСоответствиеИзСтроки(ВРЕГ(
	   "Номер,Дата,Сумма,"
	 + "ВидПлатежа,ВидОплаты,"
	 + "КвитанцияДата,КвитанцияВремя,КвитанцияСодержание,"
	 + "ДатаСписано,"
	 + "ДатаПоступило,"
	 + "ПлательщикСчет,Плательщик,ПлательщикИНН,Плательщик1,"
	 + "ПлательщикРасчСчет,ПлательщикБанк1,ПлательщикБанк2,ПлательщикБИК,ПлательщикКорсчет,"
	 + "Плательщик2,Плательщик3,Плательщик4,"
	 + "ПолучательСчет,Получатель,ПолучательИНН,Получатель1,"
	 + "ПолучательРасчСчет,ПолучательБанк1,ПолучательБанк2,ПолучательБИК,ПолучательКорсчет,"
	 + "Получатель2,Получатель3,Получатель4,"
	 + "СтатусСоставителя,ПлательщикКПП,ПолучательКПП,ПоказательКБК,ОКАТО,ПоказательОснования,"
	 + "ПоказательПериода,ПоказательНомера,ПоказательДаты,ПоказательТипа,"
	 + "Код,НазначениеПлатежа,"
	 + "НазначениеПлатежа1,НазначениеПлатежа2,НазначениеПлатежа3,"
	 + "НазначениеПлатежа4,НазначениеПлатежа5,НазначениеПлатежа6,"
	 + "СрокПлатежа,Очередность,"
	 + "УсловиеОплаты1,УсловиеОплаты2,УсловиеОплаты3,"
	 + "СрокАкцепта,ВидАккредитива,ПлатежПоПредст,ДополнУсловия,"
	 + "НомерСчетаПоставщика,ДатаОтсылкиДок"));
	
	СтрокаРазбора = ПолучитьСтрокуИмпорта(Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора);
	
	Пока Лев(Врег(СокрЛП(СтрокаРазбора)), 14) <> "КОНЕЦДОКУМЕНТА" Цикл
		
		Значение = "";
		Тег = "";
		
		Если РазобратьТеговуюСтроку(СтрокаРазбора, Тег, Значение) Тогда
			
			Если Импорт_Загружаемые[Тег] = Истина Тогда
				
				СтрокаДокумента[Тег] = Значение;
				
			Иначе
				
				Сообщение = Новый СообщениеПользователю;
		        				
				Сообщение.Текст 
						= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Неверный реквизит платежного документа, строка %1 : %2'"), 
								Импорт_ТекущаяСтрока - 1, 
								СтрокаРазбора);
									
				Сообщение.Сообщить();
				
				Возврат Ложь;
				
			КонецЕсли;
			
		Иначе
			
			Сообщение = Новый СообщениеПользователю;
						
			Сообщение.Текст 
					= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Нарушена структура платежного документа, строка %1 : %2'"), 
							Импорт_ТекущаяСтрока - 1, 
							СтрокаРазбора);
						
			Сообщение.Сообщить();
			
			Возврат Ложь;
			
		КонецЕсли;
		
		СтрокаРазбора = ПолучитьСтрокуИмпорта(Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора);
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ЗагрузитьСекциюРасчСчета(СтрокаРССчета, Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора)
	
	СтрокаТегов 
		= ВРЕГ("ДатаНачала,ДатаКонца,РасчСчет,НачальныйОстаток,ВсегоПоступило,ВсегоСписано,КонечныйОстаток,КонецРасчСчет");
	ТегиРасчетногоСчета = СоздатьСоответствиеИзСтроки(СтрокаТегов);
	
	СтрокаРазбора = ПолучитьСтрокуИмпорта(Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора);
	
	Значение = "";
	Тег = "";
	
	Пока РазобратьТеговуюСтроку(СтрокаРазбора, Тег, Значение) Цикл
		
		Если ТегиРасчетногоСчета[Тег] = Истина Тогда
			
			СтрокаРССчета[Тег] = Значение;
			
		Иначе
			
			Сообщение = Новый СообщениеПользователю;
						
			Сообщение.Текст 
					= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='Неверный реквизит в секции описания расчетного счета, строка %1 : %2'"), 
						Импорт_ТекущаяСтрока - 1, 
						СтрокаРазбора);
						
			Сообщение.Сообщить();
						
			Возврат Ложь;
			
		КонецЕсли;
		
		СтрокаРазбора = ПолучитьСтрокуИмпорта(Импорт_ТекущаяСтрока, Импорт_КоличествоСтрок, Импорт_ТекстДляРазбора);
		
		Значение = "";
		Тег = "";
		
	КонецЦикла;
	
	Если ВРЕГ(Лев(СокрЛП(СтрокаРазбора), 13)) = "КОНЕЦРАСЧСЧЕТ" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция СчетПринадлежитОрганизации(НомерСчета, Организация, БанковскийСчетОрганизации = "")
	
	ЗапросПоСчету = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
								  |	БанковскиеСчетаОрганизаций.Ссылка
	                              |ИЗ
	                              |	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	                              |ГДЕ
	                              |	БанковскиеСчетаОрганизаций.Владелец = &Организация
	                              |	И БанковскиеСчетаОрганизаций.НомерСчета = &НомерСчета");
	
	ЗапросПоСчету.УстановитьПараметр("НомерСчета", НомерСчета);
	ЗапросПоСчету.УстановитьПараметр("Организация", Организация);
	ВыборкаЗапроса = ЗапросПоСчету.Выполнить().Выбрать();
	ВыборкаЗапроса.Следующий();
	
	БанковскийСчетОрганизации = ВыборкаЗапроса.Ссылка;
	
	Возврат ЗначениеЗаполнено(БанковскийСчетОрганизации);
	
КонецФункции

Процедура РаспознатьДанныеВСтрокеДокумента(СтрокаДокумента, БанковскийСчет, Организация, НастройкаЗаполнения)
	
	ОперацияПоОплатеДСВДругуюОрганизацию = Ложь;
	
	НайденДокументСписания = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// 1) Определим вид платежа – входящий или исходящий.
	ПлатежноеПоручение  
		= СтрЗаменить(ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")), "Ё", "Е") = "ПЛАТЕЖНОЕПОРУЧЕНИЕ";
	ПлатежноеТребование 
		= СтрЗаменить(ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")), "Ё", "Е") = "ПЛАТЕЖНОЕТРЕБОВАНИЕ";
	ПлатежныйОрдер      
		= СтрЗаменить(ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")), "Ё", "Е") = "ПЛАТЕЖНЫЙОРДЕР";
	БанковскийОрдер		= ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")) = "БАНКОВСКИЙОРДЕР";	
	МемориальныйОрдер	= ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")) = "МЕМОРИАЛЬНЫЙОРДЕР";	
	ИнкассовоеПоручение = ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")) = "ИНКАССОВОЕПОРУЧЕНИЕ";	
			
	Исходящий = (СокрЛП(СтрокаДокумента.ПлательщикИНН) = СокрЛП(Организация.ИНН));
	
	Если (СтрокаДокумента.ПлательщикИНН = СтрокаДокумента.ПолучательИНН) Тогда
		Исходящий = НЕ ПустаяСтрока(СтрокаДокумента.ДатаСписано);
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаДокумента.ПлательщикИНН) Тогда
		Исходящий = НЕ ПустаяСтрока(СтрокаДокумента.ДатаСписано);
	КонецЕсли;	
	
	Если Исходящий Тогда
		БанковскийСчет = "";
		СчетПринадлежитОрганизации(СтрокаДокумента.ПлательщикСчет, Организация, БанковскийСчет);
	Иначе
		БанковскийСчет = "";
		СчетПринадлежитОрганизации(СтрокаДокумента.ПолучательСчет, Организация, БанковскийСчет);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(БанковскийСчет) Тогда
		Если Исходящий Тогда
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Файл импорта содежит информацию о банковском счете организации № %1, отсутствующем в базе. Введите информацию вручную.'"), СтрокаДокумента.ПлательщикСчет);
			
			Сообщение.Сообщить();
		Иначе
			Сообщение = Новый СообщениеПользователю;
			
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Файл импорта содежит информацию о банковском счете организации № %1, отсутствующем в базе. Введите информацию вручную.'"), СтрокаДокумента.ПолучательСчет);
			
			Сообщение.Сообщить();
		КонецЕсли;	
	КонецЕсли;	
	
	ИмяДокумента = ?(Исходящий, "Списание безналичных денежных средств", "Поступление безналичных денежных средств");
	
	// 2) Определим вид документа в программе.
	Если ПлатежноеПоручение Тогда
		ВидДокумента = "ПлатежноеПоручение";
	ИначеЕсли ПлатежноеТребование Тогда
		ВидДокумента = "ПлатежноеТребование";
	ИначеЕсли ПлатежныйОрдер Тогда
		ВидДокумента = "ПлатежныйОрдер";	
	ИначеЕсли БанковскийОрдер Тогда
		ВидДокумента = "БанковскийОрдер";		
	ИначеЕсли МемориальныйОрдер Тогда
		ВидДокумента = "МемориальныйОрдер";			
	ИначеЕсли ИнкассовоеПоручение Тогда
		ВидДокумента = "ИнкассовоеПоручение";			
	КонецЕсли;
		
	СтрокаДокумента.ИмяДокумента = ИмяДокумента;
	СтрокаДокумента.ВидДокумента = ВидДокумента;
	
	// 3) Определяем вид операции документа.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
		
		Если СтрокаДокумента.ВидОплаты = "03" Тогда
			СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка;
			
		ИначеЕсли СтрокаДокумента.ВидОплаты = "04" Тогда
			СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.СдачаДенежныхСредствВБанк;
			
		ИначеЕсли СтрокаДокумента.ВидОплаты = "13" Тогда
			Если Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту;
			Иначе
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте;
			КонецЕсли;
			
		Иначе
			СтрокаДокумента.ВидОперации = ПолучитьНастройкуЗаполнения(ИмяДокумента, "ВидОперации", НастройкаЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
	// 4) Найдем ранее загруженный (введенный вручную) документ.
	// Реквизиты для поиска: Вид документа, Дата, Номер, Номер счета.

	// Распознаем дату документа.
	ВсеРеквизитыПоискаЕсть = Ложь;
	ДокументНайден         = Ложь;
	ДатаДок                = Дата("00010101");
	
	Результат = ПолучитьДатуИзСтроки(ДатаДок, СтрокаДокумента.Дата);

	Если ЗначениеЗаполнено(Результат) Тогда
		
		СтрокаДокумента.ДатаДок = Результат;
		
		Если СтрокаДокумента.ВидДокумента = "ПлатежноеПоручение"
		 ИЛИ СтрокаДокумента.ВидДокумента = "ПлатежноеТребование"
		 ИЛИ СтрокаДокумента.ВидДокумента = "ПлатежныйОрдер"
		 ИЛИ СтрокаДокумента.ВидДокумента = "БанковскийОрдер"
		 ИЛИ СтрокаДокумента.ВидДокумента = "МемориальныйОрдер"
		 ИЛИ СтрокаДокумента.ВидДокумента = "ИнкассовоеПоручение" Тогда
		
		 	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаДокумента.Номер)
			 ИЛИ НЕ СтрокаДокумента.ВидДокумента = "ПлатежноеПоручение" Тогда
			 
				НомерДляПоискаДок = СтрокаДокумента.Номер;
				ВсеРеквизитыПоискаЕсть = Истина;
				
			Иначе
								
				Стр 
					= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Указан неправильный номер документа(%1)!'"), 
																				СтрокаДокумента.Номер);
								
				ДобавитьЗамечание(СтрокаДокумента, 4, Стр);
				ВсеРеквизитыПоискаЕсть = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокаДокумента.НомерДок = СтрокаДокумента.Номер;
	
	Если ВсеРеквизитыПоискаЕсть Тогда
		// При наличии нескольких документов предпочтение отдается первому с совпадающим номером счета
		// и владельцем счета.
		ТекстЗапроса = "";
		Если ИмяДокумента = "Поступление безналичных денежных средств" Тогда
			ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПоступлениеБезналичныхДенежныхСредств.Ссылка,
			|	ПоступлениеБезналичныхДенежныхСредств.Ссылка.ТипПлатежногоДокумента КАК ТипПлатежногоДокумента,
			|	ПоступлениеБезналичныхДенежныхСредств.Проведен,
			|	ПоступлениеБезналичныхДенежныхСредств.НомерВходящегоДокумента КАК Номер,
			|	ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента КАК Дата,
			|	БанковскиеСчетаКонтрагентов.НомерСчета КАК НомерСчета,
			|	БанковскиеСчетаКонтрагентов.Владелец.ИНН КАК ИНН,
			|	БанковскиеСчетаКонтрагентов.Владелец.КПП КАК КПП,
			|	ПоступлениеБезналичныхДенежныхСредств.Организация
			|ИЗ
			|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
			|	ПО ПоступлениеБезналичныхДенежныхСредств.Контрагент = БанковскиеСчетаКонтрагентов.Владелец
			|ГДЕ
			|	НАЧАЛОПЕРИОДА(ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента, ДЕНЬ) = &ДатаДок
			|	И ПоступлениеБезналичныхДенежныхСредств.БанковскийСчет = &БанковскийСчет
			|	И ПоступлениеБезналичныхДенежныхСредств.Организация = &Организация";
		ИначеЕсли ИмяДокумента = "Списание безналичных денежных средств" Тогда
			ТекстЗапроса = "ВЫБРАТЬ
			|	СписаниеБезналичныхДенежныхСредств.Ссылка,
			|	СписаниеБезналичныхДенежныхСредств.Ссылка.ТипПлатежногоДокумента КАК ТипПлатежногоДокумента,
			|	СписаниеБезналичныхДенежныхСредств.Проведен,
			|ВЫБОР 
			|	КОГДА СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента = """"
			|		ТОГДА СписаниеБезналичныхДенежныхСредств.Номер
			|	ИНАЧЕ СписаниеБезналичныхДенежныхСредств.НомерВходящегоДокумента 
			|	КОНЕЦ КАК Номер,
			|	СписаниеБезналичныхДенежныхСредств.Дата КАК Дата,
			|	%УсловиеСчета%
			|	СписаниеБезналичныхДенежныхСредств.Организация
			|ИЗ
			|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
			|ГДЕ
			|	НАЧАЛОПЕРИОДА(СписаниеБезналичныхДенежныхСредств.Дата, ДЕНЬ) = &ДатаДок
			|	И СписаниеБезналичныхДенежныхСредств.БанковскийСчет = &БанковскийСчет
			|	И СписаниеБезналичныхДенежныхСредств.Организация = &Организация";
		КонецЕсли;
		УсловиеСчета = "";
		Если СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет
			ИЛИ СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
			ИЛИ СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию
			ИЛИ СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
			УсловиеСчета = "СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.НомерСчета КАК НомерСчета,
								|СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.ИНН КАК ИНН,
								|СписаниеБезналичныхДенежныхСредств.БанковскийСчетПолучатель.Владелец.КПП КАК КПП,";
		Иначе
			УсловиеСчета = "СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.НомерСчета КАК НомерСчета,
								|СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Владелец.ИНН КАК ИНН,
								|СписаниеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Владелец.КПП КАК КПП,";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСчета%", УсловиеСчета);
		ЗапросПоискаДокумента = Новый Запрос;
		ЗапросПоискаДокумента.Текст = ТекстЗапроса;
		ЗапросПоискаДокумента.УстановитьПараметр("ДатаДок", ДатаДок);
		ЗапросПоискаДокумента.УстановитьПараметр("Организация", Организация);
		ЗапросПоискаДокумента.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
		РезультатЗапроса = ЗапросПоискаДокумента.Выполнить();
		Если РезультатЗапроса.Пустой()
			И ИмяДокумента = "Поступление безналичных денежных средств" Тогда
			ЗапросПоискаДокумента.Текст = "ВЫБРАТЬ
			|	ПоступлениеБезналичныхДенежныхСредств.Ссылка,
			|	ПоступлениеБезналичныхДенежныхСредств.Ссылка.ТипПлатежногоДокумента КАК ТипПлатежногоДокумента,
			|	ПоступлениеБезналичныхДенежныхСредств.Проведен,
			|	ПоступлениеБезналичныхДенежныхСредств.НомерВходящегоДокумента КАК Номер,
			|	ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента КАК Дата,
			|	ПоступлениеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.НомерСчета КАК НомерСчета,
			|	ПоступлениеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Владелец.ИНН КАК ИНН,
			|	ПоступлениеБезналичныхДенежныхСредств.БанковскийСчетКонтрагента.Владелец.КПП КАК КПП,
			|	ПоступлениеБезналичныхДенежныхСредств.Организация
			|ИЗ
			|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ПоступлениеБезналичныхДенежныхСредств
			|ГДЕ
			|	НАЧАЛОПЕРИОДА(ПоступлениеБезналичныхДенежныхСредств.ДатаВходящегоДокумента, ДЕНЬ) = &ДатаДок
			|	И ПоступлениеБезналичныхДенежныхСредств.БанковскийСчет = &БанковскийСчет
			|	И ПоступлениеБезналичныхДенежныхСредств.Организация = &Организация";
			ЗапросПоискаДокумента.УстановитьПараметр("ДатаДок", ДатаДок);
			ЗапросПоискаДокумента.УстановитьПараметр("Организация", Организация);
			ЗапросПоискаДокумента.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
			РезультатЗапроса = ЗапросПоискаДокумента.Выполнить();
		КонецЕсли;
		Если Не РезультатЗапроса.Пустой() Тогда
			Результат = РезультатЗапроса.Выбрать();
			СчетДляПоискаДок = ?(Исходящий, СтрокаДокумента.ПолучательСчет, СтрокаДокумента.ПлательщикСчет);
			ИННДляПоискаДок = ?(Исходящий, СтрокаДокумента.ПолучательИНН, СтрокаДокумента.ПлательщикИНН);
			ПроверятьИНН = ЗначениеЗаполнено(ИННДляПоискаДок);
			КППДляПоискаДок = ?(Исходящий, СтрокаДокумента.ПолучательКПП, СтрокаДокумента.ПлательщикКПП);
			ПроверятьКПП = (ЗначениеЗаполнено(КППДляПоискаДок) И КППДляПоискаДок <> "0");
			КоличествоДок = 0;
			Пока Результат.Следующий() Цикл
				ВыборкаНомер = СокрЛП(ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Результат.Номер, Истина,Истина));
				ВидПлатежногоДокумента = ВРег(СокрЛП(СтрокаДокумента.ВидДокумента));
				Если ВидПлатежногоДокумента = "МЕМОРИАЛЬНЫЙОРДЕР" Тогда
					ВидПлатежногоДокумента = "БАНКОВСКИЙОРДЕР"; 
					ВидПлатежногоДокумента2 = "ПЛАТЕЖНЫЙОРДЕР"; 
				КонецЕсли;
				Если (ВРег(СокрЛП(СтрЗаменить(Результат.ТипПлатежногоДокумента, " ", ""))) = ВидПлатежногоДокумента
						Или ВРег(СокрЛП(СтрЗаменить(Результат.ТипПлатежногоДокумента, " ", ""))) = ВидПлатежногоДокумента2)
					И ((ВыборкаНомер = НомерДляПоискаДок) или (Результат.Номер = НомерДляПоискаДок))
					И (НЕ ЗначениеЗаполнено(Результат.НомерСчета) ИЛИ СокрЛП(Результат.НомерСчета) = СчетДляПоискаДок) Тогда
					//У контрагентов могут совпадать номера счетов, например у бюджетников оплаты с одного счета казначейства.
					//Проверяем плательщика/получателя по ИНН и КПП, если заполнены в файле обмена.
					Если ПроверятьИНН И ЗначениеЗаполнено(Результат.ИНН) И НЕ Результат.ИНН = ИННДляПоискаДок Тогда
						Продолжить;
					КонецЕсли;
					Если ПроверятьКПП И ЗначениеЗаполнено(Результат.КПП) И НЕ Результат.КПП = КППДляПоискаДок Тогда
						Продолжить;
					КонецЕсли;
					Если КоличествоДок = 0 Тогда
						СтрокаДокумента.Документ = Результат.Ссылка;
						СтрокаДокумента.Проведен = Результат.Проведен;
						СтрокаДокумента.НомерДок = Результат.Номер;
						СтрокаДокумента.ДатаДок  = Результат.Дата;
					КонецЕсли;
					КоличествоДок = КоличествоДок + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Если документ уже есть в ИБ, то берем все данные из него.
		ДокументНайден = ЗначениеЗаполнено(СтрокаДокумента.Документ);
		Если ДокументНайден
			И (ТипЗнч(СтрокаДокумента.Документ) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
				ИЛИ ТипЗнч(СтрокаДокумента.Документ) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств")) Тогда
			Документ = СтрокаДокумента.Документ;
			Если Документ.РасшифровкаПлатежа.Количество() > 0 Тогда
				Если ИмяДокумента = "Поступление безналичных денежных средств" Тогда
					СтрокаДокумента.СтатьяДоходовИлиРасходов = Документ.РасшифровкаПлатежа[0].СтатьяДоходов;
				ИначеЕсли ИмяДокумента = "Списание безналичных денежных средств" Тогда
					СтрокаДокумента.СтатьяДоходовИлиРасходов = Документ.РасшифровкаПлатежа[0].СтатьяРасходов;
				КонецЕсли;
				СтрокаДокумента.Подразделение = Документ.РасшифровкаПлатежа[0].Подразделение;
				СтрокаДокумента.СтатьяДДС = Документ.РасшифровкаПлатежа[0].СтатьяДвиженияДенежныхСредств;
			Иначе
				СтрокаДокумента.СтатьяДДС = Документ.СтатьяДвиженияДенежныхСредств;
			КонецЕсли;
			СтрокаДокумента.СчетОрганизации = Документ.БанковскийСчет;
			СтрокаДокумента.Контрагент = Документ.Контрагент;
		КонецЕсли;
	КонецЕсли;
	
	// 5) Определяем банковский счет организации.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.СчетОрганизации) Тогда
		СтрокаДокумента.СчетОрганизации = БанковскийСчет;
	КонецЕсли;
	
	// 6) Определяем банковский счет контрагента.
	Если Не ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
		
		ЗапросПоискаСчета = Новый Запрос;

		Если Исходящий Тогда
						
			СчетКонтрагента = СтрокаДокумента.ПолучательСчет;
			ИННКонтрагента = СтрокаДокумента.ПолучательИНН;
			КППКонтрагента = СтрокаДокумента.ПолучательКПП;
			Если СтрокаДокумента.Получатель1 <> "" Тогда
				ИмяКонтрагента = СтрокаДокумента.Получатель1;
			Иначе
				ИмяКонтрагента = СтрокаДокумента.Получатель;
			КонецЕсли;
			ЗапросПоискаСчета.УстановитьПараметр("НомерСчета", СтрокаДокумента.ПолучательСчет);
						
		Иначе
			
			ЗапросПоискаСчета.УстановитьПараметр("НомерСчета", СтрокаДокумента.ПлательщикСчет);
			СчетКонтрагента = СтрокаДокумента.ПлательщикСчет;
			ИННКонтрагента = СтрокаДокумента.ПлательщикИНН;
			КППКонтрагента = СтрокаДокумента.ПлательщикКПП;
			Если СтрокаДокумента.Плательщик1 <> "" Тогда
				ИмяКонтрагента = СтрокаДокумента.Плательщик1;
			Иначе
				ИмяКонтрагента = СтрокаДокумента.Плательщик;
			КонецЕсли;
			
		КонецЕсли;
		
		ЗапросПоискаСчета.УстановитьПараметр("ИННКонтрагента", ИННКонтрагента);
		
		СтороннийКонтрагент = Ложь;
		ВидОперации = ПолучитьНастройкуЗаполнения(ИмяДокумента, "ВидОперации", НастройкаЗаполнения);
		Если ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
			Или ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
			Или ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
			Или ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
			
			СтороннийКонтрагент = Истина;
		КонецЕсли;
		
		Если СтороннийКонтрагент Тогда
			
			ЗапросПоискаСчета.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БанковскиеСчета.Владелец,
			|	БанковскиеСчета.Ссылка,
			|	БанковскиеСчета.НомерСчета
			|ИЗ
			|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
			|ГДЕ
			|	БанковскиеСчета.Владелец.ИНН = &ИННКонтрагента
			|	И БанковскиеСчета.НомерСчета = &НомерСчета";
			
			ВыборкаПоиска = ЗапросПоискаСчета.Выполнить().Выбрать();
			
			Контрагент = Справочники.Контрагенты.ПустаяСсылка();
			
		Иначе
			
			ЗапросПоискаСчета.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БанковскиеСчета.Владелец,
			|	БанковскиеСчета.Ссылка,
			|	БанковскиеСчета.НомерСчета
			|ИЗ
			|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
			|ГДЕ
			|	БанковскиеСчета.Владелец.ИНН = &ИННКонтрагента
			|	И БанковскиеСчета.НомерСчета = &НомерСчета";
			
			РезультатЗапроса = ЗапросПоискаСчета.Выполнить();
			ВыборкаПоиска = ЗапросПоискаСчета.Выполнить().Выбрать();
			
			Контрагент = Справочники.Организации.ПустаяСсылка();
			
		КонецЕсли;
		
		Если (ВыборкаПоиска.Количество() = 0
		 ИЛИ ОперацияПоОплатеДСВДругуюОрганизацию)
		 И НЕ НайденДокументСписания Тогда
			
			ЗапросПоискаСчета.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	БанковскиеСчета.Владелец,
			|	БанковскиеСчета.Ссылка,
			|	БанковскиеСчета.НомерСчета
			|ИЗ
			|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчета
			|ГДЕ
			|	БанковскиеСчета.Владелец.ИНН = &ИННКонтрагента
			|	И БанковскиеСчета.НомерСчета = &НомерСчета";
			
			ВыборкаПоиска = ЗапросПоискаСчета.Выполнить().Выбрать();
			
			Контрагент = Справочники.Контрагенты.ПустаяСсылка()
			
		КонецЕсли;
		
		Если ВыборкаПоиска.Следующий() Тогда
			
			СтрокаДокумента.СчетКонтрагента = ВыборкаПоиска.Ссылка;
			Контрагент = ВыборкаПоиска.Владелец;
			
		Иначе  
			
			ДобавитьЗамечание(СтрокаДокумента, 2, 
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Не найден счет контрагента (%1)!'"), СчетКонтрагента));
			СтрокаДокумента.СчетКонтрагента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru='Не найден (%1)'"),СчетКонтрагента);
			
		КонецЕсли;
		
		Если ВыборкаПоиска.Количество() > 1 Тогда
			ДобавитьЗамечание(СтрокаДокумента, 1, 
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='В информационной базе найдено несколько (%1) одинаковых банковских счетов!'"), 
								ВыборкаПоиска.Количество()));
		КонецЕсли;
	КонецЕсли;
	
	// Проверка на корректность БИКа плательщика/получателя.
	Если ЗначениеЗаполнено(СтрокаДокумента.ПлательщикБИК) Тогда
		
		Длина = СтрДлина(СтрокаДокумента.ПлательщикБИК);
		
		Если (Длина = 9 И Лев(СтрокаДокумента.ПлательщикБИК, 2) <> "04")
		 ИЛИ (Длина = 8 И Лев(СтрокаДокумента.ПлательщикБИК, 1) <> "4")
		 ИЛИ (Длина < 8) Тогда
			ДобавитьЗамечание(СтрокаДокумента, 1, 
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Некорректный БИК (%1) плательщика!'"), 
								СтрокаДокумента.ПлательщикБИК));
		ИначеЕсли Длина = 8 Тогда
			СтрокаДокумента.ПлательщикБИК = "0" + СтрокаДокумента.ПлательщикБИК;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаДокумента.ПолучательБИК) Тогда
		
		Длина = СтрДлина(СтрокаДокумента.ПолучательБИК);
		
		Если (Длина = 9 И Лев(СтрокаДокумента.ПолучательБИК, 2) <> "04")
		 ИЛИ (Длина = 8 И Лев(СтрокаДокумента.ПолучательБИК, 1) <> "4")
		 ИЛИ (Длина < 8) Тогда
			ДобавитьЗамечание(СтрокаДокумента, 1, 
								СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Некорректный БИК (%1) получателя!'"), 
								СтрокаДокумента.ПолучательБИК));
		ИначеЕсли Длина = 8 Тогда
			СтрокаДокумента.ПолучательБИК = "0" + СтрокаДокумента.ПолучательБИК;
		КонецЕсли;
		
	КонецЕсли;
		
	// 7) Определим контрагента.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
		
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			СтрокаДокумента.Контрагент = Контрагент;
			
		ИначеЕсли НЕ ПустаяСтрока(ИННКонтрагента) Тогда
			
			СтрокаДокумента.Контрагент = Контрагент;
			
			ЗапросПоискаКонтрагента = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
													|	Контрагенты.Ссылка,
													|	Контрагенты.ИНН,
													|	Контрагенты.Наименование,
													|	Контрагенты.КПП
													|ИЗ
													|	Справочник.Организации КАК Контрагенты
													|ГДЕ
													|	Контрагенты.ИНН = &КонтрагентИНН");
			                                            			
			ЗапросПоискаКонтрагента.УстановитьПараметр("КонтрагентИНН", ИННКонтрагента);
			ВыборкаПоиска = ЗапросПоискаКонтрагента.Выполнить().Выгрузить();
			
			Если ИмяДокумента = "Поступление безналичных денежных средств"
	   		   И ВыборкаПоиска.Количество() > 0 Тогда
			   
			   ОперацияПоОплатеДСВДругуюОрганизацию = Истина;
			   
		   	КонецЕсли;
		         			
			Если (ВыборкаПоиска.Количество() = 0
			 ИЛИ ОперацияПоОплатеДСВДругуюОрганизацию)
			 И НЕ НайденДокументСписания Тогда
				
				ЗапросПоискаКонтрагента = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
														|	Контрагенты.Ссылка,
														|	Контрагенты.ИНН,
														|	Контрагенты.Наименование,
														|	Контрагенты.КПП
														|ИЗ
														|	Справочник.Контрагенты КАК Контрагенты
														|ГДЕ
														|	Контрагенты.ИНН = &КонтрагентИНН");
														
				ЗапросПоискаКонтрагента.УстановитьПараметр("КонтрагентИНН", ИННКонтрагента);										
				ВыборкаПоиска = ЗапросПоискаКонтрагента.Выполнить().Выгрузить();										
													
			КонецЕсли;
						
			// Ищем контрагента по ИНН, если указан КПП, то и по нему.
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИНН", ИННКонтрагента);
			Если Не ПустаяСтрока(КППКонтрагента)Тогда
				ПараметрыОтбора.Вставить("КПП", КППКонтрагента);
			КонецЕсли;
			НайденныеКонтрагенты = ВыборкаПоиска.НайтиСтроки(ПараметрыОтбора);
			
			// Если не нашли по ИНН и по КПП, то попробуем поискать только по ИНН.
			Если НайденныеКонтрагенты.Количество() = 0
			   И НЕ ПустаяСтрока(КППКонтрагента) Тогда
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ИНН", ИННКонтрагента);
				НайденныеКонтрагенты = ВыборкаПоиска.НайтиСтроки(ПараметрыОтбора);
			КонецЕсли;
			
			Если НайденныеКонтрагенты.Количество() > 0 Тогда
				
				СтрокаДокумента.Контрагент = НайденныеКонтрагенты[0].Ссылка;
				
				Если НайденныеКонтрагенты.Количество() > 1 Тогда
					ДобавитьЗамечание(СтрокаДокумента, 2, 
									СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru='В информационной базе найдено несколько (%1) контрагентов с одинаковым ИНН!'"), 
										НайденныеКонтрагенты.Количество()));
				КонецЕсли;
			
			ИначеЕсли НайденныеКонтрагенты.Количество() = 0 Тогда			
				
				// Если не нашли в справочнике Контрагенты, повторим в поиск в спр.ФизическиеЛица
				ЗапросПоискаФизЛица = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
														|	ФизЛица.Ссылка,
														|	ФизЛица.ИНН,
														|	ФизЛица.Наименование														
														|ИЗ
														|	Справочник.ФизическиеЛица КАК ФизЛица
														|ГДЕ
														|	ФизЛица.ИНН = &ФизЛицоИНН");
														
				ЗапросПоискаФизЛица.УстановитьПараметр("ФизЛицоИНН", ИННКонтрагента);										
				ВыборкаПоиска = ЗапросПоискаФизЛица.Выполнить().Выгрузить();	
										
				Если ВыборкаПоиска.Количество() > 0 Тогда
				    СтрокаДокумента.Контрагент = ВыборкаПоиска[0].Ссылка;
                КонецЕсли;
				
				Если ВыборкаПоиска.Количество() > 1 Тогда
					ДобавитьЗамечание(СтрокаДокумента, 2, 
									СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru='В информационной базе найдено несколько (%1) физических лиц с одинаковым ИНН!'"), 
										НайденныеКонтрагенты.Количество()));
				ИначеЕсли ВыборкаПоиска.Количество() = 0 Тогда
					ДобавитьЗамечание(СтрокаДокумента, 2, 
									СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru='Не найден контрагент или физическое лицо (%1, ИНН %2)!'"), ИмяКонтрагента, ИННКонтрагента));
					СтрокаДокумента.Контрагент 
						= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='Не найден (%1, ИНН %2)'"), ИмяКонтрагента, ИННКонтрагента);
				КонецЕсли;
					
			КонецЕсли;

		Иначе
			
			ДобавитьЗамечание(СтрокаДокумента, 2, НСтр("ru='Не указан ИНН контрагента.'"));
			СтрокаДокумента.Контрагент
				= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не найден (%1, не указан ИНН)'"), ИмяКонтрагента);
			
		КонецЕсли;
		
	КонецЕсли;
		
	// 8) Определим статью ДДС по умолчанию.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.СтатьяДДС) Тогда
		СтрокаДокумента.СтатьяДДС = ПолучитьНастройкуЗаполнения(ИмяДокумента, "СтатьяДДС", НастройкаЗаполнения);
	КонецЕсли;
	
	// 9) Определим статью доходов/расходов по умолчанию.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.СтатьяДоходовИлиРасходов) Тогда
		СтатьяДоходовИлиРасходовИзНастройкиЗаполнения = ПолучитьНастройкуЗаполнения(ИмяДокумента, 
																				"СтатьяДоходовИлиРасходов", 
																				НастройкаЗаполнения);
        Если ЗначениеЗаполнено(СтатьяДоходовИлиРасходовИзНастройкиЗаполнения) Тогда

			СтрокаДокумента.СтатьяДоходовИлиРасходов = ПолучитьНастройкуЗаполнения(ИмяДокумента, 
																				"СтатьяДоходовИлиРасходов", 
																				НастройкаЗаполнения);
		Иначе
			Если ИмяДокумента = "Поступление безналичных денежных средств" Тогда
				СтрокаДокумента.СтатьяДоходовИлиРасходов = ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка();
			ИначеЕсли ИмяДокумента = "Списание безналичных денежных средств" Тогда
				СтрокаДокумента.СтатьяДоходовИлиРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();	
			КонецЕсли;
							
		КонецЕсли;																		
																				
	КонецЕсли;
	
	// 10) Определим подразделение по умолчанию.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.Подразделение) Тогда
		СтрокаДокумента.Подразделение = ПолучитьНастройкуЗаполнения(ИмяДокумента, "Подразделение", НастройкаЗаполнения);
	КонецЕсли;
			
	// 11) Определим сумму.
	
	// Преобразуем из строки в число.
	Буфер = СокрЛП(СтрЗаменить(СтрокаДокумента.Сумма, " ", ""));
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрЗаменить(СтрЗаменить(СтрЗаменить(Буфер, ".", ""), "-", ""), ",", "")) Тогда
		
		Сумма = Число(Буфер);
		
		Если Сумма < 0 Тогда
			Сумма = - Сумма;      
		КонецЕсли;
		
		СтрокаДокумента.СуммаДокумента = Сумма;
		Если Исходящий Тогда
			СтрокаДокумента.СуммаСписано = Сумма;
		Иначе
			СтрокаДокумента.СуммаПоступило = Сумма;
		КонецЕсли;
		
	Иначе
		
		ДобавитьЗамечание(СтрокаДокумента, 4, 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Указана неверная сумма документа (%1)!'"), Буфер));
		
	КонецЕсли;
		
	// 12) ДатаСписано и ДатаПоступило, ДатаПроведения
	// Преобразуем в дату из строки, если не пустая.
	Если НЕ ПустаяСТрока(СтрокаДокумента.ДатаСписано) Тогда
		
		Результат = ПолучитьДатуИзСтроки(СтрокаДокумента.Списано, СтрокаДокумента.ДатаСписано);
		
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			СтрокаДокумента.Списано = Дата("00010101");
		Иначе
			СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Списано;
		КонецЕсли;
		
	Иначе
		
		СтрокаДокумента.Списано = Дата("00010101");
		
	КонецЕсли;
	
	// Преобразуем в дату из строки, если не пустая.
	Если НЕ ПустаяСТрока(СтрокаДокумента.ДатаПоступило) Тогда
		
		Результат = ПолучитьДатуИзСтроки(СтрокаДокумента.Поступило, СтрокаДокумента.ДатаПоступило);
		
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			СтрокаДокумента.Поступило = Дата("00010101");
		Иначе
			СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Поступило;
		КонецЕсли;
		
	Иначе
		
		СтрокаДокумента.Поступило = Дата("00010101");
		
	КонецЕсли;
	
	// 13) Определим очередность платежа
	
	// Преобразуем из строки в число
	Буфер = СокрЛП(СтрокаДокумента.Очередность);
	Если Буфер <> "" И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Буфер) Тогда
		СтрокаДокумента.ОчередностьПлатежа = Число(Буфер);
	Иначе
		СтрокаДокумента.ОчередностьПлатежа = 5;
	КонецЕсли;
	
	// 14) Переопределяем вид операции документа, если документ присутствует в ИБ.
	Если ЗначениеЗаполнено(СтрокаДокумента.Документ) Тогда
		
		СтрокаДокумента.ВидОперации = СтрокаДокумента.Документ.ХозяйственнаяОперация;
						
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтрокаДокумента.Документ) Тогда
		ЗаполнитьТипПлатежногоДокумента(
			Организация,
			БанковскийСчет,
			СтрокаДокумента.ВидОперации,
			СтрокаДокумента.СуммаДокумента,
			СтрокаДокумента.ТипПлатежногоДокумента,
			СтрокаДокумента.ДатаДок,
			СтрокаДокумента.Документ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаПустоеЗначениеИмпорта(СтрокаИмпорта, ИмяСвойства, ПредставлениеСвойства)
	
	ИмпортНеПустые_ПлатежноеПоручение = СоздатьСоответствиеИзСтроки(
	"Номер,Дата,Сумма,ПлательщикСчет,ПолучательСчет");
	
	// По наличию статуса составителя определяется, что платеж налоговый.
	ИмпортНеПустые_ПлатежноеПоручениеБюджет = СоздатьСоответствиеИзСтроки(
	"Номер,Дата,Сумма,ПлательщикСчет,ПолучательСчет," +
	"СтатусСоставителя,ПлательщикКПП,ПолучательКПП,ПоказательКБК,ОКАТО,ПоказательОснования," +
	"ПоказательПериода,ПоказательНомера,ПоказательДаты,ПоказательТипа");
	
	Импорт_НеПустые = Новый Массив;
	
	Импорт_НеПустые.Добавить(ИмпортНеПустые_ПлатежноеПоручение);
	Импорт_НеПустые.Добавить(ИмпортНеПустые_ПлатежноеПоручениеБюджет);
	
	Если ПустаяСтрока(ПредставлениеСвойства) Тогда
		ПредставлениеСвойства = ИмяСвойства;
	КонецЕсли;	
	
	ВариантНеПустых = ПолучитьВариантНеПустых(СтрокаИмпорта.ТипДокумента);
	Если Импорт_НеПустые[ВариантНеПустых][ИмяСвойства] = Истина Тогда
		Если НЕ ЗначениеЗаполнено(СтрокаИмпорта[ИмяСвойства]) Тогда
						
			Стр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не заполнен реквизит ""%1""!'"), ПредставлениеСвойства);
						
			ДобавитьЗамечание(СтрокаИмпорта, 3, Стр);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьВариантНеПустых(ТипДокумента)
	
	Если ТипДокумента < 3
	 ИЛИ ТипДокумента = 11
	 ИЛИ ТипДокумента = 12 Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат 1;
	
КонецФункции

Процедура СписокНеНайденных(СтрокаДокумента, БанковскийСчет, ТаблицаКонтрагентов)

	НайденКонтрагент = ТипЗнч(СтрокаДокумента.Контрагент) <> Тип("Строка");
	НайденСчет       = ТипЗнч(СтрокаДокумента.СчетКонтрагента) <> Тип("Строка");
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		Возврат;
	КонецЕсли;
	
	ТипКонтрагента
		= ?(СокрЛП(СтрокаДокумента.ПлательщикСчет) = СокрЛП(БанковскийСчет.НомерСчета), "ПОЛУЧАТЕЛЬ", "ПЛАТЕЛЬЩИК");
		
	НайденнаяЗаписьОКонтрагенте
		= НайтиЭлементДерева(ТаблицаКонтрагентов.ПолучитьЭлементы(), "Значение", СтрокаДокумента[ТипКонтрагента + "ИНН"]);
	
	// Контрагент
	Если НайденнаяЗаписьОКонтрагенте = Неопределено Тогда
		
		НовыйКонтрагент = ТаблицаКонтрагентов.ПолучитьЭлементы().Добавить();
		
		Окончание = ?(СтрокаДокумента[ТипКонтрагента + "1"] = "", "", "1");
		
		НовыйКонтрагент.Представление = СтрокаДокумента[ТипКонтрагента + Окончание];
		НовыйКонтрагент.НомСтроки     = СтрокаДокумента.НомерСтроки;
	
		ДобавитьОписаниеНовогоРеквизита("Наименование", 
										Окончание, 
										ТипКонтрагента, 
										НовыйКонтрагент.ПолучитьЭлементы(), 
										СтрокаДокумента);
		ДобавитьОписаниеНовогоРеквизита("ИНН", 
										"ИНН", 
										ТипКонтрагента, 
										НовыйКонтрагент.ПолучитьЭлементы(), 
										СтрокаДокумента);
		ДобавитьОписаниеНовогоРеквизита("КПП", 
										"КПП",
										ТипКонтрагента,
										НовыйКонтрагент.ПолучитьЭлементы(),
										СтрокаДокумента);
				
		Если НайденКонтрагент Тогда
			НовыйКонтрагент.Реквизит = СтрокаДокумента.Контрагент;
		КонецЕсли;

	Иначе
		
		НовыйКонтрагент = НайденнаяЗаписьОКонтрагенте.ПолучитьРодителя();
		
		Если НовыйКонтрагент = Неопределено Тогда
			НовыйКонтрагент = НайденнаяЗаписьОКонтрагенте;
		КонецЕсли;
			
	КонецЕсли;
	
	НайденныеСтроки 
		= НайтиЭлементДерева(ТаблицаКонтрагентов.ПолучитьЭлементы(), "Значение", СтрокаДокумента[ТипКонтрагента + "СЧЕТ"]);
	
	Если НЕ НайденСчет И НайденныеСтроки = Неопределено Тогда
			
		РеквизитыНовогоКонтрагента
			= ДобавитьОписаниеНовогоРеквизита("Р/счет", 
												"СЧЕТ", 
												ТипКонтрагента, 
												НовыйКонтрагент.ПолучитьЭлементы(), 
												СтрокаДокумента);
				
		ПрямыеРасчеты = ПустаяСтрока(СтрокаДокумента[ТипКонтрагента + "2"]);
		
		Если ПрямыеРасчеты Тогда
			
			ДобавитьОписаниеНовогоРеквизита("Банк",
											"БАНК1",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Город банка",
											"БАНК2",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Код банка",
											"БИК",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Кор. счет банка",
											"КОРСЧЕТ",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			
		Иначе
			
			ДобавитьОписаниеНовогоРеквизита("Банк",
											"3",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Город банка",
											"4",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Кор. счет банка",
											"РАСЧСЧЕТ",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("РЦ банка",
											"БАНК1",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Местонахождение РЦ банка",
											"БАНК2",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Код РЦ банка",
											"БИК",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Кор. счет РЦ банка",
											"КОРСЧЕТ",
											ТипКонтрагента,
											РеквизитыНовогоКонтрагента.ПолучитьЭлементы(),
											СтрокаДокумента);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЗагрузитьСтрокуЗаголовка(ТекстСтрокиЗаголовка, Импорт_ТекущаяСтрока, Импорт_Заголовок)
	
	СтрокаТегов = ВРЕГ("ВерсияФормата,Кодировка,Отправитель,Получатель,ДатаСоздания,ВремяСоздания,ДатаНачала,ДатаКонца");
	ТегиЗаголовка = СоздатьСоответствиеИзСтроки(СтрокаТегов);
	
	Если ТипЗнч(Импорт_Заголовок) = Тип("Неопределено") Тогда
		Импорт_Заголовок = Новый Структура(СтрокаТегов);
	КонецЕсли;
	
	Значение = "";
	Тег = "";
	РазобратьТеговуюСтроку(ТекстСтрокиЗаголовка, Тег, Значение);
	
	Если ТегиЗаголовка[Тег] = Истина Тогда
		
		Импорт_Заголовок[Тег] = Значение;
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
				
		Сообщение.Текст 
			= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Неверный реквизит заголовка, строка %1 : %2'"), Импорт_ТекущаяСтрока - 1, ТекстСтрокиЗаголовка);
					
		Сообщение.Сообщить();
		
	КонецЕсли;
		
КонецФункции

Функция ПолучитьДатуИзСтроки(Приемник, Источник)
	
	Буфер = Источник;
	
	ПозицияТочки = СтрНайти(Буфер, ".");
	
	Если ПозицияТочки = 0 Тогда
		Возврат НСтр("ru='Неверный формат строки с датой'");
	КонецЕсли;
	
	ЧислоДаты = Лев(Буфер, ПозицияТочки - 1);
	Буфер = Сред(Буфер, ПозицияТочки + 1);
	
	ПозицияТочки = СтрНайти(Буфер, ".");
	
	Если ПозицияТочки = 0 Тогда
		Возврат НСтр("ru='Неверный формат строки с датой'");
	КонецЕсли;
	
	МесяцДаты = Лев(Буфер, ПозицияТочки - 1);
	ГодДаты = Сред(Буфер, ПозицияТочки + 1);
	
	Если СтрДлина(ГодДаты) = 2 Тогда
		
		Если Число(ГодДаты) < 50 Тогда
			ГодДаты = "20" + ГодДаты;
		Иначе
			ГодДаты = "19" + ГодДаты ;
		КонецЕсли;
		
	КонецЕсли;
	
	Попытка
		
		Приемник = Дата(Число(ГодДаты), Число(МесяцДаты), Число(ЧислоДаты));
		
	Исключение
		
		Возврат НСтр("ru='Не удалось преобразовать строку в дату'");
		
	КонецПопытки;
	
	Возврат Приемник;
	
КонецФункции

Функция ПоискБанковскогоСчета(Номер)
	
	Запрос = Новый запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	БанковскиеСчетаОрганизаций.Ссылка
	               |ИЗ
	               |	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	               |Где
	               |	БанковскиеСчетаОрганизаций.НомерСчета =&Номер";
				   
	Запрос.УстановитьПараметр("Номер", Номер);
	
	Результат = Запрос.Выполнить();
    	
	Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выгрузить();
        Возврат Выборка[0].Ссылка;
	Иначе
		Возврат Справочники.БанковскиеСчетаОрганизаций.ПустаяСсылка();
	КонецЕсли;
    		
КонецФункции

Функция ПолучитьНастройкуЗаполнения(ИмяДокумента, ИмяПараметра, НастройкаЗаполнения)
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Документ", ИмяДокумента);
	
	НайденныеСтроки = НастройкаЗаполнения.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеСтроки.Количество() > 0 Тогда
		Возврат НайденныеСтроки[0][ИмяПараметра];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ДобавитьОписаниеНовогоРеквизита(Представление, Реквизит, ТипКонтрагента, НовыйКонтрагент, СтрокаДокумента)

	РеквизитыНовогоКонтрагента = НовыйКонтрагент.Добавить();
		
	РеквизитыНовогоКонтрагента.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1'"),
												Представление);
 	РеквизитыНовогоКонтрагента.Значение      = СтрокаДокумента[ТипКонтрагента + Реквизит];
	РеквизитыНовогоКонтрагента.Реквизит      = ТипКонтрагента + Реквизит;
	
    Возврат РеквизитыНовогоКонтрагента;
	
КонецФункции

Функция ПроверитьДокументДляЗагрузки(СтруктураДокумента)
	
	Результат = "";
	
	Если СтруктураДокумента.Готовность > 3 Тогда
		Результат = СтруктураДокумента.ОписаниеОшибок;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПартнера(Контрагент, Организация)
	
	Партнер = Неопределено;
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер");
		Если НЕ ЗначениеЗаполнено(Партнер) Тогда
			Партнер = ПолучитьПартнераИзРегистраСведенийАналитикаУчетаПоПартнерам(Организация, Контрагент);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Партнер) Тогда
			Партнер = Справочники.Партнеры.НеизвестныйПартнер;
		КонецЕсли;
	КонецЕсли;
		
	Возврат Партнер;
	
КонецФункции

Процедура ЗаполнитьРеквизитыДокументаПоступлениеБезналичныхДенежныхСредств(ОбъектДокумента, 
																			ТипДокумента, 
																			СтрокаСекции, 
																			ЭтоНовыйДокумент, 
																			Организация)
	
	// Организация, дата, номер.
	УстановитьСвойство(ОбъектДокумента, "Организация", Организация, , ЭтоНовыйДокумент);
	
	Если ЭтоНовыйДокумент Тогда

		ДатаДокумента = Дата("00010101");
		УстановитьСвойство(ОбъектДокумента, 
							"Дата", 
							КонецДня(ПолучитьДатуИзСтроки(ДатаДокумента, СтрокаСекции.ДатаПоступило)), , 
							ЭтоНовыйДокумент);

	КонецЕсли;
		
	// Контрагент/подотчетное лицо
	Если ТипЗнч(СтрокаСекции.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		УстановитьСвойство(ОбъектДокумента, "Контрагент", СтрокаСекции.Контрагент, , ЭтоНовыйДокумент);
	ИначеЕсли ТипЗнч(СтрокаСекции.Контрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		УстановитьСвойство(ОбъектДокумента, "ПодотчетноеЛицо", СтрокаСекции.Контрагент, , ЭтоНовыйДокумент);
	КонецЕсли;	
	
	УстановитьСвойство(ОбъектДокумента, "СтатьяДвиженияДенежныхСредств", СтрокаСекции.СтатьяДДС, , ЭтоНовыйДокумент);
	
	// СтатьяДвиженияДенежныхСредств.
	Партнер = Неопределено;
	Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
	 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
	 
	 	Партнер = ПолучитьПартнера(ОбъектДокумента.Контрагент, Организация);
	 
	КонецЕсли;
		
	// НазначениеПлатежа.
	Если ЗначениеЗаполнено(СтрокаСекции.НазначениеПлатежа1) Тогда
		
		НазначениеПлатежа = СтрокаСекции.НазначениеПлатежа1;
		
		Для Ном = 2 По 6 Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаСекции["НазначениеПлатежа" + Ном]) Тогда
				Прервать;
			КонецЕсли;
			
			НазначениеПлатежа = НазначениеПлатежа + Символы.ПС + СтрокаСекции["НазначениеПлатежа" + Ном];
			
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(СтрокаСекции.НазначениеПлатежа) Тогда
		
		НазначениеПлатежа = СтрокаСекции.НазначениеПлатежа;	
		
		НазначениеПлатежа 
			= СтрЗаменить(СтрЗаменить(СтрЗаменить(НазначениеПлатежа, Символы.ПС, ""), Символы.ВК, ""), Символы.ПФ, "");
		
	Иначе
		
		НазначениеПлатежа = "";
		
	КонецЕсли;
	
	Если ОбъектДокумента.РасшифровкаПлатежа.Количество() = 0 Тогда
		СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
	Иначе
		СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа[0];
	КонецЕсли;
	
	ОднаСтрокаВРасшифровкеПлатежа = ОбъектДокумента.РасшифровкаПлатежа.Количество() = 1;
	ОснованиеПлатежаВРасшифровкеПлатежаНеЗаполнено = ПустаяСтрока(СтрокаРасшифровки.ОснованиеПлатежа);
	
	СчетчикСтрок=0;
	СтрокаРасшифровкиЗаполнена = Ложь;
	
	Если ЗначениеЗаполнено(СтрокаСекции.Код) и СтрокаСекции.Код <> "0" Тогда
		СтрокаРасшифровкиЗаполнена = ЗаполнитьСтрокуРасшифровкиПоУИП(Организация, СтрокаСекции,СтрокаРасшифровки, ЭтоНовыйДокумент);
	КонецЕсли;
	
	Если ОднаСтрокаВРасшифровкеПлатежа И ОснованиеПлатежаВРасшифровкеПлатежаНеЗаполнено и Не СтрокаРасшифровкиЗаполнена Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ОрганизацияРасчетов = Организация;
		ВалютаДенежныхСредствРасчетов = СтрокаСекции.СчетОрганизации.ВалютаДенежныхСредств;
		КонтрагентРасчетов = СтрокаСекции.Контрагент;
		ПартнерРасчетов = Партнер;
		ВидОперацииРасчетов = СтрокаСекции.ВидОперации;
		ДатаРасчетов = СтрокаСекции.ДатаДок;
					
		ТаблицаОстатковРасчетов = Неопределено;
		
		ЗаполнитьТаблицуПоРасчетамСПартнерами(ОрганизацияРасчетов, 
												ВалютаДенежныхСредствРасчетов, 
												КонтрагентРасчетов, 
												ПартнерРасчетов, 
												ВидОперацииРасчетов, 
												ДатаРасчетов, 
												ТаблицаОстатковРасчетов, 0);
		
		Если ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
			Или ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
			Или ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
			Или ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
			ЭтоРасчетыСКлиентами = Истина;
		Иначе
			ЭтоРасчетыСКлиентами = Ложь;
		КонецЕсли;
		 		
		Если ЭтоРасчетыСКлиентами Тогда
			
			ПараметрыОтбора = ВзаиморасчетыСервер.ПараметрыОтбораПриВыбореДокументаРасчетовСКлиентами();
			ПараметрыОтбора.ОтборПоОрганизацииИКонтрагенту = Истина;
			
			ТекстЗапроса = ВзаиморасчетыСервер.ПолучитьТекстЗапросаДокументыРасчетовСКлиентами(Истина, ПараметрыОтбора);
			
		Иначе
			
			ПараметрыОтбора = ВзаиморасчетыСервер.ПараметрыОтбораПриВыбореДокументаРасчетовСПоставщиками();
			ПараметрыОтбора.ОтборПоОрганизацииИКонтрагенту = Истина;
			
			ТекстЗапроса = ВзаиморасчетыСервер.ПолучитьТекстЗапросаДокументыРасчетовСПоставщиками(ПараметрыОтбора);
			
		КонецЕсли;
		
		ЗапросДокументов = Новый Запрос;
		ЗапросДокументов.Текст = ТекстЗапроса;
		ЗапросДокументов.УстановитьПараметр("Дата", ДатаРасчетов);
		ЗапросДокументов.УстановитьПараметр("Организация", ОрганизацияРасчетов);
		ЗапросДокументов.УстановитьПараметр("Контрагент", КонтрагентРасчетов);
		ЗапросДокументов.УстановитьПараметр("Партнер", ПартнерРасчетов);
		ЗапросДокументов.УстановитьПараметр("ВыборОснованияПлатежа", Истина);
		
		ПараметрыЗапроса = ЗапросДокументов.НайтиПараметры();
		Если ПараметрыЗапроса.Найти("СтатьяДвиженияДенежныхСредств") <> Неопределено Тогда
			ЗапросДокументов.УстановитьПараметр("СтатьяДвиженияДенежныхСредств", СтрокаСекции.СтатьяДДС);
		КонецЕсли;
		
		ТаблицаДокументов = ЗапросДокументов.Выполнить().Выгрузить();
		
		ТаблицаОстатковРасчетов.Колонки.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
		ТаблицаОстатковРасчетов.Колонки.Добавить("ОснованиеПлатежа");
		
		РаспределяемаяСумма = СтрокаСекции.СуммаДокумента;
		
		Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
			Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(СтрокаТаблицы.Заказ)) Тогда
				
				СтрокаТаблицыДокументов = ТаблицаДокументов.Найти(СтрокаТаблицы.Заказ, "Ссылка");
				
				Если НЕ СтрокаТаблицыДокументов = Неопределено Тогда
					СтрокаТаблицы.ОснованиеПлатежа = СтрокаТаблицыДокументов.ОбъектРасчетов;
				КонецЕсли;
				
				Если Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(СтрокаТаблицы.Заказ)) 
					и НЕ (СтрокаТаблицы.ОснованиеПлатежа = Неопределено) Тогда
					
					НомерДокументаРасчетов 
						= ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СтрокаТаблицы.ОснованиеПлатежа.Номер, Истина, Истина);
					Если СтрЧислоВхождений(НазначениеПлатежа, НомерДокументаРасчетов)>0 Тогда
						
						Если СчетчикСтрок > 0 Тогда
							СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
						КонецЕсли;	
						
						СчетчикСтрок = СчетчикСтрок + 1;
						
						СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
						РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
						
						УстановитьСвойство(СтрокаРасшифровки, "Партнер", Партнер, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "Подразделение", СтрокаСекции.Подразделение, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "Сумма", СуммаСтроки, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "СуммаВзаиморасчетов", 0, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "ОснованиеПлатежа", СтрокаТаблицы.ОснованиеПлатежа, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "Заказ", СтрокаТаблицы.Заказ, Истина, ЭтоНовыйДокумент);				
						УстановитьСвойство(СтрокаРасшифровки, "СтатьяДоходов", СтрокаСекции.СтатьяДоходовИлиРасходов, Истина, ЭтоНовыйДокумент);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
		Если РаспределяемаяСумма > 0 Тогда
			
			Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
				Если НЕ ПустаяСтрока(СтрокаТаблицы.Заказ) Тогда
					Если СтрокаТаблицы.Обработано Тогда
						Продолжить;
					КонецЕсли;	
										
					Если СчетчикСтрок > 0 Тогда
						СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
					КонецЕсли;	
					
					СчетчикСтрок = СчетчикСтрок + 1;
					
					СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
					РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
									
					УстановитьСвойство(СтрокаРасшифровки, "Партнер", Партнер, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "Подразделение", СтрокаСекции.Подразделение, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "Сумма", СуммаСтроки, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "СуммаВзаиморасчетов", 0, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "ОснованиеПлатежа", СтрокаТаблицы.ОснованиеПлатежа, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "Заказ", СтрокаТаблицы.Заказ, Истина, ЭтоНовыйДокумент);				
					УстановитьСвойство(СтрокаРасшифровки, "СтатьяДоходов", СтрокаСекции.СтатьяДоходовИлиРасходов, Истина, ЭтоНовыйДокумент);
									
					СтрокаТаблицы.Обработано = Истина;
					
					Если РаспределяемаяСумма <= 0 Тогда
						Прервать;
					КонецЕсли;	
					
				КонецЕсли;	
			КонецЦикла;
		КонецЕсли;
				
		Если РаспределяемаяСумма > 0 Тогда
			
			Если СчетчикСтрок > 0 Тогда
				СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
			КонецЕсли;	
						
			УстановитьСвойство(СтрокаРасшифровки, "Партнер", Партнер, Истина, ЭтоНовыйДокумент);
			УстановитьСвойство(СтрокаРасшифровки, "Подразделение", СтрокаСекции.Подразделение, Истина, ЭтоНовыйДокумент);
			УстановитьСвойство(СтрокаРасшифровки, "Сумма", РаспределяемаяСумма, Истина, ЭтоНовыйДокумент);
			УстановитьСвойство(СтрокаРасшифровки, "СуммаВзаиморасчетов", 0, Истина, ЭтоНовыйДокумент);
			УстановитьСвойство(СтрокаРасшифровки, "СтатьяДоходов", СтрокаСекции.СтатьяДоходовИлиРасходов, Истина, ЭтоНовыйДокумент);
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	// СчетОрганизации.
	УстановитьСвойство(ОбъектДокумента, "БанковскийСчет", СтрокаСекции.СчетОрганизации, , ЭтоНовыйДокумент);

	// СчетКонтрагента.
	Если ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		Или ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчетОтправитель", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);
		
	ИначеЕсли ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
		Или ВидОперацииРасчетов = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчетПолучатель", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);
		
	Иначе
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчетКонтрагента", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);
	КонецЕсли;
	
	// Валюта.
	УстановитьСвойство(ОбъектДокумента, "Валюта", СтрокаСекции.СчетОрганизации.ВалютаДенежныхСредств, , ЭтоНовыйДокумент);
	
	// ТипПлатежногоДокумента.
	Если ЗначениеЗаполнено(СтрокаСекции.ВидДокумента)
	 И ЭтоНовыйДокумент Тогда
		Если СтрокаСекции.ВидДокумента = "МемориальныйОрдер" Тогда
			ОбъектДокумента.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер;
		Иначе
			ОбъектДокумента.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов[СтрокаСекции.ВидДокумента];
		КонецЕсли;
	КонецЕсли;
				
	// ВидОперации.
	УстановитьСвойство(ОбъектДокумента, "ХозяйственнаяОперация", СтрокаСекции.ВидОперации, , ЭтоНовыйДокумент);
	
	УстановитьСвойство(ОбъектДокумента, "ИдентификаторПлатежа", СтрокаСекции.Код, Истина, ЭтоНовыйДокумент);
	
	// СуммаДокумента.
	УстановитьСвойство(ОбъектДокумента, "СуммаДокумента", СтрокаСекции.СуммаДокумента, Истина, ЭтоНовыйДокумент);
		
	// ДатаВходящегоДокумента.
	УстановитьСвойство(ОбъектДокумента, "ДатаВходящегоДокумента", СтрокаСекции.ДатаДок, , ЭтоНовыйДокумент);
	
	// НомерВходящегоДокумента.
	УстановитьСвойство(ОбъектДокумента, "НомерВходящегоДокумента", СтрокаСекции.НомерДок, , ЭтоНовыйДокумент);
	
	Если (ОднаСтрокаВРасшифровкеПлатежа) И (СчетчикСтрок = 0) Тогда
		
		СтрокаРасшифровки.Сумма = ОбъектДокумента.СуммаДокумента;
						
	КонецЕсли;
	
	УстановитьСвойство(ОбъектДокумента, "НазначениеПлатежа", НазначениеПлатежа, Истина, ЭтоНовыйДокумент);
	
	УстановитьСвойство(ОбъектДокумента, "Комментарий",  НСтр("ru='#Загружен из клиент-банка'") , , ЭтоНовыйДокумент);
	
	Если ЗначениеЗаполнено(СтрокаСекции.СтатьяДДС) Тогда
		Для Каждого СтрокаРасшифровки Из ОбъектДокумента.РасшифровкаПлатежа Цикл
			УстановитьСвойство(СтрокаРасшифровки, "СтатьяДвиженияДенежныхСредств", СтрокаСекции.СтатьяДДС, , ЭтоНовыйДокумент);
		КонецЦикла;
	КонецЕсли;
	
	Если ОбъектДокумента.ЭтоНовый() Тогда
		
		ОбъектДокумента.УстановитьНовыйНомер();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыДокументаСписаниеБезналичныхДенежныхСредств(ОбъектДокумента, 
																		ТипДокумента, 
																		СтрокаСекции, 
																		ЭтоНовыйДокумент, 
																		Организация)
	
	// Организация, дата, номер.
	УстановитьСвойство(ОбъектДокумента, "Организация", Организация, , ЭтоНовыйДокумент);
		
	Если ЭтоНовыйДокумент Тогда
		УстановитьСвойство(ОбъектДокумента, "Дата", КонецДня(СтрокаСекции.ДатаДок), , ЭтоНовыйДокумент);
	КонецЕсли;
	
	// Контрагент/подотчетное лицо
	Если ТипЗнч(СтрокаСекции.Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
		УстановитьСвойство(ОбъектДокумента, "Контрагент", СтрокаСекции.Контрагент, , ЭтоНовыйДокумент);
	ИначеЕсли ТипЗнч(СтрокаСекции.Контрагент) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		УстановитьСвойство(ОбъектДокумента, "ПодотчетноеЛицо", СтрокаСекции.Контрагент, , ЭтоНовыйДокумент);
	КонецЕсли;	

	УстановитьСвойство(ОбъектДокумента, "СтатьяДвиженияДенежныхСредств", СтрокаСекции.СтатьяДДС, , ЭтоНовыйДокумент);
	
	// Контрагент, партнер.
	Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
	 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
	 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне Тогда
		
		Партнер = ПолучитьПартнера(ОбъектДокумента.Контрагент, Организация);
		
	КонецЕсли;
	
	// ПодотчетноеЛицо.
	Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
		УстановитьСвойство(ОбъектДокумента, "ПодотчетноеЛицо", СтрокаСекции.Контрагент, , ЭтоНовыйДокумент);
	КонецЕсли;
		
	// БанковскийСчетПолучателя, БанковскийСчетКонтрагента.
	Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
	 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию
	 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
	 
	 	Если ТипЗнч(СтрокаСекции.Контрагент) = Тип("СправочникСсылка.Организации")
		   И ТипЗнч(СтрокаСекции.СчетКонтрагента) = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций") Тогда
		   
		   УстановитьСвойство(ОбъектДокумента, "БанковскийСчетПолучатель", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);
		 
	   	Иначе
		   
		 	УстановитьСвойство(ОбъектДокумента, "БанковскийСчетПолучатель", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);
			
	 	КонецЕсли;
	ИначеЕсли СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчетПолучатель", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);

	Иначе
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчетКонтрагента", СтрокаСекции.СчетКонтрагента, , ЭтоНовыйДокумент);
		
	КонецЕсли;
		
	// СчетОрганизации, Валюта.
	Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчет", СтрокаСекции.СчетОрганизации, , ЭтоНовыйДокумент);
		УстановитьСвойство(ОбъектДокумента, 
							"Валюта", 
							СтрокаСекции.СчетОрганизации.ВалютаДенежныхСредств, , 
							ЭтоНовыйДокумент);

	ИначеЕсли ТипЗнч(СтрокаСекции.Контрагент) = Тип("СправочникСсылка.Организации")
	   И ТипЗнч(СтрокаСекции.СчетКонтрагента) = Тип("СправочникСсылка.БанковскиеСчетаОрганизаций") Тогда
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчет", СтрокаСекции.СчетОрганизации, , ЭтоНовыйДокумент);
		УстановитьСвойство(ОбъектДокумента, 
							"Валюта", 
							СтрокаСекции.СчетОрганизации.ВалютаДенежныхСредств, , 
							ЭтоНовыйДокумент);
		
	Иначе
		
		УстановитьСвойство(ОбъектДокумента, "БанковскийСчет", СтрокаСекции.СчетОрганизации, , ЭтоНовыйДокумент);
		УстановитьСвойство(ОбъектДокумента, 
							"Валюта", 
							СтрокаСекции.СчетОрганизации.ВалютаДенежныхСредств, , 
							ЭтоНовыйДокумент);
		
	КонецЕсли;
	      	
	// ТипПлатежногоДокумента.
	Если ЗначениеЗаполнено(СтрокаСекции.ВидДокумента)
	 И ЭтоНовыйДокумент Тогда
		Если СтрокаСекции.ВидДокумента = "МемориальныйОрдер" Тогда
			ОбъектДокумента.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер;
		Иначе
			ОбъектДокумента.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов[СтрокаСекции.ВидДокумента];
		КонецЕсли;	   
   	КонецЕсли;
   
    // НеКонтролироватьЗаполнениеЗаявки.
	Если СтрокаСекции.ВидДокумента = "ПлатежныйОрдер"
 	 ИЛИ СтрокаСекции.ВидДокумента = "БанковскийОрдер"
	 ИЛИ СтрокаСекции.ВидДокумента = "МемориальныйОрдер"
	 ИЛИ СтрокаСекции.ВидДокумента = "ИнкассовоеПоручение" Тогда
		ОбъектДокумента.НеКонтролироватьЗаполнениеЗаявки = Истина;
	КонецЕсли;
		
	// СуммаДокумента.
	УстановитьСвойство(ОбъектДокумента, "СуммаДокумента", СтрокаСекции.СуммаДокумента, Истина, ЭтоНовыйДокумент);
	
	УстановитьСвойство(ОбъектДокумента, "ДатаВходящегоДокумента", СтрокаСекции.ДатаДок, , ЭтоНовыйДокумент);
		
	// НомерВходящегоДокумента.
	УстановитьСвойство(ОбъектДокумента, "НомерВходящегоДокумента", 
						ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СтрокаСекции.НомерДок, Истина, Истина), , ЭтоНовыйДокумент);
		
	// ВидПлатежа.
	УстановитьСвойство(ОбъектДокумента, "ВидПлатежа", СтрокаСекции.ВидПлатежа, , ЭтоНовыйДокумент);
	
	// ОчередностьПлатежа.
	УстановитьСвойство(ОбъектДокумента, "ОчередностьПлатежа", СтрокаСекции.ОчередностьПлатежа, , ЭтоНовыйДокумент);

	Если ЗначениеЗаполнено(СтрокаСекции.СтатусСоставителя) Тогда
				
		// ПеречислениеВБюджет.
		УстановитьСвойство(ОбъектДокумента, "ПеречислениеВБюджет", Истина, , ЭтоНовыйДокумент);
		
		// Если статус составителя 08, в создаваемом документе устанавливается вид перечисления
	    // в бюджет «Иной платеж», если статус 06 или 16 – 20, вид перечисления «Таможенный платеж»,
	    // иначе – «Налоговый платеж».
		//
		Если СтрокаСекции.СтатусСоставителя = "08" Тогда
			
			СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств;
			
			УстановитьСвойство(ОбъектДокумента, 
								"ВидПеречисленияВБюджет", 
								Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж, , 
								ЭтоНовыйДокумент);
			
		ИначеЕсли СтрокаСекции.СтатусСоставителя = "06"
			  ИЛИ СтрокаСекции.СтатусСоставителя = "16"
			  ИЛИ СтрокаСекции.СтатусСоставителя = "17"
			  ИЛИ СтрокаСекции.СтатусСоставителя = "18"
			  ИЛИ СтрокаСекции.СтатусСоставителя = "19"
			  ИЛИ СтрокаСекции.СтатусСоставителя = "20" Тогда
			  
			СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне;
			  
			УстановитьСвойство(ОбъектДокумента, 
								"ВидПеречисленияВБюджет", 
								Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж, , 
								ЭтоНовыйДокумент); 
			
		Иначе
			
			СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет;
			
			УстановитьСвойство(ОбъектДокумента, 
								"ВидПеречисленияВБюджет", 
								Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж, , 
								ЭтоНовыйДокумент); 
			
		КонецЕсли;
		 		
		// СтатусСоставителя.
		УстановитьСвойство(ОбъектДокумента, "СтатусСоставителя", СтрокаСекции.СтатусСоставителя, , ЭтоНовыйДокумент);
		
		// КодБК.
		УстановитьСвойство(ОбъектДокумента, "КодБК", СтрокаСекции.ПоказательКБК, , ЭтоНовыйДокумент);
		
		// КодОКАТО.
		УстановитьСвойство(ОбъектДокумента, "КодОКАТО", СтрокаСекции.ОКАТО, , ЭтоНовыйДокумент);
		
		// ПоказательОснования.
		УстановитьСвойство(ОбъектДокумента, "ПоказательОснования", СтрокаСекции.ПоказательОснования, , ЭтоНовыйДокумент);
		
		// ПоказательПериода.
		УстановитьСвойство(ОбъектДокумента, "ПоказательПериода", СтрокаСекции.ПоказательПериода, , ЭтоНовыйДокумент);
		
		// ПоказательНомера.
		УстановитьСвойство(ОбъектДокумента, "ПоказательНомера", СтрокаСекции.ПоказательНомера, , ЭтоНовыйДокумент);
		
		// ПоказательДаты.
		УстановитьСвойство(ОбъектДокумента, "ПоказательДаты", СтрокаСекции.ПоказательДаты, , ЭтоНовыйДокумент);
		
		// ПоказательТипа.
		УстановитьСвойство(ОбъектДокумента, "ПоказательТипа", СтрокаСекции.ПоказательТипа, , ЭтоНовыйДокумент);
		
	КонецЕсли;
	
	// НазначениеПлатежа.
	Если ЗначениеЗаполнено(СтрокаСекции.НазначениеПлатежа1) Тогда
		
		НазначениеПлатежа = СтрокаСекции.НазначениеПлатежа1;
		
		Для Ном = 2 По 6 Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаСекции["НазначениеПлатежа" + Ном]) Тогда
				Прервать;
			КонецЕсли;
			
			НазначениеПлатежа = НазначениеПлатежа + Символы.ПС + СтрокаСекции["НазначениеПлатежа" + Ном];
			
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(СтрокаСекции.НазначениеПлатежа) Тогда
		
		НазначениеПлатежа = СтрокаСекции.НазначениеПлатежа;	
		
		НазначениеПлатежа = СтрЗаменить(СтрЗаменить(СтрЗаменить(НазначениеПлатежа, Символы.ПС, ""), 
										Символы.ВК, ""), Символы.ПФ, "");
		
	Иначе
		
		НазначениеПлатежа = "";
		
	КонецЕсли;
		
	УстановитьСвойство(ОбъектДокумента, "НазначениеПлатежа", НазначениеПлатежа, Истина, ЭтоНовыйДокумент);
    	
	Если НЕ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет Тогда
		
		Если ОбъектДокумента.РасшифровкаПлатежа.Количество() = 0 Тогда
			СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
		Иначе
			СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа[0];
		КонецЕсли;
				
		ОднаСтрокаВРасшифровкеПлатежа = ОбъектДокумента.РасшифровкаПлатежа.Количество() = 1;
					
		СчетчикСтрок=0;
		
		ОднаСтрокаВРасшифровкеПлатежа = ОбъектДокумента.РасшифровкаПлатежа.Количество() = 1;
		ОснованиеПлатежаВРасшифровкеПлатежаНеЗаполнено = ПустаяСтрока(СтрокаРасшифровки.Заказ);
	
		СчетчикСтрок=0;
	
		Если ОднаСтрокаВРасшифровкеПлатежа И ОснованиеПлатежаВРасшифровкеПлатежаНеЗаполнено Тогда
					
			Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
			 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию	
			 ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
				
				УстановитьСвойство(СтрокаРасшифровки, "Сумма", СтрокаСекции.СуммаДокумента, Истина, ЭтоНовыйДокумент);
				
			Иначе
				
				ОрганизацияРасчетов = Организация;
				ВалютаДенежныхСредствРасчетов = СтрокаСекции.СчетОрганизации.ВалютаДенежныхСредств;
				КонтрагентРасчетов = СтрокаСекции.Контрагент;
				ПартнерРасчетов = Партнер;
                ВидОперацииРасчетов = СтрокаСекции.ВидОперации;
				ДатаРасчетов = СтрокаСекции.ДатаДок;
				
				ТаблицаОстатковРасчетов = Неопределено;
			                			
				ЗаполнитьТаблицуПоРасчетамСПартнерами(ОрганизацияРасчетов, 
														ВалютаДенежныхСредствРасчетов, 
														КонтрагентРасчетов, 
														ПартнерРасчетов, 
														ВидОперацииРасчетов, 
														ДатаРасчетов, 
														ТаблицаОстатковРасчетов, 0);
								
				ТаблицаОстатковРасчетов.Колонки.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
				
				РаспределяемаяСумма = СтрокаСекции.СуммаДокумента;
	
				Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
					Если НЕ ПустаяСтрока(СтрокаТаблицы.Заказ) Тогда
						Если СтрокаТаблицы.Обработано Тогда
							Продолжить;
						КонецЕсли;	
						
						Если СчетчикСтрок > 0 Тогда
							СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
						КонецЕсли;	
						
						СчетчикСтрок = СчетчикСтрок + 1;
						
						СуммаСтроки = Мин(РаспределяемаяСумма, СтрокаТаблицы.Сумма);
						РаспределяемаяСумма = РаспределяемаяСумма - СтрокаТаблицы.Сумма;
						
						УстановитьСвойство(СтрокаРасшифровки, "Партнер", Партнер, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "Подразделение", СтрокаСекции.Подразделение, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "Сумма", СуммаСтроки, Истина, ЭтоНовыйДокумент);
						УстановитьСвойство(СтрокаРасшифровки, "СуммаВзаиморасчетов", 0, Истина, ЭтоНовыйДокумент);

						УстановитьСвойство(СтрокаРасшифровки, "Заказ", СтрокаТаблицы.Заказ, Истина, ЭтоНовыйДокумент);				
						
						Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПрочиеРасходы
							ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
							УстановитьСвойство(СтрокаРасшифровки, 
												"СтатьяРасходов", 
												СтрокаСекции.СтатьяДоходовИлиРасходов, Истина, 
												ЭтоНовыйДокумент);
						КонецЕсли;
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;	
						
					КонецЕсли;	
				КонецЦикла;
								
				Если РаспределяемаяСумма > 0 Тогда
					
					Если СчетчикСтрок > 0 Тогда
						СтрокаРасшифровки = ОбъектДокумента.РасшифровкаПлатежа.Добавить();
					КонецЕсли;	
					
					УстановитьСвойство(СтрокаРасшифровки, "Партнер", Партнер, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "Подразделение", СтрокаСекции.Подразделение, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "Сумма", РаспределяемаяСумма, Истина, ЭтоНовыйДокумент);
					УстановитьСвойство(СтрокаРасшифровки, "СуммаВзаиморасчетов", 0, Истина, ЭтоНовыйДокумент);
					
					Если СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПрочиеРасходы
						ИЛИ СтрокаСекции.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
						УстановитьСвойство(СтрокаРасшифровки, 
											"СтатьяРасходов", 
											СтрокаСекции.СтатьяДоходовИлиРасходов, Истина, 
											ЭтоНовыйДокумент);
					КонецЕсли;
					
				КонецЕсли;
 				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьСвойство(ОбъектДокумента, "ИдентификаторПлатежа", СтрокаСекции.Код, Истина, ЭтоНовыйДокумент);
	
	// ВидОперации.
	УстановитьСвойство(ОбъектДокумента, "ХозяйственнаяОперация", СтрокаСекции.ВидОперации, , ЭтоНовыйДокумент);
	
	// НазначениеПлатежа.
	Если ЗначениеЗаполнено(СтрокаСекции.НазначениеПлатежа1) Тогда
		
		НазначениеПлатежа = СтрокаСекции.НазначениеПлатежа1;
		
		Для Ном = 2 По 6 Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаСекции["НазначениеПлатежа" + Ном]) Тогда
				Прервать;
			КонецЕсли;
			
			НазначениеПлатежа = НазначениеПлатежа + Символы.ПС + СтрокаСекции["НазначениеПлатежа" + Ном];
			
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(СтрокаСекции.НазначениеПлатежа) Тогда
		
		НазначениеПлатежа = СтрокаСекции.НазначениеПлатежа;	
		
		НазначениеПлатежа = СтрЗаменить(СтрЗаменить(СтрЗаменить(НазначениеПлатежа, Символы.ПС, ""),
										Символы.ВК, ""), Символы.ПФ, "");
		
	Иначе
		
		НазначениеПлатежа = "";
		
	КонецЕсли;
		
	УстановитьСвойство(ОбъектДокумента, "НазначениеПлатежа", НазначениеПлатежа, Истина, ЭтоНовыйДокумент);
	
	УстановитьСвойство(ОбъектДокумента, "Комментарий", "#Загружен из клиент-банка", , ЭтоНовыйДокумент);
	
	Если ЗначениеЗаполнено(СтрокаСекции.СтатьяДДС) Тогда
		Для Каждого СтрокаРасшифровки Из ОбъектДокумента.РасшифровкаПлатежа Цикл
			УстановитьСвойство(СтрокаРасшифровки, "СтатьяДвиженияДенежныхСредств", СтрокаСекции.СтатьяДДС, , ЭтоНовыйДокумент);
		КонецЦикла;
	КонецЕсли;
	
	Если ОбъектДокумента.ЭтоНовый() Тогда
		
		ОбъектДокумента.УстановитьНовыйНомер();
		УстанавливаемыйНомер = "";
		
		Если СтрокаСекции.ВидДокумента = "ПлатежноеПоручение" Тогда
			
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СтрокаСекции.Номер) Тогда
				
				ОкончаниеНомера = Формат(Число(СокрЛП(СтрокаСекции.Номер)), "ЧН=; ЧГ=0");
				ДлинаНомера     = СтрДлина(СокрЛП(ОбъектДокумента.Номер));
				
				НомерПозиции = СтрНайти(ОбъектДокумента.Номер, "-");
				
				Если НомерПозиции > 0 Тогда
					ДлинаОкончанияНомера = ДлинаНомера - (НомерПозиции + 1);
				Иначе	
					ДлинаОкончанияНомера = ДлинаНомера - СтрДлина(Организация.Префикс);
				КонецЕсли;
				
				
				Если СтрДлина(ОкончаниеНомера) < ДлинаОкончанияНомера Тогда
					Если НомерПозиции > 0 Тогда		
						
						УстанавливаемыйНомер = Лев(ОбъектДокумента.Номер, НомерПозиции + 1) 
						+ Формат(Число(ОкончаниеНомера), "ЧЦ=" 
						+ Строка(ДлинаОкончанияНомера) + "; ЧВН=; ЧГ=0");
						
					Иначе
						
						УстанавливаемыйНомер = Организация.Префикс + Формат(Число(ОкончаниеНомера), "ЧЦ=" 
						+ Строка(ДлинаОкончанияНомера) + "; ЧВН=; ЧГ=0");
						
					КонецЕсли;	
				КонецЕсли;						
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗапросПоискаДокумента = Новый Запрос;
		
		ЗапросПоискаДокумента.Текст = "ВЫБРАТЬ
			|	СписаниеБезналичныхДенежныхСредств.Ссылка
			|ИЗ
			|	Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеБезналичныхДенежныхСредств
			|ГДЕ
			|	НАЧАЛОПЕРИОДА(СписаниеБезналичныхДенежныхСредств.Дата, ГОД) = &ДатаДок
			|	И СписаниеБезналичныхДенежныхСредств.Номер = &Номер
			|	И СписаниеБезналичныхДенежныхСредств.Организация = &Организация";
		
		ЗапросПоискаДокумента.УстановитьПараметр("Номер", УстанавливаемыйНомер);
		ЗапросПоискаДокумента.УстановитьПараметр("ДатаДок", НачалоГода( СтрокаСекции.ДатаДок));
		ЗапросПоискаДокумента.УстановитьПараметр("Организация", Организация);
		Результат = ЗапросПоискаДокумента.Выполнить().Выбрать();
		
		Если Результат.Количество() = 0 ТОГДА
			ОбъектДокумента.Номер = УстанавливаемыйНомер;
		КонецЕсли;	
	
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСвойство(Объект, 
								ИмяСвойства, 
								ЗначениеСвойства, 
								ОбязательнаяЗаменаСтарогоЗначения = Ложь, 
								ЭтоНовыйДокумент)
	
	Если НЕ ЗначениеСвойства = Неопределено
	   И НЕ Объект[ИмяСвойства] = ЗначениеСвойства Тогда
		
	   Если ЭтоНовыйДокумент 
		ИЛИ (НЕ ЗначениеЗаполнено(Объект[ИмяСвойства]) ИЛИ ОбязательнаяЗаменаСтарогоЗначения) 
		ИЛИ ТипЗнч(Объект[ИмяСвойства]) = Тип("Булево") 
		ИЛИ ТипЗнч(Объект[ИмяСвойства]) = Тип("Дата") Тогда
			
			Объект[ИмяСвойства] = ЗначениеСвойства;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗадатьПометкуНаУдаление(ОбъектДляПометки, Пометка)
	
	НазваниеОбъекта = ПолучитьНазваниеОбъекта(ОбъектДляПометки);
	НазваниеДействия = ?(Пометка, НСтр("ru='помечен на удаление'"), НСтр("ru='отменена пометка на удаление'"));
		
	Попытка
		
		ОбъектДляПометки.Записать(РежимЗаписиДокумента.Запись);
		ОбъектДляПометки.УстановитьПометкуУдаления(Пометка);
		
		Сообщение = Новый СообщениеПользователю;
				
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1:%2.'"), 
							Врег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2), НазваниеДействия);
						
		Сообщение.Сообщить();
		
	Исключение
		
		Сообщение = Новый СообщениеПользователю;
				
		Сообщение.Текст
			= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1: не %2! Произошли ошибки при записи!'"), 
				Врег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2), НазваниеДействия);
								
		Сообщение.Сообщить();
		
	КонецПопытки
	
КонецПроцедуры

Функция ПолучитьНазваниеОбъекта(Объект)
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
					
		НазваниеОбъекта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='документ ""Поступление безналичных денежных средств"" № %1 от %2'"),
							СокрЛП(Объект.Номер), 
							Объект.Дата);
				
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств") Тогда
						
		НазваниеОбъекта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='документ ""Списание безналичных денежных средств"" № %1 от %2'"), СокрЛП(Объект.Номер), Объект.Дата);
						
	Иначе
		
		НазваниеОбъекта = НСтр("ru='объект'");
		
	КонецЕсли;
	
	Возврат НазваниеОбъекта;
             	
КонецФункции

Процедура ЗаписатьОбъект(ОбъектДляЗаписи, 
							СтрокаСекции, 
							ЭтоНовыйДокумент, 
							НастройкаЗаполнения, 
							ПроводитьДокумент = Истина)
	
	ТипДокумента = ОбъектДляЗаписи.Метаданные().Имя;
	
	Если ТипДокумента = "ПоступлениеБезналичныхДенежныхСредств" Тогда
		ИмяДокумента = "Поступление безналичных денежных средств";
	ИначеЕсли ТипДокумента = "СписаниеБезналичныхДенежныхСредств" Тогда
		ИмяДокумента = "Списание безналичных денежных средств";	
	КонецЕсли;

	Проводить = (ПолучитьНастройкуЗаполнения(ИмяДокумента, "Проводить", НастройкаЗаполнения) = Истина);
	
	Если НЕ ПроводитьДокумент Тогда
		Проводить = Ложь;
	КонецЕсли;
							
	ОбъектМодифицирован = ОбъектДляЗаписи.Модифицированность();
	ОбъектПроведен = ОбъектДляЗаписи.Проведен;
	
	Если ОбъектМодифицирован Тогда
		
		Попытка
			
			Если ОбъектПроведен Тогда
				
				ОбъектДляЗаписи.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				СтрокаСекции.Проведен = ОбъектДляЗаписи.Проведен;
				
				Если НЕ Проводить Тогда
					
					НазваниеОбъекта = ПолучитьНазваниеОбъекта(ОбъектДляЗаписи);
					
					Если Лев(НазваниеОбъекта, 8) = "документ" Тогда
						НазваниеОбъекта = "документа " + Сред(НазваниеОбъекта, 9);
					КонецЕсли;	
					
					Сообщение = Новый СообщениеПользователю;
				   
				   	Сообщение.Текст 
				   		= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2'"), 
						НСтр("ru='Отменено проведение'"), НазваниеОбъекта);
				   
				    Сообщение.Сообщить();
					
				КонецЕсли;	
				
			Иначе
				
				ОбъектДляЗаписи.Записать(РежимЗаписиДокумента.Запись);
				
			КонецЕсли;
			
			Если ЭтоНовыйДокумент Тогда
				
				Сообщение = Новый СообщениеПользователю;
				
				Сообщение.Текст 
					= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2.'"), 
						?(ЭтоНовыйДокумент, НСтр("ru='Создан'"), НСтр("ru='Перезаписан'")), ПолучитьНазваниеОбъекта(ОбъектДляЗаписи));
				
				Сообщение.Сообщить();
				
			КонецЕсли;
			
		Исключение
			
			НазваниеОбъекта = ПолучитьНазваниеОбъекта(ОбъектДляЗаписи);
			
			// Запись события в журнал регистрации для системного администратора.
    		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись документа'"),
       								 УровеньЖурналаРегистрации.Ошибка, ,
									 ВРег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2),
       								 ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Сообщение = Новый СообщениеПользователю;
						
			Сообщение.Текст 
				= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='%1%2! Произошли ошибки при записи!'"), 
					Врег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2), 
					?(ЭтоНовыйДокумент, НСтр("ru='Не создан'"), НСтр("ru='не записан'")));
						
			Сообщение.Сообщить();
			
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
	НазваниеОбъекта = ПолучитьНазваниеОбъекта(ОбъектДляЗаписи);

	Если Проводить
	   И (ОбъектМодифицирован ИЛИ НЕ ОбъектПроведен) Тогда
	   
	   Если ОбъектДляЗаписи.ПроверитьЗаполнение() Тогда
		   
		   Попытка
			   
			   ОбъектДляЗаписи.Записать(РежимЗаписиДокумента.Проведение);
			   
			   Если НЕ ОбъектПроведен Тогда
				   
				   Сообщение = Новый СообщениеПользователю;
				   
				   Сообщение.Текст 
				   	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2'"), 
						?(ОбъектПроведен, НСтр("ru='Перепроведен'"), НСтр("ru='Проведен'")),
						НазваниеОбъекта);
				   
				   Сообщение.Сообщить();
				   
			   КонецЕсли;
			   
			   СтрокаСекции.Проведен = ОбъектДляЗаписи.Проведен;
			   
		   Исключение
			   
			   Сообщение = Новый СообщениеПользователю;
			   
			   Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			   						НСтр("ru='%1 не проведен! Произошли ошибки при проведении!'"), 
									Врег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2));
			   
			   Сообщение.Сообщить();
			   
		   КонецПопытки
		   
	   Иначе
		   
		   Сообщение = Новый СообщениеПользователю;
		   
		   Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		   						НСтр("ru='%1 не проведен! Произошли ошибки при проведении!'"), 
								Врег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2));
		   
		   Сообщение.Сообщить();
		   
	   КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Функция НайтиБанк(НаимБанка, ГородБанка, БИК, КоррСчет)
	
	НайденныйБанк = Справочники.КлассификаторБанковРФ.ПустаяСсылка();
	
	Если НЕ ПустаяСтрока(БИК) Тогда
		НайденныйБанк = Справочники.КлассификаторБанковРФ.НайтиПоКоду(БИК);
	КонецЕсли;
	
	Если НайденныйБанк = Справочники.КлассификаторБанковРФ.ПустаяСсылка() Тогда
		НайденныйБанк = Справочники.КлассификаторБанковРФ.НайтиПоРеквизиту("КоррСчет", КоррСчет);
	КонецЕсли;
			
	Возврат НайденныйБанк;

КонецФункции

Функция ПлатежныйДокументПоВидуОперации(ВидОперации, Организация, БанковскийСчет, СуммаДокумента, ДатаПроведения)
	
	ТекстЗапроса = "";
	Если ВидОперации = Перечисления.ХозяйственныеОперации.СдачаДенежныхСредствВБанк Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	ДанныеДокумента.Ссылка КАК Документ
		|ИЗ
		|	Документ.РасходныйКассовыйОрдер КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк)
		|	И ДанныеДокумента.Организация = &Организация
		|	И ДанныеДокумента.БанковскийСчет = &БанковскийСчет
		|	И (ДанныеДокумента.СуммаДокумента = &СуммаДокумента ИЛИ &СуммаДокумента = 0)
		|	И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ДЕНЬ) = &ДатаПроведения
		|	И ДанныеДокумента.Проведен
		|";
		
	ИначеЕсли ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	ДанныеДокумента.Ссылка КАК Документ
		|ИЗ
		|	Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка)
		|	И ДанныеДокумента.Организация = &Организация
		|	И ДанныеДокумента.БанковскийСчет = &БанковскийСчет
		|	И (ДанныеДокумента.СуммаДокумента = &СуммаДокумента ИЛИ &СуммаДокумента = 0)
		|	И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ДЕНЬ) = &ДатаПроведения
		|	И ДанныеДокумента.Проведен
		|";
		
	КонецЕсли;
	
	ПлатежныйДокумент = Неопределено;
	
	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
		Запрос.УстановитьПараметр("СуммаДокумента", СуммаДокумента);
		Запрос.УстановитьПараметр("ДатаПроведения", НачалоДня(ДатаПроведения));
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Количество() = 1 Тогда
			
			Выборка.Следующий();
			ПлатежныйДокумент = Выборка.Документ;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПлатежныйДокумент;
	
КонецФункции

Процедура ЗаполнитьТаблицуПоРасчетамСПартнерами(Организация, 
												Валюта, 
												Контрагент, 
												Партнер, 
												ХозяйственнаяОперация, 
												ДатаДокумента, 
												ТаблицаОстатковРасчетов, 
												СуммаДокумента)
	
	МассивРасчетыСПоставщиками = Новый Массив;
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ОплатаПоставщику);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ПеречислениеТаможне);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.АвансовыйОтчет);
	МассивРасчетыСПоставщиками.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями);
	ЕстьРасчетыСПоставщиками = МассивРасчетыСПоставщиками.Найти(ХозяйственнаяОперация) <> Неопределено;
	
	МассивРасчетыСКлиентами = Новый Массив;
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации);
	МассивРасчетыСКлиентами.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию);
	ЕстьРасчетыСКлиентами = МассивРасчетыСКлиентами.Найти(ХозяйственнаяОперация) <> Неопределено;
	
	Если ЕстьРасчетыСКлиентами ИЛИ ЕстьРасчетыСПоставщиками Тогда
		Реквизиты = Новый Структура("Дата, Организация, СуммаДокумента, Валюта, Контрагент, Партнер, ХозяйственнаяОперация",
		ДатаДокумента,
		Организация,
		СуммаДокумента,
		Валюта,
		Контрагент,
		Партнер,
		ХозяйственнаяОперация);
		
		АдресПлатежейВХранилище = "";
		
		ВзаиморасчетыСервер.ЗаполнитьТаблицуОстатковРасчетов(Реквизиты, АдресПлатежейВХранилище, ТаблицаОстатковРасчетов);
	Иначе	
		ТаблицаОстатковРасчетов = Новый ТаблицаЗначений;
		ТаблицаОстатковРасчетов.Колонки.Добавить("Заказ", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
		ТаблицаОстатковРасчетов.Колонки.Добавить("ВалютаВзаиморасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
		ТаблицаОстатковРасчетов.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	
	КонецЕсли;
			
КонецПроцедуры

Функция ЗаполнитьСтрокуРасшифровкиПоУИП(Организация, СтрокаСекции, СтрокаРасшифровки, ЭтоНовыйДокумент)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ВзаиморасчетыСервер.ЗаполнитьДанныеВыбораОснованияПлатежа(ДанныеВыбора,
		СтрокаСекции.Код, 
		Новый Структура("Организация, Контрагент", Организация, СтрокаСекции.Контрагент),
		Истина);

	Если ДанныеВыбора.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
		
	УстановитьСвойство(СтрокаРасшифровки, "Партнер", ДанныеВыбора[0].Значение.Значение.Партнер, Истина, ЭтоНовыйДокумент);
	УстановитьСвойство(СтрокаРасшифровки, "Подразделение", СтрокаСекции.Подразделение, Истина, ЭтоНовыйДокумент);
	УстановитьСвойство(СтрокаРасшифровки, "Сумма", СтрокаСекции.СуммаДокумента, Истина, ЭтоНовыйДокумент);
	УстановитьСвойство(СтрокаРасшифровки, "СуммаВзаиморасчетов", 0, Истина, ЭтоНовыйДокумент);
	УстановитьСвойство(СтрокаРасшифровки, "ОснованиеПлатежа", ДанныеВыбора[0].Значение.Значение.ОснованиеПлатежа, Истина, ЭтоНовыйДокумент);
	УстановитьСвойство(СтрокаРасшифровки, "Заказ", ДанныеВыбора[0].Значение.Значение.Заказ, Истина, ЭтоНовыйДокумент);				
	УстановитьСвойство(СтрокаРасшифровки, "СтатьяДоходов", СтрокаСекции.СтатьяДоходовИлиРасходов, Истина, ЭтоНовыйДокумент);
	
	Возврат Истина;

КонецФункции

Процедура СнятьПризнакПроведенияБанком(ДокументОбъект)
	
	Попытка
		
		ДокументОбъект.ПроведеноБанком = Ложь;
		ДокументОбъект.ДатаПроведенияБанком = Неопределено;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Документ %1 отмечен как непроведенный банком'"),
			ДокументОбъект);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ДокументОбъект.Ссылка);
		
	Исключение
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Документ %1 не записан. Произошли ошибки при записи!'"),
			ДокументОбъект);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
		ОписаниеОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ОписаниеОшибки.Описание);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
