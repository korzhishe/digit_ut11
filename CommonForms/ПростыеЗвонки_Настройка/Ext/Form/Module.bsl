&НаКлиенте
Процедура ОбновитьНадписьСостояния(Статус = Неопределено)
	
	Если Статус = Неопределено Тогда
		Статус = ПростыеЗвонки_ОбщийМодуль_Клиент.ПолучитьСостояние();
	КонецЕсли;
	
	Если Статус = 1 Тогда
		
		Состояние = "Есть соединение";
		Элементы.Состояние.ЦветТекста = WebЦвета.Зеленый;
				
	Иначе
		
		Если Статус = 0 Тогда
			Состояние = "Нет соединения";
		ИначеЕсли Статус = 2 Тогда
			Состояние = "Лицензия истекла";
		ИначеЕсли Статус = 3 Тогда
			Состояние = "Превышен лимит клиентов";
		КонецЕсли;
		Элементы.Состояние.ЦветТекста = WebЦвета.Кирпичный;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПростыеЗвонки_ИзменилосьСостояние" Тогда
		
		ОбновитьНадписьСостояния(Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключиться(Команда)
	
	ПростыеЗвонки_ОбщийМодуль_Клиент.Подключиться();	
	
КонецПроцедуры

&НаКлиенте
Процедура Отключиться(Команда)
	
	ПростыеЗвонки_ОбщийМодуль_Клиент.Отключиться();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНомераПоследнихВерсий() Экспорт
	
	ИмяФайла = КаталогВременныхФайлов()+"CTI_version.xml";
	
	Попытка
		Соединение = Новый HTTPСоединение("prostiezvonki.ru");
		Соединение.Получить("version.xml", ИмяФайла);
	Исключение
		Возврат;
	КонецПопытки;
	
	Чтение = Новый ЧтениеXML;
	
	Попытка
		
		Чтение.ОткрытьФайл(ИмяФайла);
		Чтение.Прочитать();
		
	Исключение
		
		Возврат;
		
	КонецПопытки;
	
	ТаблицаРелизов = Новый ТаблицаЗначений;
	ТаблицаРелизов.Колонки.Добавить("Имя");
	ТаблицаРелизов.Колонки.Добавить("Версия");
	ТаблицаРелизов.Колонки.Добавить("Описание");
	ТаблицаРелизов.Колонки.Добавить("Ссылка");
	
	Пока Чтение.Прочитать() Цикл
		
		Если Чтение.КоличествоАтрибутов()>0 Тогда
			
			НоваяСтрока = ТаблицаРелизов.Добавить();
			
			Пока Чтение.ПрочитатьАтрибут() Цикл
				
				ИмяАтрибута = НРег(Чтение.Имя);
				
				Если ИмяАтрибута = "name" Тогда
				    НоваяСтрока.Имя = Чтение.Значение;
				ИначеЕсли ИмяАтрибута = "version" Тогда
					НоваяСтрока.Версия = Чтение.Значение;
				ИначеЕсли ИмяАтрибута = "description" Тогда
					НоваяСтрока.Описание = Чтение.Значение;
				ИначеЕсли ИмяАтрибута = "path" Тогда
					НоваяСтрока.Ссылка = Чтение.Значение;
				КонецЕсли;

			КонецЦикла;
			
			Если НоваяСтрока.Имя = "1C_UF" Тогда
				АктуальнаяВерсия1С = НоваяСтрока.Версия;
			ИначеЕсли НоваяСтрока.Имя = "CTIControl_1C" Тогда
				АктуальнаяВерсияActiveX = НоваяСтрока.Версия;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Сайт = "www.prostiezvonki.ru";
	ТехническаяПоддержка = "www.prostiezvonki.ru/support";
		
	ПолучитьНомераПоследнихВерсий();	

КонецПроцедуры

&НаКлиенте
Процедура СерверАТСПриИзменении(Элемент)
	
	ПростыеЗвонки_ОбщийМодуль_Сервер.УстановитьЗначениеНастройки("СерверАТС", СерверАТС);
	ПростыеЗвонкиПараметры["СерверАТС"] = СерверАТС;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПростыеЗвонки_ОбщийМодуль_Сервер.УстановитьЗначениеНастройки("Пароль", Пароль);
	ПростыеЗвонкиПараметры["Пароль"] = Пароль;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Статус = 0;
	Попытка
		Если НЕ ПростыеЗвонкиКомпонента = Неопределено Тогда
			Статус = ПростыеЗвонкиКомпонента.connectionState;
		КонецЕсли;
	Исключение
	КонецПопытки;
	
	ОбновитьНадписьСостояния(Статус);
	
	Версия1С = ПростыеЗвонки_ОбщийМодуль_Клиент.ПолучитьВерсиюМодуля();
	ВерсияActiveX = ПростыеЗвонки_ОбщийМодуль_Клиент.ПолучитьВерсиюКомпоненты();

	Если Версия1С = АктуальнаяВерсия1С Тогда
		Элементы.ГруппаВерсияМодуляАктуальная.Видимость = Ложь;
	Иначе
		Элементы.ВерсияМодуляУстановленаПоследняя.Видимость = Ложь;
	КонецЕсли;
	
	Если ВерсияActiveX = АктуальнаяВерсияActiveX Тогда
		Элементы.ГруппаВерсияКомпонентыАктуальная.Видимость = Ложь;
	Иначе
		Элементы.ВерсияКомпонентыУстановленаПоследняя.Видимость = Ложь;
	КонецЕсли;
	
	СерверАТС								= ПростыеЗвонкиПараметры.СерверАТС;
	Пароль									= ПростыеЗвонкиПараметры.Пароль;
	
	ПоказыватьОкноВходящегоВызова			= ПростыеЗвонкиПараметры.ПоказыватьОкноВходящегоВызова;
	ПоказыватьОкноИсходящегоВызова			= ПростыеЗвонкиПараметры.ПоказыватьОкноИсходящегоВызова;
	СоздаватьВходящееСобытиеЗвонок			= ПростыеЗвонкиПараметры.СоздаватьВходящееСобытиеЗвонок;
	СоздаватьИсходящееСобытиеЗвонок			= ПростыеЗвонкиПараметры.СоздаватьИсходящееСобытиеЗвонок;
	ПоказыватьСозданноеСобытиеЗвонок		= ПростыеЗвонкиПараметры.ПоказыватьСозданноеСобытиеЗвонок;
	СохранятьИсториюЗвонков					= ПростыеЗвонкиПараметры.СохранятьИсториюЗвонков;
	ИспользоватьАвтоматическуюПереадресацию	= ПростыеЗвонкиПараметры.ИспользоватьАвтоматическуюПереадресацию;
 	КоличествоПоследнихЦифрНомера			= ПростыеЗвонкиПараметры.КоличествоПоследнихЦифрНомера;

КонецПроцедуры

&НаКлиенте
Процедура СайтНажатие(Элемент, СтандартнаяОбработка)
	
	ЗапуститьПриложение("http://prostiezvonki.ru");
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТехническаяПоддержкаНажатие(Элемент, СтандартнаяОбработка)
	
	ЗапуститьПриложение("http://prostiezvonki.ru/support");
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры
 
&НаКлиенте
Процедура Версия1ССкачатьНажатие(Элемент)
	ЗапуститьПриложение("http://www.prostiezvonki.ru/download");
КонецПроцедуры

&НаКлиенте
Процедура ВерсияКомпонентыСкачатьНажатие(Элемент)
	ЗапуститьПриложение("http://www.prostiezvonki.ru/download");
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяНастройкаПриИзменении(Элемент)
	
	ПростыеЗвонки_ОбщийМодуль_Сервер.УстановитьЗначениеНастройки(Элемент.Имя,ЭтаФорма[Элемент.Имя]);
	ПростыеЗвонкиПараметры[Элемент.Имя]=ЭтаФорма[Элемент.Имя];
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительнаяНастройкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокВыбора = Новый СписокЗначений();
	
	Если Элемент.Имя = "ПоказыватьОкноВходящегоВызова" Тогда
		СписокВыбора.Добавить("Никогда");
		СписокВыбора.Добавить("При поступлении звонка");
		СписокВыбора.Добавить("По поднятию трубки");
		СписокВыбора.Добавить("По завершению разговора");
	ИначеЕсли Элемент.Имя = "ПоказыватьОкноИсходящегоВызова" 
		ИЛИ Элемент.Имя = "СоздаватьВходящееСобытиеЗвонок" 
		ИЛИ Элемент.Имя = "СоздаватьИсходящееСобытиеЗвонок" Тогда
		
		СписокВыбора.Добавить("Никогда");
		СписокВыбора.Добавить("При начале звонка");
		СписокВыбора.Добавить("По поднятию трубки");
		СписокВыбора.Добавить("По завершению разговора");
	ИначеЕсли Элемент.Имя = "ПоказыватьСозданноеСобытиеЗвонок" Тогда
  		СписокВыбора.Добавить("Не показывать");
		СписокВыбора.Добавить("Только входящие");
		СписокВыбора.Добавить("Только исходящие");
		СписокВыбора.Добавить("Входящие и исходящие");
	ИначеЕсли Элемент.Имя = "СохранятьИсториюЗвонков" Тогда
		СписокВыбора.Добавить("Никогда");
		СписокВыбора.Добавить("Только входящие");
		СписокВыбора.Добавить("Только исходящие");
		СписокВыбора.Добавить("Входящие и исходящие");
	КонецЕсли;
	
	ВыбранноеЗначение = ВыбратьИзСписка(СписокВыбора,Элемент);
	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		
		ЭтаФорма[Элемент.Имя]=ВыбранноеЗначение.Значение;	
		
		ПростыеЗвонки_ОбщийМодуль_Сервер.УстановитьЗначениеНастройки(Элемент.Имя,ЭтаФорма[Элемент.Имя]);
		ПростыеЗвонкиПараметры[Элемент.Имя]=ЭтаФорма[Элемент.Имя];

	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПоследнихЦифрНомераПриИзменении(Элемент)
	
	ПростыеЗвонки_ОбщийМодуль_Сервер.УстановитьЗначениеНастройки(Элемент.Имя,ЭтаФорма[Элемент.Имя]);
	ПростыеЗвонкиПараметры[Элемент.Имя]=ЭтаФорма[Элемент.Имя];
	
КонецПроцедуры
